10 DIM X(30),Y(30):GOSUB2000:CLS:PRINT"PLEASE WAIT..."
20 P=0:C=0:D=0:E=2:F=0
30 FOR N=1 TO 18:P=P+20
40 X(N)=INT(122+(95*COS(P/(180/3.142))))
50 Y(N)=INT(96-(95*SIN(P/(180/3.142))))
60 NEXT
70 PMODE3,1:SCREEN1,D:PCLS
80 IF F<2 THEN PCLSE
90 C=C+1:IF C=10 THEN 150
100 A=0:B=C
110 A=A+1:IF A=19 THEN 190
120 B=B+1:IF B=19 THEN B=1
130 LINE(X(A),Y(A))-(X(B),Y(B)),PSET
135 PLAY"T100O5BO4BO3BO2C"
140 GOTO110
150 C=0:E=E+1:IF E=5 THEN D=1
160 IF E=8 THEN E=2:D=0:C=0
170 F=F+1:IF F=4 THEN F=0
180 GOTO 70
190 FOR I=1 TO 10:PLAY"T100O2CDEFGABAGFEDC":NEXT:GOTO80
2000 CLS0:TT$="STITCHET PMODE3":TBY$="BY GRAHAM SMITH":TD$="MAGAZINE TYPE-IN"
2010 TV$="DRAGON USER JAN 1985":TC$=CHR$(159):GOSUB 9010
2030 GOSUB9930:GOTO9810
2500 RETURN

9010 TM=LEN(TT$):IF LEN(TBY$)>TM THEN TM=LEN(TBY$)
9015 IF LEN(TV$)>TM THEN TM=LEN(TV$)
9020 BH=7:BW=TM+2:BY=3:BX=(30-BW)/2+1:BC$=CHR$(143):GOSUB9510
9030 BH=9:BW=TM+4:BY=1:BX=(32-BW)/2:BC$=TC$:GOSUB9410
9040 ?@(BY+1)*32 + ((32-LEN(TT$))/2),TT$;
9050 ?@(BY+3)*32 + ((32-LEN(TBY$))/2),TBY$;
9060 ?@(BY+5)*32 + ((32-LEN(TD$))/2),TD$;
9070 ?@(BY+7)*32 + ((32-LEN(TV$))/2),TV$;
9100 RETURN

9410 BR$=STRING$(BW,BC$)
9430 ?@BX+((BY+1)*32),BR$;
9435 ?@BX+((BY+BH)*32),BR$;
9437 BH=BH-1:BY=BY+1:BW=BW-1
9440 FOR BQ=1TOBH:?@BX+((BY+BQ)*32),BC$;:?@BX+BW+((BY+BQ)*32),BC$;:NEXT
9499 RETURN

9510 BR$=STRING$(BW,BC$)
9530 FOR BQ=1TOBH:?@BX+((BY-1+BQ)*32),BR$;:NEXT
9599 RETURN

9610 PRINT@(32-LEN(T$))/2+32*TL,T$;
9620 RETURN

9710 FORDZ=1TO5000:NEXT:RETURN

9810 WA$=INKEY$:IF WA$="" THEN 9810
9820 RETURN

9930 BC$=CHR$(128):?@483,"press"+BC$+"any"+BC$+"key"+BC$+"to"+BC$+"continue";:GOSUB9810:RETURN
9999 REM
