10 ' PATTERNS (c) I.ap Gwynn, 10/83
20 ' ** A PROGRAM TO DEMONSTRATE SOME VARIATIONS ON PMODE AND PCOPY
25 GOSUB 2000
30 PCLS RND(5)-1
40 PMODE RND(5)-1,1:SCREEN 1,1
50 FOR X=2 TO 255 STEP RND(10)
60 COLOR RND(4),RND(4)
70 LINE(0,X)-(X,0),PSET
80 LINE(255,X)-(255-X,0),PSET
90 NEXT
100 FOR X=2 TO 255 STEP RND(10)
110 LINE(0,X)-(X,0),PRESET
120 LINE(255,X)-(255-X,0),PRESET
130 NEXT
140 REM SOUND RND(255),1
150 FOR I=1 TO 4
160 PCOPY I TO RND(4)
170 NEXT
180 GOTO 40

2000 CLS0:TT$="PATTERNS":TBY$="IOLO AP GWYNN":TD$="MAGAZINE TYPE-IN"
2010 TV$="DRAGON USER MARCH 1984":TC$=CHR$(159):GOSUB 9010
2020 GOSUB9930:GOTO9810
2500 RETURN

9010 TM=LEN(TT$):IF LEN(TBY$)>TM THEN TM=LEN(TBY$)
9015 IF LEN(TV$)>TM THEN TM=LEN(TV$)
9017 IF LEN(TD$)>TM THEN TM=LEN(TD$)
9020 BH=7:BW=TM+2:BY=3:BX=(30-BW)/2+1:BC$=CHR$(143):GOSUB9510
9030 BH=9:BW=TM+4:BY=1:BX=(32-BW)/2:BC$=TC$:GOSUB9410
9040 ?@(BY+1)*32 + ((32-LEN(TT$))/2),TT$;
9050 ?@(BY+3)*32 + ((32-LEN(TBY$))/2),TBY$;
9060 ?@(BY+5)*32 + ((32-LEN(TD$))/2),TD$;
9070 ?@(BY+7)*32 + ((32-LEN(TV$))/2),TV$;
9100 RETURN

9410 BR$=""
9420 BR$=STRING$(BW,BC$)
9430 ?@BX+((BY+1)*32),BR$;
9435 ?@BX+((BY+BH)*32),BR$;
9437 BH=BH-1:BY=BY+1:BW=BW-1
9440 FOR BQ=1TOBH:?@BX+((BY+BQ)*32),BC$;:?@BX+BW+((BY+BQ)*32),BC$;:NEXT
9499 RETURN

9510 BR$=""
9520 FOR BR=1TOBW:BR$=BR$+BC$:NEXT
9530 FOR BQ=1TOBH:?@BX+((BY-1+BQ)*32),BR$;:NEXT
9599 RETURN

9810 WA$=INKEY$:IF WA$="" THEN 9810
9820 RETURN

9930 BC$=CHR$(128):?@483,"press"+BC$+"any"+BC$+"key"+BC$+"to"+BC$+"continue";:GOSUB9810:RETURN
