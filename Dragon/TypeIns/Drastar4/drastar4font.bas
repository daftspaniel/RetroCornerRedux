10 REM *******************************
20 REM ***** DRASTAR FONT DEMO   ***** 
30 REM ***** BRIAN MORLEY  FEB84 ***** 
35 REM ***** DAVY MITCHELL JUN23 ***** 
40 REM *******************************
50 DIM LT$ (26),NU$ (9)
55 POKE 65495,0: GOSUB 1480 :GOSUB2000
90 REM SET UP SCREEN
100 PMODE4,1: COLOR 0,1:SCREEN1,1:PCLS:GOSUB430
320 GOTO 320
430 DRAW"BM4,10"
440 A$="ABCDEFGHIJKLMNOPQRSTUVWXYZ":GOSUB 1260 
450 DRAW"BM4,50"
460 A$="01234567890":GOSUB 1310
470 DRAW"BM4,100"
480 A$="A@HANDY@FONT@FOR@HIGH@RES":GOSUB 1260
490 RETURN
1260 REM DRAW LETTERS 
1270 DRAW"S4C0"
1280 FOR X=1 TO LEN(A$)
1290 DRAW LT$(ASC(MID$(A$, X, 1))-64 )+"BR3"
1300 NEXT: DRAW"C1": RETURN 
1310 REM DRAW NUMBERS
1320 DRAW"S4C0"
1330 FOR X=1 TO LEN (A$) 
1340 DRAW NU$(VAL(MID$(A$,X,1)))+"BR3"
1350 NEXT X: DRAW"C1": RETURN 
1480 REM SET UP ARRAYS 
1490 FOR X=0 TO 26:READ LT$ (X): NEXT
1500 FOR X=0 TO 9: READ NU$ (X): NEXT
1510 RETURN
1520 'DATA @ TO Z
1530 DATA BR4
1540 DATA D4RU2R3ND2U2NL3
1550 DATA D4RNU2R2U2L2R1U2NL2BR
1560 DATA D4RNUR2UBU2UL3R3
1570 DATA D4RNU2REU2HNL2BR
1580 DATA D4RNUR2BU2BLL2U2R3
1590 DATA D4RU2RBU2L2R3
1600 DATA D4R2NURU2LBU2L2R3
1610 DATA D4RU2R2D2U4 
1620 DATA BDD3RU4
1630 DATA BD3DR2NURU4
1640 DATA D4U2R3D2U2L2E2
1650 DATA ND4RDBD3R2BU4
1660 DATA D4RU2BU2RND2R2D4BU4
1670 DATA D4RU1BU3LF3DU4
1680 DATA NR3D4RBU2U2BD4R2U4
1690 DATA D4RU2R2U2L3R3
1700 DATA NR3D4R2NURU4
1710 DATA NR3D4RU2R3ND2LU2NL3BR
1720 DATA NR3DNRDR3D2L3BR3BU4
1730 DATA BD4BR2NU4RUBU3L3R4
1740 DATA D4RNU2R2U4
1750 DATA ND4RD2BD2E4
1760 DATA D4RNU2RNU4R2U4 
1770 DATA NDF4LBL3E4
1780 DATA D2R3D2U2LU2RND2
1790 DATA NDR4G4RNUR3BU4
1800 REM DATA O TO 9
1810 DATA D4R4U4L3ND2R3
1820 DATA BR2D4RU2BU2BR 
1830 DATA R3D2RU2D2L4D2R4BU4
1840 DATA R4D2L2RD2L3R4U4
1850 DATA D3RNU3R2NDRNDRBU3
1860 DATA NR4D2RNU2R3D2L4BR4BU4
1870 DATA ND4RD2R3D2L4BR4BU4
1880 DATA DRUR3D4LU2RU2
1890 DATA D4R4U2NL4U2L4RD2R2D2RBU4
1900 DATA R4D4LU2L3U2R4

2000 CLS0:TT$="HIGH-RES FONT DEMO":TBY$="FONT BY B MORLEY":TD$="FONT DEMO JUNE"
2010 TV$="2023 DAVY MITCHELL":TC$=CHR$(175):GOSUB 9010
2020 GOSUB9930:GOTO9810
2500 RETURN

9010 TM=LEN(TT$):IF LEN(TBY$)>TM THEN TM=LEN(TBY$)
9015 IF LEN(TV$)>TM THEN TM=LEN(TV$)
9017 IF LEN(TD$)>TM THEN TM=LEN(TD$)
9020 BH=7:BW=TM+2:BY=3:BX=(30-BW)/2+1:BC$=CHR$(143):GOSUB9510
9030 BH=9:BW=TM+4:BY=1:BX=(32-BW)/2:BC$=TC$:GOSUB9410
9040 ?@(BY+1)*32 + ((32-LEN(TT$))/2),TT$;
9050 ?@(BY+3)*32 + ((32-LEN(TBY$))/2),TBY$;
9060 ?@(BY+5)*32 + ((32-LEN(TD$))/2),TD$;
9070 ?@(BY+7)*32 + ((32-LEN(TV$))/2),TV$;
9100 RETURN

9410 BR$=""
9420 BR$=STRING$(BW,BC$)
9430 ?@BX+((BY+1)*32),BR$;
9435 ?@BX+((BY+BH)*32),BR$;
9437 BH=BH-1:BY=BY+1:BW=BW-1
9440 FOR BQ=1TOBH:?@BX+((BY+BQ)*32),BC$;:?@BX+BW+((BY+BQ)*32),BC$;:NEXT
9499 RETURN

9510 BR$=""
9520 FOR BR=1TOBW:BR$=BR$+BC$:NEXT
9530 FOR BQ=1TOBH:?@BX+((BY-1+BQ)*32),BR$;:NEXT
9599 RETURN

9810 WA$=INKEY$:IF WA$="" THEN 9810
9820 RETURN

9930 BC$=CHR$(128):?@483,"press"+BC$+"any"+BC$+"key"+BC$+"to"+BC$+"continue";:GOSUB9810:RETURN
