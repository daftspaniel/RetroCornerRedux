1 DIMX,Y,R,F
2 GOSUB2000

10 REM *********
20 REM * WEAVE *
30 REM *********
40 PMODE 3,1:R=10
50 PCLS:SCREEN1,1
60 F=5+RND(3)-1:COLORF,5
70 FOR X=1 TO 240 STEP R
75 GOSUB 150
80 FOR Y=1 TO 170 STEP R
90 CIRCLE(X,Y),R,,1,.3,.8
96 IFRND(6)=1 THEN GOSUB 150
100 NEXT Y,X
120 GOSUB9710:R=3+RND(8)
140 GOTO 50
150 F=5+RND(3):COLORF,5:RETURN

2000 CLS0:TT$="WEAVE":TBY$="DR TIM LANGDELL":TD$="BOOK TYPE-IN"
2010 TV$="35 PROGRAMS FOR DRAGON 32":TC$=CHR$(159):GOSUB 9010
2020 TL=13:T$="ENHANCED VERSION 2023":GOSUB9610
2030 GOSUB9930:GOTO9810
2500 RETURN

9010 TM=LEN(TT$):IF LEN(TBY$)>TM THEN TM=LEN(TBY$)
9015 IF LEN(TV$)>TM THEN TM=LEN(TV$)
9020 BH=7:BW=TM+2:BY=3:BX=(30-BW)/2+1:BC$=CHR$(143):GOSUB9510
9030 BH=9:BW=TM+4:BY=1:BX=(32-BW)/2:BC$=TC$:GOSUB9410
9040 ?@(BY+1)*32 + ((32-LEN(TT$))/2),TT$;
9050 ?@(BY+3)*32 + ((32-LEN(TBY$))/2),TBY$;
9060 ?@(BY+5)*32 + ((32-LEN(TD$))/2),TD$;
9070 ?@(BY+7)*32 + ((32-LEN(TV$))/2),TV$;
9100 RETURN

9410 BR$=STRING$(BW,BC$)
9430 ?@BX+((BY+1)*32),BR$;
9435 ?@BX+((BY+BH)*32),BR$;
9437 BH=BH-1:BY=BY+1:BW=BW-1
9440 FOR BQ=1TOBH:?@BX+((BY+BQ)*32),BC$;:?@BX+BW+((BY+BQ)*32),BC$;:NEXT
9499 RETURN

9510 BR$=STRING$(BW,BC$)
9530 FOR BQ=1TOBH:?@BX+((BY-1+BQ)*32),BR$;:NEXT
9599 RETURN

9610 PRINT@(32-LEN(T$))/2+32*TL,T$;
9620 RETURN

9710 FORDZ=1TO5000:NEXT:RETURN

9810 WA$=INKEY$:IF WA$="" THEN 9810
9820 RETURN

9930 BC$=CHR$(128):?@483,"press"+BC$+"any"+BC$+"key"+BC$+"to"+BC$+"continue";:GOSUB9810:RETURN


