10 DIM B$,X,Y,T,H,A,B,N,S
20 B$=CHR$(128):P$=CHR$(159):R$=CHR$(191):S$=CHR$(171)+CHR$(163)+CHR$(254)
30 CLS0
40 TT$="GIFT DROP":TBY$="RAINBOW BOOK OF BASIC":TD$="PROGRAMS W.H.SMITH 1984":TV$="MERRRY CHRISTMAS!":TC$=CHR$(175):GOSUB 9010
50 GOSUB 9920

100 REM XMAS
110 GOSUB1100:H=0
120 TL=0:T$="gift"+B$+"drop":GOSUB9600
121 ?@64, "HELP SANTA DELIVER PRESENTS"
122 ? "PRESS . TO DROP A PRESENT"
123 ? "AIM FOR THE CHIMNEY"
124 GOSUB 9920
130 T=2
140 H=0:FORS=1TO10:CLS0: FORSL=1TO10
145 SET(RND(63),RND(24),5):NEXT:?@0,S;
150 X=0:Y=14:GOSUB1700:FORN=1TO32:?R$;:NEXT
160 N=20:GOSUB1300:R=R+4
170 X=R:FORY=12TO14:GOSUB1700:?R$+B$+R$;:NEXTY
180 A=0:B=1:P=0
190 T=.2:GOSUB1400:A=A+1:X=A:Y=B:GOSUB1700:?B$+S$;
200 IFX=27THEN310
210 IFP=1THEN250
220 GOSUB1600:IFK$<>"."THEN240
230 P=1:C=A:D=B:N=1
240 GOTO 190
250 C=C+1:D=D+N:N=N+1:X=C:Y=D-1:SOUND120+Y*2,1:GOSUB1700:?P$;
260 IF (X=R OR X=R+3) AND Y>11 THEN 310
270 IF Y<14THEN190
280 IF X=R+1 OR X=R+2 THEN 300
290 GOTO 310
300 X=8:Y=5:GOSUB1700:?@266,"bullseye";:H=H+1:SOUND205,1:SOUND185,1:GOTO320
310 X=6:Y=5:GOSUB1700:?@269,"missed";:SOUND100,1
320 T=2:GOSUB1400:NEXT S
330 T=2:GOSUB1400:GOSUB2800
340 ?:?:?"YOU GOT ";STR$(H);" PRESENTS"
350 ?:?:?"OUT OF A POSSIBLE 10":?:?
360 IF H>4 THEN 380
370 ?"NOT MUCH FOR XMAS":?:?"   THIS YEAR!":GOTO390
380 ?"APPLY FOR A JOB AT":?:?"THE NORTH POLE!"
390 GOSUB 9920: GOTO110

1000 REM DRAGON SUBROUTINES 
1100 W=32: D=16: F=8:GOSUB 1200: RETURN 
1200 CLS0: ? @ 256,"";: RETURN 
1300 R=RND(N): RETURN 
1400 FOR Z=1 TO 650*T: NEXT Z:RETURN 
1500 G$=INKEY$: IF G$="" THEN 1500 
1510 RETURN 
1600 K$=INKEY$: RETURN 
1700 ? @32*Y+X, "";: RETURN 
1800 ? TAB(X); : RETURN 
1900 ? CHR$(94);: RETURN 
2000 ? CHR$(9); : RETURN 
2100 INPUT K: RETURN 
2200 INPUT K$: RETURN 
2300 CLS0: RETURN 
2400 SOUND Z, 1: RETURN 
2500 R$=CHR$(R+64): RETURN 
2600 P$=MID$(K$, P, 1): RETURN 
2700 GOSUB 2600: A=ASC(P$) : RETURN 
2800 CLS0: RETURN 
2900 DIM V$(26): RETURN 
3000 DIM W$(60): RETURN 
3100 GG=ASC(G$): RETURN

5999 REM ------LIBRARY LIVES HERE------

9000 REM TITLE SCREEN
9001 REM TT$ = PROGRAM NAME
9002 REM TBY$ = AUTHOR
9003 REM TD$ = DATE
9004 REM TV$ = VERSION
9010 TM=LEN(TT$):IF LEN(TBY$)>TM THEN TM=LEN(TBY$)
9015 IF LEN(TV$)>TM THEN TM=LEN(TV$)
9017 IF LEN(TD$)>TM THEN TM=LEN(TD$)
9020 BH=7:BW=TM+2:BY=3:BX=(30-BW)/2+1:BC$=CHR$(143):GOSUB9510
9030 BH=9:BW=TM+4:BY=1:BX=(32-BW)/2:BC$=TC$:GOSUB9410
9040 ?@(BY+1)*32 + ((32-LEN(TT$))/2),TT$;
9050 ?@(BY+3)*32 + ((32-LEN(TBY$))/2),TBY$;
9060 ?@(BY+5)*32 + ((32-LEN(TD$))/2),TD$;
9070 ?@(BY+7)*32 + ((32-LEN(TV$))/2),TV$;
9100 RETURN

9200 REM VERTICAL LINE
9201 REM LC$ = CHARACTER FOR LINE
9202 REM LX,LY = POSITION 
9203 REM LL = LENGTH
9210 LZ=LY+LL-1
9220 FOR BR=LY TO LZ:?@LX+(BR*32),LC$;:NEXT
9240 RETURN

9300 REM HORIZONTAL LINE
9301 REM LC$ = CHARACTER FOR LINE
9302 REM LX,LY = POSITION 
9304 REM LL = LENGTH
9310 LO$=""
9320 REM MC10: FOR BR=1TOLL:LO$=LO$+LC$:NEXT
9320 REM COCO/DRAGON: LO$=STRING$(LL,LC$)
9320 LO$=STRING$(LL,LC$)
9330 ?@LX+(LY*32),LO$;
9340 RETURN

9400 REM BOX OUTLINE
9403 REM BX,BY = BOX POSITION
9404 REM BH,BW = BOX HEIGHT & WIDTH
9405 REM BC$ = CHARACTER FOR BOX EDGE
9410 REM MC10: BR$="":FOR BR=1TOBW:BR$=BR$+BC$:NEXT
9410 REM COCO/DRAGON: BR$=STRING$(BW,BC$)
9410 BR$=STRING$(BW,BC$)
9420 ?@BX+((BY+1)*32),BR$;
9430 ?@BX+((BY+BH)*32),BR$;
9440 BH=BH-1:BY=BY+1:BW=BW-1
9450 FOR BQ=1TOBH:?@BX+((BY+BQ)*32),BC$;:?@BX+BW+((BY+BQ)*32),BC$;:NEXT
9460 RETURN

9500 REM FILLED BOX
9503 REM BX,BY = BOX POSITION
9504 REM BH,BW = BOX HEIGHT & WIDTH
9505 REM BC$ = CHARACTER FOR BOX
9510 REM MC10: BR$="":FOR BR=1TOBW:BR$=BR$+BC$:NEXT
9510 REM COCO/DRAGON: BR$=STRING$(BW,BC$)
9510 BR$=STRING$(BW,BC$)
9530 FOR BQ=1TOBH:?@BX+((BY-1+BQ)*32),BR$;:NEXT
9540 RETURN

9600 REM CENTER TEXT. TL = vertical line number.
9610 PRINT@(32-LEN(T$))/2+32*TL,T$;
9620 RETURN

9700 REM DELAY
9710 FORDZ=1TO1000:NEXT:RETURN

9740 REM WAIT FOR SPACEBAR
9750 BC$=CHR$(128):?@483,"press"+BC$+"space"+BC$+"bar"+BC$+"to"+BC$+"continue";:
9760 WA$=INKEY$:IFWA$<>" "THEN9760 ELSE RETURN

9800 REM WAIT FOR ANY KEY
9810 WA$=INKEY$:IF WA$="" THEN 9810
9820 RETURN

9900 REM ANY KEY PROMPT AT BOTTOM OF SCREEN.
9910 ?@483,"PRESS ANY KEY TO CONTINUE";:GOSUB9810:RETURN

9920 REM INVERTED ANY KEY PROMPT AT BOTTOM OF SCREEN.
9930 BC$=CHR$(128):?@483,"press"+BC$+"any"+BC$+"key"+BC$+"to"+BC$+"continue";:GOSUB9810:RETURN

9950 REM LOWER CASE TEXT
9960 TL=LEN(T$):O$=""
9970 FORBR=1TOTL:C$=MID$(T$,BR,1):BH=ASC(C$):IF BH<91 AND BH>64 THEN C$=CHR$(BH+32)
9971 IFBH=32THENC$=CHR$(128)
9975 O$=O$+C$
9979 NEXT
9980 T$=O$
9999 RETURN