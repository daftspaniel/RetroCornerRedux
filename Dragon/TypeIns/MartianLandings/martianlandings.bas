1 DIMP(20,20),Z,X,A$,T,E,FUEL
2 GOSUB 2000
10 REM*************************
20 REM***MARTIAN LANDINGS******
30 REM***BY C. GARRINGTON******
50 REM****17/8/83**************
60 GOSUB 960
70 CLS:PRINT@0,"      MARTIAN LANDINGS"
80 PRINT@224,"": INPUT"DO YOU NEED INSTRUCTIONS";H$
90 IF LEFT$(H$,1)="Y" THEN GOSUB 850
100 CLS:PRINT@224,"": INPUT"DO YOU WANT A FAST OR SLOW GAME"; E$
110 IF LEFT$(E$,1)="F"THEN E=50
120 IF LEFT$(E$,1)="S"THEN E=100
130 Z=200:X=10:T=50:V=50:FUEL=75:SHIP=3:SCOR=0
140 PMODE 3,1:PCLS: DRAW"BM0,170;E10;F5;R15;U2;C2;L20;C4;D2;R20F4;E15;F10;R30U2C2;L30C4D2R30;U10;E5;F10;E15;F6E10R20U2C2L20C4D2R30;U2C2L30C4D2R30;E12F15;E3U7E5F8E10F19E4U6R10E4F7E10"
150 CIRCLE(50,50),30,4
160 PAINT(50,51),2,4
170 PAINT(0,190),3,4
175 LINE(0,0)-(150,1),PSET,BF
180 SCREEN1,0
190 PUT(Z,X)-(Z+20,X+20),P,PSET
200 A$=INKEY$
210 REM***RANDOM DECENT********
220 Z=Z-(RND(4)-1):X=X+(RND(4)-1)
230 FORT=1 TO E: NEXT T
240 A$=INKEY$
250 REM***MOVEMENT OF POD******
260 IF A$=CHR$(9) THEN GOSUB 350
270 IF A$=CHR$(94) THEN GOSUB 420
280 IF PPOINT(Z,X+20)>2 THEN GOSUB 490
290 IF PPOINT(Z+20,X+20)>2 THEN GOSUB 490
300 IF PPOINT(Z,X+20)=2 THEN GOSUB 620
310 IF PPOINT(Z+20,X+20)=2 THEN GOSUB 620
320 IF PPOINT(Z,X)>2 THEN GOSUB 490
330 IF PPOINT(Z+20,X)>2 THEN GOSUB 490
340 GOTO 190
350 REM*****MOVE POD**********
360 Z=Z+4
370 FUEL=FUEL-1:IFFUEL>-1THENLINE(150,0)-(FUEL*2,1),PRESET,BF
380 IF FUEL<5 THEN 740
390 REM*****END OF ROUTINE****
400 PLAY"O1;T250;AAAAAAAAA"
410 RETURN
420 REM***MOVE POD************
430 Z=Z-1:X=X-3
440 FUEL=FUEL-1:IF FUEL<0 THEN 740 ELSE LINE(150,0)-(FUEL*2,1),PRESET,BF
445 IF FUEL<5 THEN 740
450 PLAY"O2;T250;AAAAAAAAA" 
460 RETURN
470 REM****END OF ROUTINE****** 
480 REM*****CRASH POD ROUTINE**
490 PUT(Z,X)-(Z+20,X+20),P,PSET
492 PLAY "T250;O5AAAAAAAAAAAAA04AAAAAAAAAAAAAAO3AAAAAAAAAAAAAAAAO2AAAAAAAAAAAAAAAO1AAAAAAAAAAAAAAA"
495 SCREEN1,1
500 FORQ=1 TO 30
510 CIRCLE(Z+10,X+10),Q,4
520 NEXT
530 FORQ=1 TO 30
540 CIRCLE(Z+10,X+10),Q,1
550 NEXT
555 SCREEN1,0
560 SHIP=SHIP-1: IF SHIP=0 THEN 680
570 FOR T=1 TO 500: NEXT
580 Z=200:X=0:S=S+1
590 RETURN
600 REM****END OF ROUTINE****
610 REM*******LAND POD ROUTINE*
620 PUT(Z,X)-(Z+20,X+20),P,PSET
625 PLAY"T250;O1AGAGAGAGAGGGGAAGAGAGAGO2AGAGAGAGAGAGGAGAGAGAGO3AGAGGGGGAAGAGAO4A"
630 SCOR=SCOR+1
640 Z=200: X=10:FOR T=1 TO 1500: NEXT
650 SHIP=SHIP-1:IF SHIP=0 THEN 680
660 GOTO 190
670 REM***END OF ROUTINE*******
680 CLS:PRINT
690 PRINT"YOU MANAGED TO LAND, "SCOR" OUT OF YOUR 3 SHIPS"
700 PRINT"YOU HAVE "FUEL" UNITS OF FUEL LEFT"
710 FORT=1 TO 1000:NEXT T
720 PRINT@448,"": INPUT"ANOTHER GAME (Y/N)"; F$
730 IF F$="N" THEN END ELSE 70
740 PLAY "T250;O5AAAAAAAAAAAAAAO1AAAAAAAAAAAAAAAO5AAAAAAAAAAAAAAO1AAAAAAAAAAAAAAA"
750 IF FUEL<0 THEN 760 ELSE RETURN
760 SHIP=SHIP-1:IF SHIP THEN 680
765 SCREEN1,1
770 FORQ=1 TO 30
780 CIRCLE(Z+10,X+10),Q,4
790 NEXT Q
800 FORQ=1 TO 30
810 CIRCLE(Z+10,X+10),Q,1
820 NEXT Q
825 SCREEN1,0
830 Z=200:X=10
840 RETURN
850 CLS
860 PRINT"THE OBJECT OF THE GAME IS TO    LAND THREE SUPPLY PODS ONTO  THEMARTIAN LANDSCAPE."
862 PRINT"THERE ARE 3  LANDING PADS ON THESURFACE. YOU START THE GAME WITH 75 UNITS OF FUEL. ONE IS USED  WITH EACH RETRO THRUST."
870 PRINT:PRINT"THE THRUSTS ARE CONTROLLED WITH THE 'UP' AND 'RIGHT' ARROW KEYS"
880 PRINT"THE PODS USE NUCLEAR FUEL AND   TOO ROUGH A LANDING WILL CAUSE  IT TO EXPLODE."
890 PRINT@480, "PRESS A KEY TO CONTINUE";
900 IF INKEY$=""THEN 900
910 CLS:PRINT"IF YOUR FUEL LEVELS GET"
911 PRINT"DANGEROUSLY LOW, YOU WILL       RECEIVE A WARNING SIGNAL. THIS  MEANS YOU ONLY HAVE A VERY SMALL AMOUNT OF FUEL LEFT.":PRINT
912 PRINT"IF THE FUEL SUPPLY IS OVER-USED,IT WILL CAUSE A NUCLEAR REACTIONWHICH WILL DESTROY YOUR POD."
930 PRINT@480,"PRESS A KEY TO START";
940 IF INKEY$=""THEN 940 ELSE RETURN
950 GOTO 950
960 PMODE 3,1:PCLS
980 CIRCLE(11,13),5,2
990 DRAW"BM8,15;G2;BM12,15;F2"
1000 GET(0,0)-(20,20),P,G
1010 PCLS:RETURN
1120 REM
2000 CLS0:TT$="MARTIAN LANDINGS":TBY$="BY C. GARRINGTON":TD$="MAGAZINE TYPE-IN"
2010 TV$="DRAGON USER JAN 1984":TC$=CHR$(159):GOSUB 9010
2030 GOSUB9930:GOTO9810
2500 RETURN

9010 TM=LEN(TT$):IF LEN(TBY$)>TM THEN TM=LEN(TBY$)
9015 IF LEN(TV$)>TM THEN TM=LEN(TV$)
9020 BH=7:BW=TM+2:BY=3:BX=(30-BW)/2+1:BC$=CHR$(143):GOSUB9510
9030 BH=9:BW=TM+4:BY=1:BX=(32-BW)/2:BC$=TC$:GOSUB9410
9040 ?@(BY+1)*32 + ((32-LEN(TT$))/2),TT$;
9050 ?@(BY+3)*32 + ((32-LEN(TBY$))/2),TBY$;
9060 ?@(BY+5)*32 + ((32-LEN(TD$))/2),TD$;
9070 ?@(BY+7)*32 + ((32-LEN(TV$))/2),TV$;
9100 RETURN

9410 BR$=STRING$(BW,BC$)
9430 ?@BX+((BY+1)*32),BR$;
9435 ?@BX+((BY+BH)*32),BR$;
9437 BH=BH-1:BY=BY+1:BW=BW-1
9440 FOR BQ=1TOBH:?@BX+((BY+BQ)*32),BC$;:?@BX+BW+((BY+BQ)*32),BC$;:NEXT
9499 RETURN

9510 BR$=STRING$(BW,BC$)
9530 FOR BQ=1TOBH:?@BX+((BY-1+BQ)*32),BR$;:NEXT
9599 RETURN

9610 PRINT@(32-LEN(T$))/2+32*TL,T$;
9620 RETURN

9710 FORDZ=1TO5000:NEXT:RETURN

9810 WA$=INKEY$:IF WA$="" THEN 9810
9820 RETURN

9930 BC$=CHR$(128):?@483,"press"+BC$+"any"+BC$+"key"+BC$+"to"+BC$+"continue";:GOSUB9810:RETURN
9999 REM