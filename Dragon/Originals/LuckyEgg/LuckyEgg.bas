10 DIM AL(1),B(1),E(1),X(16),P,PP,E,EY,EO,V,F
20 FORI=0TO15:X(I)=I*16:NEXT
30 Y=166:YY=Y+15
40 REM SET UP GRAPHICS
50 PMODE0,1:PCLS:SCREEN1,1
60 GET(0,0)-(15,15),B,G
70 FOR L=0TO7:READ D:POKE1536+(L*16),D:NEXT
80 GET(0,0)-(15,15),AL,G
90 FOR L=0TO7:READ D:POKE1536+(L*16),D:NEXT
100 GET(0,0)-(15,15),E,G
110 PCLS
120 REM GAME INIT
130 P=7:PP=10:F=8:E=X(F):V=5
140 LINE(31,185)-(255,185),PSET
150 LINE(31,0)-(31,195),PSET
160 REM GAME LOOP
170 A$=INKEY$:IF A$="P"THENP=P+1 ELSE IF A$="Q"THENP=P-1
180 IF P<3THENP=3
190 IF P>15THENP=15
200 IF PP<>P THEN PUT(X(P),Y)-(X(P)+15,YY),AL,PSET:PUT(X(PP),Y)-(X(PP)+15,YY),B,PSET:PP=P
210 PUT(E,EY)-(E+15,EY+15),E,PSET:PUT(E,EO)-(E+15,EO+V),B,PSET
220 EO=EY:EY=EY+V:IF EY>160 THEN IF P<>F GOSUB240 ELSE GOSUB310
230 GOTO 170
240 REM EGG MISS
250 PUT(E,180)-(E+15,EY+15),E,OR
260 EY=EY-V:PUT(E,EY)-(E+15,EY+15),B,PSET
270 SCREEN1,0:SOUND100,2:SCREEN1,1
280 PUT(X(P),Y)-(X(P)+15,YY),AL,PSET
290 GOSUB350
300 RETURN
310 REM EGG CATCH
311 PLAY"T200ABACDEFGG"
312 PUT(E,EY)-(E+15,EY+15),B,PSET
320 GOSUB350
330 RETURN
340 REM RESET EGG
350 EY=0:F=2+RND(13):E=X(F):V=V+0.25:IFV>15THENV=8
360 RETURN
370 REM STICK MAN
380 DATA 24,60,24,126,24,24,36,66
390 REM EGG
400 DATA 0,32,112,112,248,248,112,32


