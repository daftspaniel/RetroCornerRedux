10 DIMAL(1),B(1),E(1),S(1),X(16),P,PP,E,EY,EO,V,F,M,SC,HS
20 FORI=0TO15:X(I)=I*16:NEXT
30 Y=166:YY=Y+15
40 REM SET UP GRAPHICS
50 PMODE0,1:PCLS
60 GET(0,0)-(15,15),B
70 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
80 GET(0,0)-(15,15),AL
90 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
100 GET(0,0)-(15,15),E
110 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
120 GET(0,0)-(15,15),S
130 PCLS
140 GOSUB510
150 REM GAME INIT
160 P=7:PP=10:F=8:E=X(F):V=5:M=0:SC=0:EY=0
170 LINE(2,2)-(2,17),PSET:LINE(2,17)-(10,17),PSET
180 LINE(15,2)-(15,17),PSET:LINE(15,17)-(25,17),PSET:LINE(15,2)-(25,2),PSET:LINE(15,9)-(22,9),PSET
190 LINE(31,185)-(255,185),PSET
200 LINE(31,0)-(31,195),PSET
210 FORN=1TO6:PUT(10,10+20*N)-(25,25+20*N),E,PSET:NEXT
220 REM GAME LOOP
230 A$=INKEY$:IFA$="P"THENP=P+1ELSEIFA$="Q"THENP=P-1
240 J=JOYSTK(0):IFJ=0THENP=P-1ELSEIFJ=63THENP=P+1
250 IFP<2THENP=15
260 IFP>15THENP=2
270 IF PP<>P THEN PUT(X(P),Y)-(X(P)+15,YY),AL,PSET:PUT(X(PP),Y)-(X(PP)+15,YY),B,PSET:PP=P
280 PUT(E,EY)-(E+15,EY+15),E,PSET:PUT(E,EO)-(E+15,EO+V),B,PSET
290 EO=EY:EY=EY+V:IF EY>160 THEN IF P<>F THEN GOSUB310ELSE GOSUB390
300 IF M<6 THEN230ELSE630
310 REM EGG MISS
320 EY=EY-V:PUT(E,169)-(E+15,184),S,PSET:PUT(E,153)-(E+15,168),B,PSET
330 SCREEN1,0:PLAY"T200O3GGFFEE":SCREEN1,1
340 GOSUB450:GOSUB490
350 M=M+1:PUT(8,10+20*M)-(23,25+20*M),S,PSET
360 IF M=6 THENPCLS:SCREEN1,0:FORG=1TO12:E=X(RND(16)):EY=RND(160):PUT(E,EY)-(E+15,EY+15),E,PSET:PLAY"T255BADEGG":NEXT
370 PUT(X(P),Y)-(X(P)+15,YY),AL,PSET:LINE(31,185)-(255,185),PSET
380 RETURN
390 REM EGG CATCH
400 PLAY"T240ABACDEFGG":SC=SC+1
410 PUT(E,EY)-(E+15,EY+15),B,PSET
420 PUT(X(P),Y)-(X(P)+15,YY),AL,PSET:LINE(31,185)-(255,185),PSET
430 GOSUB450:GOSUB490
440 RETURN
450 REM REDRAW STICK MAN
460 PUT(X(P),Y)-(X(P)+15,YY),AL,PSET:PUT(X(P),Y-15)-(X(P)+15,Y),B,PSET
470 RETURN
480 REM RESET EGG
490 EY=0:F=1+RND(13):E=X(F):V=V+0.45:IFV>15THENV=8
500 RETURN
510 REM TITLE SCREEN
520 CLS0:?@27,"v0.1";:?@0,"2024";
530 TL=7:T$="lucky"+CHR$(128)+"egg":GOSUB880
540 TL=8:T$="by"+CHR$(128)+"davy"+CHR$(128)+"mitchell":GOSUB880
550 GOSUB750
560 TL=9:T$="high"+CHR$(128)+"score"+CHR$(128)+STR$(HS):GOSUB880
570 TL=11:T$=" CATCH THE FALLING EGGS! ":GOSUB880
580 TL=12:T$=" CONTROLS LEFT q RIGHT p ":GOSUB880
590 TL=13:T$=" 6 MISSES IS GAME OVER!  ":GOSUB880
600 GOSUB980:ZZ=RND(-TIMER)
610 SCREEN1,1:PCLS
620 GOTO160
630 REM SCORE SCREEN
640 CLS0
650 TL=7:T$="lucky"+CHR$(128)+"egg":GOSUB880:
660 TL=10:T$="score"+CHR$(128)+STR$(SC):GOSUB880:PLAY"T200ABACDEFGG"
670 GOSUB750:PLAY"T200EEEEEEGGGGGGGGGGGGG"
680 IFSC<10THENT$="a"+CHR$(128)+"rotten"+CHR$(128)+"result"
690 IFSC>9 AND SC<20 THEN T$="you"+CHR$(128)+"are"+CHR$(128)+"a"+CHR$(128)+"good"+CHR$(128)+"egg"
700 IFSC>19 THEN T$="EGG-cellent"+CHR$(128)+"score"
710 TL=12:GOSUB880
720 GOSUB920:GOSUB980
730 IF SC>HS THEN HS=SC
740 GOTO520
750 REM DRAW EGG
760 PRINT@32+15,CHR$(207)CHR$(207);
770 PRINT@64+14,CHR$(207)CHR$(207)CHR$(207)CHR$(207);
780 PRINT@96+13,CHR$(207)CHR$(207)CHR$(207)CHR$(207)CHR$(207)CHR$(207);
790 PRINT@128+13,CHR$(207)CHR$(207)CHR$(207)CHR$(207)CHR$(207)CHR$(207);
800 PRINT@160+14,CHR$(207)CHR$(207)CHR$(207)CHR$(207);
810 RETURN
820 REM STICK MAN
830 DATA24,60,24,126,24,24,36,66
840 REM EGG
850 DATA0,32,112,112,248,248,112,32
860 REM BROKEN EGG
870 DATA0,0,0,0,66,102,102,60
880 REM CENTER TEXT. TL = vertical line number.
890 PRINT@(32-LEN(T$))/2+32*TL,T$;
900 RETURN
910 REM DELAY
920 FORDZ=1TO1000:NEXT:RETURN
930 REM WAIT FOR SPACEBAR
940 WA$=INKEY$:IFWA$<>" "THEN940
950 RETURN
960 REM ANY KEY PROMPT AT BOTTOM OF SCREEN.
970 ?@483,"PRESS ANY KEY TO CONTINUE";:GOSUB940:RETURN
980 REM INVERTED SPACEBAR PROMPT AT BOTTOM OF SCREEN.
990 BC$=CHR$(128):?@483,"press"+BC$+"space"+BC$+"bar"+BC$+"to"+BC$+"continue";:GOSUB940:RETURN
1000 REM LOWER CASE TEXT
1010 TL=LEN(T$):O$=""
1020 FORBR=1TOTL:C$=MID$(T$,BR,1):BH=ASC(C$):IFBH<91ANDBH>64THENC$=CHR$(BH+32)
1030 IFBH=32THENC$=CHR$(128)
1040 O$=O$+C$
1050 NEXT
1060 T$=O$
1070 RETURN



