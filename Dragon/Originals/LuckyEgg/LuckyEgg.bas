10 DIM AL(1),B(1),E(1),X(16),P,PP,E,EY,EO,V
20 FORI=0TO15:X(I)=I*16:NEXT
30 Y=166:YY=Y+15
40 REM SET UP GRAPHICS
50 PMODE0,1:PCLS:SCREEN1,1
60 GET(0,0)-(15,15),B,G
70 FOR L=0TO7:READ D:POKE1536+(L*16),D:NEXT
80 GET(0,0)-(15,15),AL,G
90 FOR L=0TO7:READ D:POKE1536+(L*16),D:NEXT
100 GET(0,0)-(15,15),E,G
110 PCLS
120 REM GAME INIT
130 P=7:PP=10:E=X(RND(16)):V=5
140 LINE(0,185)-(255,185),PSET
150 REM GAME LOOP
160 A$=INKEY$:IF A$="P"THENP=P+1 ELSE IF A$="Q"THENP=P-1
170 IF P<0THENP=0
180 IF P>15THENP=15
190 IF PP<>P THEN PUT(X(P),Y)-(X(P)+15,YY),AL,PSET:PUT(X(PP),Y)-(X(PP)+15,YY),B,PSET:PP=P
200 PUT(E,EY)-(E+15,EY+15),E,PSET:PUT(E,EO)-(E+15,EO+V),B,PSET
210 EO=EY:EY=EY+V:IF EY>160 THEN GOSUB222
220 GOTO 160
221 REM RESET EGG
222 EY=0:E=X(RND(16)):V=V+1:IFV>15THENV=15
223 RETURN
230 REM STICK MAN DATA
240 DATA 24,60,24,126,24,24,36,66
250 REM EGG
260 DATA 0,32,112,112,248,248,112,32


