10 DIM AL(1),B(1),E(1),S(1),X(16),P,PP,E,EY,EO,V,F,M,SC
20 FORI=0TO15:X(I)=I*16:NEXT
30 Y=166:YY=Y+15
40 REM SET UP GRAPHICS
50 PMODE0,1:PCLS
60 GET(0,0)-(15,15),B,G
70 FOR L=0TO7:READ D:POKE1536+(L*16),D:NEXT
80 GET(0,0)-(15,15),AL,G
90 FOR L=0TO7:READ D:POKE1536+(L*16),D:NEXT
100 GET(0,0)-(15,15),E,G
110 FOR L=0TO7:READ D:POKE1536+(L*16),D:NEXT
120 GET(0,0)-(15,15),S,G
130 PCLS
140 GOSUB490
150 REM GAME INIT
160 P=7:PP=10:F=8:E=X(F):V=5:M=0:SC=0
170 LINE(2,2)-(2,17),PSET:LINE(2,17)-(10,17),PSET
180 LINE(15,2)-(15,17),PSET:LINE(15,17)-(25,17),PSET:LINE(15,2)-(25,2),PSET:LINE(15,9)-(22,9),PSET
190 LINE(31,185)-(255,185),PSET
200 LINE(31,0)-(31,195),PSET
210 REM GAME LOOP
220 A$=INKEY$:IF A$="P"THENP=P+1 ELSE IF A$="Q"THENP=P-1
230 IF P<3THENP=3
240 IF P>15THENP=15
250 IF PP<>P THEN PUT(X(P),Y)-(X(P)+15,YY),AL,PSET:PUT(X(PP),Y)-(X(PP)+15,YY),B,PSET:PP=P
260 PUT(E,EY)-(E+15,EY+15),E,PSET:PUT(E,EO)-(E+15,EO+V),B,PSET
270 EO=EY:EY=EY+V:IF EY>160 THEN IF P<>F GOSUB290ELSE GOSUB380
280 IF M<6 THEN 220 ELSE 610
290 REM EGG MISS
300 PUT(E,180)-(E+15,EY+15),E,OR
310 EY=EY-V:PUT(E,EY)-(E+15,EY+15),B,PSET:PUT(E,169)-(E+15,184),S,PSET
320 SCREEN1,0:PLAY"T200O3GGFFEE":SCREEN1,1
330 PUT(X(P),Y)-(X(P)+15,YY),AL,PSET
340 GOSUB430:GOSUB470
350 M=M+1:PUT(10,10+20*M)-(25,25+20*M),E,PSET
360 IFM=6THEN PCLS:SCREEN1,0:FORG=1TO12:E=X(RND(16)):EY=RND(160):PUT(E,EY)-(E+15,EY+15),E,PSET:PLAY"T255ABACDEFGG":NEXT
370 RETURN
380 REM EGG CATCH
390 PLAY"T200ABACDEFGG":SC=SC+1
400 PUT(E,EY)-(E+15,EY+15),B,PSET
410 GOSUB430:GOSUB470
420 RETURN
430 REM REDRAW STICK MAN
440 PUT(X(P),Y)-(X(P)+15,YY),AL,PSET:PUT(X(P),Y-15)-(X(P)+15,Y),B,PSET
450 RETURN
460 REM RESET EGG
470 EY=0:F=2+RND(13):E=X(F):V=V+0.25:IFV>15THENV=8
480 RETURN
490 REM TITLE SCREEN
500 CLS0
510 PRINT@11,"lucky egg";
520 PRINT@32+15,CHR$(207)CHR$(207);
530 PRINT@64+14,CHR$(207)CHR$(207)CHR$(207)CHR$(207);
540 PRINT@96+13,CHR$(207)CHR$(207)CHR$(207)CHR$(207)CHR$(207)CHR$(207);
550 PRINT@128+13,CHR$(207)CHR$(207)CHR$(207)CHR$(207)CHR$(207)CHR$(207);
560 PRINT@160+14,CHR$(207)CHR$(207)CHR$(207)CHR$(207);
570 PRINT@192+15,CHR$(207)CHR$(207);
580 INPUT K
590 SCREEN1,1:PCLS
600 GOTO160
610 REM SCORE SCREEN
620 CLS0:PRINT"LUCKY EGG"
630 PRINT"SCORE":PRINTSC
640 INPUT K
650 GOTO 500
660 REM STICK MAN
670 DATA 24,60,24,126,24,24,36,66
680 REM EGG
690 DATA 0,32,112,112,248,248,112,32
700 REM BROKEN EGG
710 DATA 0,0,0,0,66,102,102,60


