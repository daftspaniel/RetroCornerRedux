10 DIM AL(1),B(1),E(1),X(16),P,PP,E,EY,EO,V,F,M,SC
20 FORI=0TO15:X(I)=I*16:NEXT
30 Y=166:YY=Y+15
40 REM SET UP GRAPHICS
50 PMODE0,1:PCLS
60 GET(0,0)-(15,15),B,G
70 FOR L=0TO7:READ D:POKE1536+(L*16),D:NEXT
80 GET(0,0)-(15,15),AL,G
90 FOR L=0TO7:READ D:POKE1536+(L*16),D:NEXT
100 GET(0,0)-(15,15),E,G
110 PCLS
120 GOSUB510
130 REM GAME INIT
140 P=7:PP=10:F=8:E=X(F):V=5:M=0:SC=0
150 LINE(2,2)-(2,17),PSET:LINE(2,17)-(10,17),PSET
160 LINE(15,2)-(15,17),PSET:LINE(15,17)-(25,17),PSET:LINE(15,2)-(25,2),PSET:LINE(15,9)-(22,9),PSET
170 LINE(31,185)-(255,185),PSET
180 LINE(31,0)-(31,195),PSET
190 REM GAME LOOP
200 A$=INKEY$:IF A$="P"THENP=P+1 ELSE IF A$="Q"THENP=P-1
210 IF P<3THENP=3
220 IF P>15THENP=15
230 IF PP<>P THEN PUT(X(P),Y)-(X(P)+15,YY),AL,PSET:PUT(X(PP),Y)-(X(PP)+15,YY),B,PSET:PP=P
240 PUT(E,EY)-(E+15,EY+15),E,PSET:PUT(E,EO)-(E+15,EO+V),B,PSET
250 EO=EY:EY=EY+V:IF EY>160 THEN IF P<>F GOSUB270ELSE GOSUB360
260 IF M<6 THEN 200 ELSE 560
270 REM EGG MISS
280 PUT(E,180)-(E+15,EY+15),E,OR
290 EY=EY-V:PUT(E,EY)-(E+15,EY+15),B,PSET
300 SCREEN1,0:PLAY"T200O3GGFFEE":SCREEN1,1
310 PUT(X(P),Y)-(X(P)+15,YY),AL,PSET
320 GOSUB410:GOSUB450
330 M=M+1:PUT(10,10+20*M)-(25,25+20*M),E,PSET
340 IFM=6THEN PCLS:SCREEN1,0:FORG=1TO12:E=X(RND(16)):EY=RND(160):PUT(E,EY)-(E+15,EY+15),E,PSET:PLAY"T255ABACDEFGG":NEXT
350 RETURN
360 REM EGG CATCH
370 PLAY"T200ABACDEFGG":SC=SC+1
380 PUT(E,EY)-(E+15,EY+15),B,PSET
390 GOSUB410:GOSUB450
400 RETURN
410 REM REDRAW STICK MAN
420 PUT(X(P),Y)-(X(P)+15,YY),AL,PSET:PUT(X(P),Y-15)-(X(P)+15,Y),B,PSET
430 RETURN
440 REM RESET EGG
450 EY=0:F=2+RND(13):E=X(F):V=V+0.25:IFV>15THENV=8
460 RETURN
470 REM STICK MAN
480 DATA 24,60,24,126,24,24,36,66
490 REM EGG
500 DATA 0,32,112,112,248,248,112,32
510 REM TITLE SCREEN
520 CLS:PRINT"LUCKY EGG"
530 INPUT K
540 SCREEN1,1:PCLS
550 GOTO140
560 REM SCORE SCREEN
570 CLS:PRINT"LUCKY EGG"
580 PRINT"SCORE":PRINTSC
590 INPUT K
600 GOTO 520

