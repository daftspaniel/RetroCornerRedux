10 DIMAL(1),B(1),E(1),S(1),X(16),P,PP,E,EY,EO,V,F,M,SC,HS
20 FORI=0TO15:X(I)=I*16:NEXT
30 Y=166:YY=Y+15
40 REM SET UP GRAPHICS
50 PMODE0,1:PCLS
60 GET(0,0)-(15,15),B,G
70 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
80 GET(0,0)-(15,15),AL,G
90 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
100 GET(0,0)-(15,15),E,G
110 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
120 GET(0,0)-(15,15),S,G
130 PCLS
140 GOSUB500
150 REM GAME INIT
160 P=7:PP=10:F=8:E=X(F):V=5:M=0:SC=0:EY=0
170 LINE(2,2)-(2,17),PSET:LINE(2,17)-(10,17),PSET
180 LINE(15,2)-(15,17),PSET:LINE(15,17)-(25,17),PSET:LINE(15,2)-(25,2),PSET:LINE(15,9)-(22,9),PSET
190 LINE(31,185)-(255,185),PSET
200 LINE(31,0)-(31,195),PSET
210 FORN=1TO6:PUT(10,10+20*N)-(25,25+20*N),E,PSET:NEXT
220 REM GAME LOOP
230 A$=INKEY$:IFA$="P"THENP=P+1ELSEIFA$="Q"THENP=P-1
240 IFP<2THENP=15
250 IFP>15THENP=2
260 IF PP<>P THEN PUT(X(P),Y)-(X(P)+15,YY),AL,PSET:PUT(X(PP),Y)-(X(PP)+15,YY),B,PSET:PP=P
270 PUT(E,EY)-(E+15,EY+15),E,PSET:PUT(E,EO)-(E+15,EO+V),B,PSET
280 EO=EY:EY=EY+V:IF EY>160 THEN IF P<>F THEN GOSUB300ELSE GOSUB380
290 IF M<6 THEN230ELSE610
300 REM EGG MISS
310 EY=EY-V:PUT(E,169)-(E+15,184),S,PSET:PUT(E,153)-(E+15,168),B,PSET
320 SCREEN1,0:PLAY"T200O3GGFFEE":SCREEN1,1
330 GOSUB440:GOSUB480
340 M=M+1:PUT(8,10+20*M)-(23,25+20*M),S,PSET
350 IF M=6 THENPCLS:SCREEN1,0:FORG=1TO12:E=X(RND(16)):EY=RND(160):PUT(E,EY)-(E+15,EY+15),E,PSET:PLAY"T255BADEGG":NEXT
360 PUT(X(P),Y)-(X(P)+15,YY),AL,PSET:LINE(31,185)-(255,185),PSET
370 RETURN
380 REM EGG CATCH
390 PLAY"T200ABACDEFGG":SC=SC+1
400 PUT(E,EY)-(E+15,EY+15),B,PSET
410 PUT(X(P),Y)-(X(P)+15,YY),AL,PSET:LINE(31,185)-(255,185),PSET
420 GOSUB440:GOSUB480
430 RETURN
440 REM REDRAW STICK MAN
450 PUT(X(P),Y)-(X(P)+15,YY),AL,PSET:PUT(X(P),Y-15)-(X(P)+15,Y),B,PSET
460 RETURN
470 REM RESET EGG
480 EY=0:F=1+RND(13):E=X(F):V=V+0.45:IFV>15THENV=8
490 RETURN
500 REM TITLE SCREEN
510 CLS0:?@27,"v0.1";:?@0,"2024";
520 TL=7:T$="lucky"+CHR$(128)+"egg":GOSUB860
530 TL=8:T$="by"+CHR$(128)+"davy"+CHR$(128)+"mitchell":GOSUB860
540 GOSUB730
545 TL=9:T$="high"+CHR$(128)+"score"+CHR$(128)+STR$(HS):GOSUB860
550 TL=11:T$=" CATCH THE FALLING EGGS! ":GOSUB860
560 TL=12:T$=" CONTROLS LEFT q RIGHT p ":GOSUB860
570 TL=13:T$=" 6 MISSES IS GAME OVER!  ":GOSUB860
580 GOSUB960:ZZ=RND(-TIMER)
590 SCREEN1,1:PCLS
600 GOTO160
610 REM SCORE SCREEN
620 CLS0
630 TL=7:T$="lucky"+CHR$(128)+"egg":GOSUB860:
640 TL=10:T$="score"+CHR$(128)+STR$(SC):GOSUB860:PLAY"T200ABACDEFGG"
650 GOSUB730:PLAY"T200EEEEEEGGGGGGGGGGGGG"
660 IFSC<10THENT$="a"+CHR$(128)+"rotten"+CHR$(128)+"result"
670 IFSC>9 AND SC<20 THEN T$="you"+CHR$(128)+"are"+CHR$(128)+"a"+CHR$(128)+"good"+CHR$(128)+"egg"
680 IFSC>19 THEN T$="EGG-cellent"+CHR$(128)+"score"
690 TL=12:GOSUB860
700 GOSUB900:GOSUB960
710 IF SC>HS THEN HS=SC
720 GOTO510
730 REM DRAW EGG
740 PRINT@32+15,CHR$(207)CHR$(207);
750 PRINT@64+14,CHR$(207)CHR$(207)CHR$(207)CHR$(207);
760 PRINT@96+13,CHR$(207)CHR$(207)CHR$(207)CHR$(207)CHR$(207)CHR$(207);
770 PRINT@128+13,CHR$(207)CHR$(207)CHR$(207)CHR$(207)CHR$(207)CHR$(207);
780 PRINT@160+14,CHR$(207)CHR$(207)CHR$(207)CHR$(207);
790 RETURN
800 REM STICK MAN
810 DATA24,60,24,126,24,24,36,66
820 REM EGG
830 DATA0,32,112,112,248,248,112,32
840 REM BROKEN EGG
850 DATA0,0,0,0,66,102,102,60
860 REM CENTER TEXT. TL = vertical line number.
870 PRINT@(32-LEN(T$))/2+32*TL,T$;
880 RETURN
890 REM DELAY
900 FORDZ=1TO1000:NEXT:RETURN
910 REM WAIT FOR SPACEBAR
920 WA$=INKEY$:IFWA$<>" "THEN920
930 RETURN
940 REM ANY KEY PROMPT AT BOTTOM OF SCREEN.
950 ?@483,"PRESS ANY KEY TO CONTINUE";:GOSUB920:RETURN
960 REM INVERTED SPACEBAR PROMPT AT BOTTOM OF SCREEN.
970 BC$=CHR$(128):?@483,"press"+BC$+"space"+BC$+"bar"+BC$+"to"+BC$+"continue";:GOSUB920:RETURN
980 REM LOWER CASE TEXT
990 TL=LEN(T$):O$=""
1000 FORBR=1TOTL:C$=MID$(T$,BR,1):BH=ASC(C$):IFBH<91ANDBH>64THENC$=CHR$(BH+32)
1010 IFBH=32THENC$=CHR$(128)
1020 O$=O$+C$
1030 NEXT
1040 T$=O$
1050 RETURN



