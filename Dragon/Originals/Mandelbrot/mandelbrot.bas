1 DIMN,A,B,X,Y,Z1,Z2,P:CLEAR 300, 32735
2 GOSUB2600:GOSUB2000
10 X1=31:Y1=15
20 I1=-1:I2=1:R1=-2:R2=1
25 CLS0
30 S1=(R2-R1)/X1:S2=(I2-I1)/Y1:P=0
40 FOR Y=0 TO Y1
50 I3=I1+S2*Y
60 FOR X=0 TO X1
70 R3=R1+S1*X:Z1=R3:Z2=I3
80 FOR N=0 TO 30
90 A=Z1*Z1:B=Z2*Z2
100 IF A+B>4 THEN 130
110 Z2=2*Z1*Z2+I3:Z1=A-B+R3
120 NEXT N
130 IF N=31 THEN POKE1024+P,159 ELSE POKE1024+P,255-N*4
140 P=P+1
150 NEXT X
160 NEXT Y
165 I1=I1+0.1:I2=I2-0.1:R1=R1+0.1:R2=R2-0.1:GOTO30
170 IFINKEY$=""THEN170

2000 CLS0:TT$="SG4 MANDELBROT SET":TBY$="DAVY MITCHELL":TD$="INSPIRED BY MISC VERSIONS"
2010 TV$="VERSION 0.1":TC$=CHR$(159):GOSUB 9010
2020 GOSUB9930:GOTO9810
2500 RETURN
2600 REM SPEED UPS
2610 POKE65495,0
2630 FOR X = 32736 TO 32756
2640 READ A$: A=VAL("&H"+A$)
2650 POKE X,A: NEXT
2660 POKE 411,127: POKE 412,224:POKE 410,126
2670 REM BREAK DISABLE
2680 DATA 32,62,1C,AF,BD,80,06,26,07,81,13,26,03,7E,85,2B,97,87,7E,84,A6
2690 RETURN
9010 TM=LEN(TT$):IF LEN(TBY$)>TM THEN TM=LEN(TBY$)
9015 IF LEN(TV$)>TM THEN TM=LEN(TV$)
9017 IF LEN(TD$)>TM THEN TM=LEN(TD$)
9020 BH=7:BW=TM+2:BY=3:BX=(30-BW)/2+1:BC$=CHR$(143):GOSUB9510
9030 BH=9:BW=TM+4:BY=1:BX=(32-BW)/2:BC$=TC$:GOSUB9410
9040 ?@(BY+1)*32 + ((32-LEN(TT$))/2),TT$;
9050 ?@(BY+3)*32 + ((32-LEN(TBY$))/2),TBY$;
9060 ?@(BY+5)*32 + ((32-LEN(TD$))/2),TD$;
9070 ?@(BY+7)*32 + ((32-LEN(TV$))/2),TV$;
9100 RETURN
9410 BR$=""
9420 BR$=STRING$(BW,BC$)
9430 ?@BX+((BY+1)*32),BR$;
9435 ?@BX+((BY+BH)*32),BR$;
9437 BH=BH-1:BY=BY+1:BW=BW-1
9440 FOR BQ=1TOBH:?@BX+((BY+BQ)*32),BC$;:?@BX+BW+((BY+BQ)*32),BC$;:NEXT
9499 RETURN
9510 BR$=""
9520 FOR BR=1TOBW:BR$=BR$+BC$:NEXT
9530 FOR BQ=1TOBH:?@BX+((BY-1+BQ)*32),BR$;:NEXT
9599 RETURN
9810 WA$=INKEY$:IF WA$="" THEN 9810
9820 RETURN
9930 BC$=CHR$(128):?@483,"press"+BC$+"any"+BC$+"key"+BC$+"to"+BC$+"continue";:GOSUB9810:RETURN
