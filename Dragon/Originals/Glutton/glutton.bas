10 CLEAR 500:DIM G(32,16),X,Y,A$,HS
20 DIM BX,BY,BH,BW,BQ,BC$,T$,TT$,BR,LX,LC$,LZ,LY,LL,TL,LO$
30 CLS 0
40 TT$="GLUTTON":TBY$="DAVY MITCHELL":TD$="BASED ON GREED BY M.DAY"
50 TV$="VERSION 1.0 AUG 2025":TC$=CHR$(175):GOSUB 830
60 GOSUB 1450
70 CLS:TL=0:T$="welcome to glutton":GOSUB1320
80 TL=2:T$="Q / A = UP AND DOWN":GOSUB1320
90 TL=3:T$="O / P = LEFT AND RIGHT":GOSUB1320
100 TL=5:T$="X = FINISH ROUND ":GOSUB1320
110 TL=7:T$="MOVING ONTO A NUMBERED SQUARE":GOSUB1320
120 TL=8:T$="MOVES YOU N STEPS IN THAT":GOSUB1320
130 TL=9:T$="DIRECTION. YOU MUST STAY ON THE":GOSUB1320
140 TL=10:T$="BOARD AND CANNOT STEP ON A":GOSUB1320
150 TL=11:T$="SQUARE TWICE. KEEP PLAYING":GOSUB1320
160 TL=12:T$="UNTIL YOU CANNOT MAKE A MOVE.":GOSUB1320

170 GOSUB 1450

180 REM SET THE INITIAL PLAYING FIELD
190 FORY=1TO16
200 FORX=1TO32
210 G(X,Y)=RND(9)
220 POKE&H400+((Y-1)*&H20)+(X-1),&H30+G(X,Y)
230 NEXT
240 NEXT
250 REM INIT GAME
260 S=0
270 X=15:Y=8:G(X,Y)=0
280 POKE&H400+((Y-1)*&H20)+(X-1),106
290 A$=INKEY$:IFA$=""THEN290
300 IFA$="O" AND X>1 THEN GOSUB380
310 IFA$="P" AND X<32 THEN GOSUB460
320 IFA$="Q" AND Y>1 THEN GOSUB540
330 IFA$="A" AND Y<16 THEN GOSUB620
340 IFA$="X" THEN GOSUB700:GOTO70
350 S=S+D
360 GOTO 280
370 GOTO 370
380 REM *** MOVE LEFT ***
390 D=G(X-1,Y)
400 IF D=0 OR D=>X THEN SOUND100,1:RETURN
410 V=0:FOR J=X-1 TO X-D STEP-1:IFG(J,Y)=0THENV=1
420 NEXT:IFV=1THENSOUND100,1:RETURN
430 FOR J=X TO X-D STEP-1:G(J,Y)=0:POKE&H400+((Y-1)*&H20)+(J-1),128:NEXT
440 X=X-D
450 RETURN
460 REM *** MOVE RIGHT ***
470 D=G(X+1,Y)
480 IF D=0 OR D+X>32 THEN SOUND100,1:RETURN
490 V=0:FOR J=X+1 TO X+D:IFG(J,Y)=0THENV=1
500 NEXT:IFV=1THENSOUND100,1:RETURN
510 FOR J=X TO X+D:G(J,Y)=0:POKE&H400+((Y-1)*&H20)+(J-1),128:NEXT
520 X=X+D
530 RETURN
540 REM *** MOVE UP ***
550 D=G(X,Y-1)
560 IF D=0 OR Y-D<1 THEN SOUND100,1:RETURN
570 V=0:FOR J=Y-1 TO Y-D STEP-1:IFG(X,J)=0THENV=1
580 NEXT:IFV=1THENSOUND100,1:RETURN
590 FOR J=Y TO Y-D STEP-1:G(X,J)=0:POKE&H400+((J-1)*&H20)+(X-1),128:NEXT
600 Y=Y-D
610 RETURN
620 REM *** MOVE DOWN ***
630 D=G(X,Y+1)
640 IF D=0 OR Y+D>16 THEN SOUND100,1:RETURN
650 V=0:FOR J=Y+1 TO Y+D:IFG(X,J)=0THENV=1
660 NEXT:IFV=1THENSOUND100,1:RETURN
670 FOR J=Y TO Y+D:G(X,J)=0:POKE&H400+((J-1)*&H20)+(X-1),128:NEXT
680 Y=Y+D
690 RETURN

700 REM SCORE PAGE
710 CLS
720 IF S>HS THEN HS=S
730 TT$="GLUTTON":TBY$="SCORE:"+STR$(S):TD$=""
740 TV$="HIGH SCORE:"+STR$(HS):TC$=CHR$(175):GOSUB 830
750 GOSUB 1450
760 RETURN

770 REM ------LIBRARY LIVES HERE------

780 REM TITLE SCREEN
790 REM TT$ = PROGRAM NAME
800 REM TBY$ = AUTHOR
810 REM TD$ = DATE
820 REM TV$ = VERSION
830 TM=LEN(TT$):IF LEN(TBY$)>TM THEN TM=LEN(TBY$)
840 IF LEN(TV$)>TM THEN TM=LEN(TV$)
850 IF LEN(TD$)>TM THEN TM=LEN(TD$)
860 BH=7:BW=TM+2:BY=3:BX=(30-BW)/2+1:BC$=CHR$(143):GOSUB1260
870 BH=9:BW=TM+4:BY=1:BX=(32-BW)/2:BC$=TC$:GOSUB1140
880 ?@(BY+1)*32 + ((32-LEN(TT$))/2),TT$;
890 ?@(BY+3)*32 + ((32-LEN(TBY$))/2),TBY$;
900 ?@(BY+5)*32 + ((32-LEN(TD$))/2),TD$;
910 ?@(BY+7)*32 + ((32-LEN(TV$))/2),TV$;
920 RETURN

930 REM VERTICAL LINE
940 REM LC$ = CHARACTER FOR LINE
950 REM LX,LY = POSITION 
960 REM LL = LENGTH
970 LZ=LY+LL-1
980 FOR BR=LY TO LZ:?@LX+(BR*32),LC$;:NEXT
990 RETURN

1000 REM HORIZONTAL LINE
1010 REM LC$ = CHARACTER FOR LINE
1020 REM LX,LY = POSITION 
1030 REM LL = LENGTH
1040 LO$=""
1050 REM MC10: FOR BR=1TOLL:LO$=LO$+LC$:NEXT
1060 REM COCO/DRAGON: LO$=STRING$(LL,LC$)
1070 LO$=STRING$(LL,LC$)
1080 ?@LX+(LY*32),LO$;
1090 RETURN

1100 REM BOX OUTLINE
1110 REM BX,BY = BOX POSITION
1120 REM BH,BW = BOX HEIGHT & WIDTH
1130 REM BC$ = CHARACTER FOR BOX EDGE
1140 REM MC10: BR$="":FOR BR=1TOBW:BR$=BR$+BC$:NEXT
1150 REM COCO/DRAGON: BR$=STRING$(BW,BC$)
1160 BR$=STRING$(BW,BC$)
1170 ?@BX+((BY+1)*32),BR$;
1180 ?@BX+((BY+BH)*32),BR$;
1190 BH=BH-1:BY=BY+1:BW=BW-1
1200 FOR BQ=1TOBH:?@BX+((BY+BQ)*32),BC$;:?@BX+BW+((BY+BQ)*32),BC$;:NEXT
1210 RETURN

1220 REM FILLED BOX
1230 REM BX,BY = BOX POSITION
1240 REM BH,BW = BOX HEIGHT & WIDTH
1250 REM BC$ = CHARACTER FOR BOX
1260 REM MC10: BR$="":FOR BR=1TOBW:BR$=BR$+BC$:NEXT
1270 REM COCO/DRAGON: BR$=STRING$(BW,BC$)
1280 BR$=STRING$(BW,BC$)
1290 FOR BQ=1TOBH:?@BX+((BY-1+BQ)*32),BR$;:NEXT
1300 RETURN

1310 REM CENTER TEXT. TL = vertical line number.
1320 PRINT@(32-LEN(T$))/2+32*TL,T$;
1330 RETURN

1340 REM DELAY
1350 FORDZ=1TO1000:NEXT:RETURN

1360 REM WAIT FOR SPACEBAR
1370 BC$=CHR$(128):?@483,"press"+BC$+"space"+BC$+"bar"+BC$+"to"+BC$+"continue";:
1380 WA$=INKEY$:IFWA$<>" "THEN1380ELSE RETURN

1390 REM WAIT FOR ANY KEY
1400 WA$=INKEY$:IF WA$="" THEN 1400
1410 RETURN

1420 REM ANY KEY PROMPT AT BOTTOM OF SCREEN.
1430 ?@483,"PRESS ANY KEY TO CONTINUE";:GOSUB1400:RETURN

1440 REM INVERTED ANY KEY PROMPT AT BOTTOM OF SCREEN.
1450 BC$=CHR$(128):?@483,"press"+BC$+"any"+BC$+"key"+BC$+"to"+BC$+"continue";:GOSUB1400:RETURN

1460 REM LOWER CASE TEXT
1470 TL=LEN(T$):O$=""
1480 FORBR=1TOTL:C$=MID$(T$,BR,1):BH=ASC(C$):IF BH<91 AND BH>64 THEN C$=CHR$(BH+32)
1490 IFBH=32THENC$=CHR$(128)
1500 O$=O$+C$
1510 NEXT
1520 T$=O$
1530 RETURN


