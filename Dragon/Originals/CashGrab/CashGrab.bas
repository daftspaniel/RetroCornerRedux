10 DIMH(16),A(1),B(1),C(1),P(1),R(1),M(16,11),N(11),X,Y,V,W,D,J,O,R,L,S,M,Q,HP,VP,CL,A$,T,PR,OU$(8)
20 DIMC$(27),N$(10):GOSUB1350:REM SET UP TEXT
30 FORI=0TO15:H(I)=I*16:NEXT
40 GOSUB790:REM SET UP GRAPHICS
50 GOSUB910:REM TITLE SCREEN
60 GOSUB1010:REM LEGEND SCREEN
70 GOSUB650:REM INIT GAME
80 GOSUB680:REM INIT LEVEL
90 PCLS:GOSUB430
100 REM GAME LOOP
110 A$=INKEY$:IFA$="P"THENX=X+1ELSEIFA$="O"THENX=X-1
120 IFA$="Q"THENY=Y-1ELSEIFA$="A"THENY=Y+1
130 J=JOYSTK(0):IFJ=0THENX=X-1ELSEIFJ=63THENX=X+1
140 J=JOYSTK(1):IFJ=0THENY=Y-1ELSEIFJ=63THENY=Y+1
150 IFX<0THENX=0ELSEIFX>15THENX=15
160 IFY<1THENY=1ELSEIFY>11THENY=11
170 GOSUB220
180 IFX=V THEN IF Y=W THEN200
190 PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET:PUT(H(V),H(W))-(H(V)+15,H(W)+15),B,PSET:V=X:W=Y
200 ON M(X,Y) GOSUB300,360,390:IF M THEN110
210 GOSUB1090:GOTO50
220 REM DRAW SCREEN UPDATES
230 R=RND(11):IF N(R) THENN(R)=N(R)-1:O=N(R):M(O,R)=1:PUT(H(O),H(R))-(H(O)+15,H(R)+15),R,PSET
240 IF PR THEN PUT(0,H(PR))-(15,H(PR)+15),P,NOT
250 IFRND(10)>1THENRETURN
260 O=2+RND(14):R=1+RND(9):IFT<3THEN O=2+RND(7)
270 IF H(O) AND M(O,R)=0 THEN M(O,R)=2:PUT(H(O),H(R))-(H(O)+15,H(R)+15),C,PSET
280 RETURN
290 REM BURN
300 SCREEN1,0:PLAY"T255AEAEAAAAEEA":SCREEN1,1
310 O$=OU$(RND(8)):HP=INT((255-(LEN(O$)*8))/2):VP=100:
320 LINE(0,VP-2)-(255,VP+10),PRESET,BF:GOSUB1290:PLAY"T15AEAEAAAAEEA"
330 M=M-1:IF M THENGOSUB580:GOSUB430:GOSUB690ELSEGOTO340
340 RETURN
350 REM SCORE
360 M(X,Y)=0:S=S+10+X:GOSUB530:PLAY"T255BCDGG":GOSUB260:T=T+1:IFT=3THENGOSUB620
370 RETURN
380 REM PORTAL AND NEXT LEVEL
390 L=L+1:T=0:PLAY"O3;V15;T194;GFEDCBAAAAAAAO2AAAAV9AAGFEDCBAAAAAAAO2AAAAV8AAGFEDCBAAAAAAAO2AAAAV7AA;V15":GOSUB580
400 GOSUB550:GOSUB690
410 RETURN
420 REM DRAW INITIAL SCREEN
430 LINE(0,15)-(255,15),PSET
440 HP=6:VP=2:O$="LIVES": GOSUB1290
450 HP=84:VP=2:O$="SCORE": GOSUB1290
460 HP=180:VP=2:O$="LEVEL": GOSUB1290
470 GOSUB500:GOSUB530:GOSUB550
480 RETURN
490 REM UPDATE LIVES DISPLAY
500 LINE(55,2)-(64,10),PRESET,BF:HP=55:VP=2:N$=MID$(STR$(M),2):GOSUB1320
510 RETURN
520 REM UPDATE SCORE DISPLAY
530 LINE(135,2)-(160,10),PRESET,BF:HP=135:VP=2:N$=MID$(STR$(S),2):FORQ=1TOLEN(N$):DRAW"BM"+STR$(HP)+",2"+N$(ASC(MID$(N$,Q,1))-48):HP=HP+8:NEXT:RETURN
540 REM UPDATE LEVEL DISPLAY
550 LINE(231,2)-(240,10),PRESET,BF:HP=231:VP=2:N$=MID$(STR$(L),2):GOSUB1320
560 RETURN
570 REM CLEAR PLAYFIELD HORIZONTAL
580 FORCL=16TO192STEP32:LINE(0,CL)-(255,CL+16),PRESET,BF:PLAY"T255BAAA":NEXT
590 FORCL=32TO192STEP32:LINE(0,CL)-(255,CL+16),PRESET,BF:PLAY"T255ABBB":NEXT
600 RETURN
610 REM POSITION AND DISPLAY PORTAL
620 PR=RND(11):M(0,PR)=3
630 PUT(0,H(PR))-(15,H(PR)+15),P,PSET
640 RETURN
650 REM INIT GAME
660 L=1:S=0:M=3
670 RETURN
680 REM LEVEL SET UP
690 X=5:Y=2:V=1:W=1:T=0:PR=0
700 GOSUB750
710 IFL>2THENI=L+2:FORJ=1TOI:O=RND(16):R=RND(11):M(O,R)=1:PUT(H(O),H(R))-(H(O)+15,H(R)+15),R,PSET:SOUND255,1:NEXT
720 M(X,Y)=0: REM ENSURE STARTING POSITION CLEAR
730 RETURN
740 REM RESET LEVEL DATA - SCREEN LAYOUT AND LAVA
750 FORJ=0TO16:FORI=0TO11:M(J,I)=0:NEXT:NEXT
760 FORI=0TO11:N(I)=16:NEXT
770 RETURN
780 REM DRAW GFX
790 PMODE0,1:PCLS:SCREEN1,1
800 GET(0,0)-(15,15),B
810 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
820 GET(0,0)-(15,15),A
830 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
840 GET(0,0)-(15,15),R
850 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
860 GET(0,0)-(15,15),C
870 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
880 GET(0,0)-(15,15),P
890 RETURN
900 REM TITLE SCREEN
910 PCLS:LINE(0,176)-(255,176),PSET:LINE(0,15)-(255,15),PSET
920 HP=87:VP=40:O$="CASH@GRAB":GOSUB1290:PLAY"T255AO3AO2GG"
930 HP=85:VP=180:O$="VERSION":GOSUB1290
940 HP=145:VP=180:N$="0":GOSUB1320
950 HP=157:VP=180:N$="2":GOSUB1320:PSET(153,188,1):PLAY"T255AO3AO2GG"
960 HP=110:VP=60:O$="BY":GOSUB1290
970 HP=70:VP=80:O$="DAVY@MITCHELL":GOSUB1290:PLAY"T255AO3AO2GG"
980 X=7:Y=7:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET
990 GOSUB1180
1000 RETURN
1010 REM LEGEND SCREEN
1020 LINE(14,30)-(200,170),PRESET,BF
1030 X=3:Y=2:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET
1040 X=3:Y=4:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),C,PSET
1050 X=3:Y=6:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),P,PSET
1060 X=3:Y=8:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),R,PSET
1070 GOSUB1180
1080 RETURN
1090 REM GAME OVER SCREEN
1100 PCLS:LINE(0,176)-(255,176),PSET:LINE(0,15)-(255,15),PSET
1110 HP=91:VP=40:O$="GAME@OVER": GOSUB1290
1120 HP=87:VP=80:O$="YOU@SCORED": GOSUB1290
1130 HP=INT((255-LEN(STR$(S)))/2):VP=100:O$=MID$(STR$(S),2):GOSUB1320
1140 X=6:Y=6:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),C,PSET
1150 A$=INKEY$:FORD=1TO4000:NEXT:GOSUB1180
1160 RETURN
1170 REM AWAIT INPUT
1180 A$=INKEY$:IFA$=""THEN1180
1190 RETURN
1200 REM STICK MAN
1210 DATA24,60,24,126,24,24,36,66
1220 REM LAVA
1230 DATA97,95,204,32,244,31,120,30
1240 REM CASH
1250 DATA0,0,60,24,60,126,126,60
1260 REM PORTAL
1270 DATA126,129,189,165,165,189,129,126
1280 REM DISPLAY TEXT
1290 FORQ=1TOLEN(O$):DRAW"BM"+STR$(HP)+","+STR$(VP)+";"+C$(ASC(MID$(O$,Q,1))-64):HP=HP+8:NEXT
1300 RETURN
1310 REM DISPLAY NUMBERS
1320 FORQ=1TOLEN(N$):DRAW"BM"+STR$(HP)+","+STR$(VP)+";"+N$(ASC(MID$(N$,Q,1))-48):HP=HP+8:NEXT
1330 RETURN
1340 REM LETTER DATA
1350 C$(1)="D8U8R4D4L4R4D4"
1360 C$(2)="R4D8L4U8D4R4"
1370 C$(3)="R4L4D8R4"
1380 C$(4)="D8R3E1U6M-4,-1"
1390 C$(5)="R4L4D4R4L4D4R4"
1400 C$(6)="R4L4D4R4L4D4"
1410 C$(7)="R4L4D8R4U4"
1420 C$(8)="D8U4R4U4D8"
1430 C$(9)="R4L2D8R2L4"
1440 C$(10)="R4L2D8L2"
1450 C$(11)="D8U4M+4,-4M-4,+4M+4,+4"
1460 C$(12)="D8R4"
1470 C$(13)="D8U8M+2,+2M+2,-2D8"
1480 C$(14)="D8U8M+4,+8U8"
1490 C$(15)="D8R4U8L4"
1500 C$(16)="D8U8R4D4L4"
1510 C$(17)="D6R4D2U8L4"
1520 C$(18)="D8U8R4D4L4F4"
1530 C$(19)="R4L4D4R4D4L4"
1540 C$(20)="R4L2D8"
1550 C$(21)="D8R4U8"
1560 C$(22)="D6F2E2U6"
1570 C$(23)="D6F2E2U6D6F2E2U6"
1580 C$(24)="M+4,+8BM+0,-8M-4,+8"
1590 C$(25)="D4R4U4D4L2D4"
1600 C$(26)="R4D2G2G2D2R4"
1610 REM NUMBERS
1620 N$(0)="D8R4U8L4"
1630 N$(1)="BM+2,+0D8"
1640 N$(2)="D2U2R4D4G4R4"
1650 N$(3)="R4D4L4R4D4L4"
1660 N$(4)="D4R4U4D8"
1670 N$(5)="R4L4D4R4D4L4"
1680 N$(6)="R4L4D4R4D4L4U4"
1690 N$(7)="R4D8"
1700 N$(8)="R4D8L4U8D4R4"
1710 N$(9)="R4D8U4L4U4"
1720 REM OUCH
1730 OU$(1)="HEY@THAT@STUFF@IS@HOT"
1740 OU$(2)="OUCH@OUCH@"
1750 OU$(3)="FEEL@THE@BURN"
1760 OU$(4)="LAVA@IS@BAD@CARPET"
1770 OU$(5)="DO@NOT@TREAD@ON@LAVA"
1780 OU$(6)="I@NEED@NEW@SHOES"
1790 OU$(7)="FEELING@HOT@HOT@HOT"
1800 OU$(8)="BURN@BABY@BURN@"
1810 RETURN
1820 REM