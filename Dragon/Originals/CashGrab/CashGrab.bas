10 DIMH(16),A(1),B(1),C(1),P(1),R(1),M(16,11),N(11),X,Y,V,W,D,J,O,R,L,S,M,Q,HP,VP,CL,A$,T,PR,OU$(8)
20 DIMC$(27),N$(10):GOSUB1470:REM SET UP TEXT
30 FORI=0TO15:H(I)=I*16:NEXT
40 GOSUB850:REM SET UP GRAPHICS
50 GOSUB970:REM TITLE SCREEN
60 GOSUB1090:REM LEGEND SCREEN
70 GOSUB710:REM INIT GAME
80 GOSUB740:REM INIT LEVEL
90 PCLS:GOSUB450
100 REM GAME LOOP
110 A$=INKEY$:IFA$="P"THENX=X+1ELSEIFA$="O"THENX=X-1
120 IFA$="Q"THENY=Y-1ELSEIFA$="A"THENY=Y+1
130 J=JOYSTK(0):IFJ=0THENX=X-1ELSEIFJ=63THENX=X+1
140 J=JOYSTK(1):IFJ=0THENY=Y-1ELSEIFJ=63THENY=Y+1
150 IFX<0THENX=0ELSEIFX>15THENX=15
160 IFY<1THENY=1ELSEIFY>11THENY=11
170 GOSUB220
180 IFX=V THEN IF Y=W THEN200
190 PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET:PUT(H(V),H(W))-(H(V)+15,H(W)+15),B,PSET:V=X:W=Y
200 ON M(X,Y) GOSUB300,360,390:IF M THEN110
210 GOSUB1210:GOTO50
220 REM DRAW SCREEN UPDATES
230 R=RND(11):IF N(R) THENN(R)=N(R)-1:O=N(R):M(O,R)=1:PUT(H(O),H(R))-(H(O)+15,H(R)+15),R,PSET
240 IF PR THEN PUT(0,H(PR))-(15,H(PR)+15),P,NOT
250 IFRND(10)>1THENRETURN
260 O=2+RND(14):R=1+RND(9):IFT<3THEN O=2+RND(7)
270 IF H(O) AND M(O,R)=0 THEN M(O,R)=2:PUT(H(O),H(R))-(H(O)+15,H(R)+15),C,PSET
280 RETURN
290 REM BURN
300 SCREEN1,0:PLAY"T255AEAEAAAAEEA":SCREEN1,1
310 O$=OU$(RND(8)):HP=INT((255-(LEN(O$)*8))/2):VP=100:
320 LINE(0,VP-2)-(255,VP+10),PRESET,BF:GOSUB1410:PLAY"T15AEAEAAAAEEA"
330 M=M-1:IF M THENGOSUB600:GOSUB450:GOSUB750ELSEGOTO340
340 RETURN
350 REM SCORE
360 M(X,Y)=0:S=S+10+X:GOSUB550:PLAY"T255BCDGG":GOSUB260:T=T+1:IFT=3THENGOSUB680
370 RETURN
380 REM PORTAL AND NEXT LEVEL
390 L=L+1:T=0:PLAY"O3;V15;T194;GFEDCBAAAAAAAO2AAAAV9AAGFEDCBAAAAAAAO2AAAAV8AAGFEDCBAAAAAAAO2AAAAV7AA;V15"
400 O$="NEXT@LEVEL":HP=INT((255-(LEN(O$)*8))/2):VP=100:
410 LINE(0,VP-2)-(255,VP+10),PRESET,BF:GOSUB1410
420 GOSUB570:GOSUB640:GOSUB750
430 RETURN
440 REM DRAW INITIAL SCREEN
450 LINE(0,15)-(255,15),PSET
460 HP=6:VP=2:O$="LIVES": GOSUB1410
470 HP=84:VP=2:O$="SCORE": GOSUB1410
480 HP=180:VP=2:O$="LEVEL": GOSUB1410
490 GOSUB520:GOSUB550:GOSUB570
500 RETURN
510 REM UPDATE LIVES DISPLAY
520 LINE(55,2)-(64,10),PRESET,BF:HP=55:VP=2:N$=MID$(STR$(M),2):GOSUB1440
530 RETURN
540 REM UPDATE SCORE DISPLAY
550 LINE(135,2)-(160,10),PRESET,BF:HP=135:VP=2:N$=MID$(STR$(S),2):FORQ=1TOLEN(N$):DRAW"BM"+STR$(HP)+",2"+N$(ASC(MID$(N$,Q,1))-48):HP=HP+8:NEXT:RETURN
560 REM UPDATE LEVEL DISPLAY
570 LINE(231,2)-(240,10),PRESET,BF:HP=231:VP=2:N$=MID$(STR$(L),2):GOSUB1440
580 RETURN
590 REM CLEAR PLAYFIELD HORIZONTAL
600 FORCL=16TO192STEP32:LINE(0,CL)-(255,CL+16),PRESET,BF:PLAY"T255BAA":NEXT
610 FORCL=32TO192STEP32:LINE(0,CL)-(255,CL+16),PRESET,BF:PLAY"T255ABB":NEXT
620 RETURN
630 REM CLEAR PLAYFIELD VERTICAL
640 FORCL=0TO255STEP32:LINE(CL,16)-(CL+16,192),PRESET,BF:PLAY"T255CDD":NEXT
650 FORCL=16TO255STEP32:LINE(CL,16)-(CL+16,192),PRESET,BF:PLAY"T255GFF":NEXT
660 RETURN
670 REM POSITION AND DISPLAY PORTAL
680 PR=RND(11):M(0,PR)=3
690 PUT(0,H(PR))-(15,H(PR)+15),P,PSET
700 RETURN
710 REM INIT GAME
720 L=1:S=0:M=3
730 RETURN
740 REM LEVEL SET UP
750 X=5:Y=2:V=1:W=1:T=0:PR=0
760 GOSUB810
770 IFL>2THENI=L+2:FORJ=1TOI:O=RND(16):R=RND(11):M(O,R)=1:PUT(H(O),H(R))-(H(O)+15,H(R)+15),R,PSET:NEXT
780 M(X,Y)=0: REM ENSURE STARTING POSITION CLEAR
790 RETURN
800 REM RESET LEVEL DATA - SCREEN LAYOUT AND LAVA
810 FORJ=0TO16:FORI=0TO11:M(J,I)=0:NEXT:NEXT
820 FORI=0TO11:N(I)=16:NEXT
830 RETURN
840 REM DRAW GFX
850 PMODE0,1:PCLS:SCREEN1,1
860 GET(0,0)-(15,15),B
870 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
880 GET(0,0)-(15,15),A
890 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
900 GET(0,0)-(15,15),R
910 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
920 GET(0,0)-(15,15),C
930 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
940 GET(0,0)-(15,15),P
950 RETURN
960 REM TITLE SCREEN
970 PCLS:LINE(0,176)-(255,176),PSET:LINE(0,15)-(255,15),PSET
980 HP=87:VP=40:O$="CASH@GRAB":GOSUB1410:PLAY"T255AO3AO2GG"
990 HP=85:VP=180:O$="VERSION":GOSUB1410
1000 HP=145:VP=180:N$="0":GOSUB1440
1010 HP=157:VP=180:N$="2":GOSUB1440:PSET(153,188,1):PLAY"T255AO3AO2GG"
1020 HP=110:VP=60:O$="BY":GOSUB1410
1030 HP=70:VP=80:O$="DAVY@MITCHELL":GOSUB1410:PLAY"T255AO3AO2GG"
1040 X=7:Y=7:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET
1050 X=5:Y=7:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),C,PSET
1060 X=9:Y=7:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),C,PSET
1070 GOSUB1300
1080 RETURN
1090 REM LEGEND SCREEN
1100 LINE(14,30)-(200,170),PRESET,BF
1110 X=5:Y=2:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET
1120 X=5:Y=4:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),C,PSET
1130 X=5:Y=6:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),P,PSET
1140 X=5:Y=8:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),R,PSET
1150 HP=16*8:VP=8*4.25:O$="THE@HERO":GOSUB1410
1160 HP=16*8:VP=8*8.25:O$="CASH":GOSUB1410
1170 HP=16*8:VP=8*12.25:O$="PORTAL":GOSUB1410
1180 HP=16*8:VP=8*16.25:O$="LAVA":GOSUB1410
1190 GOSUB1300
1200 RETURN
1210 REM GAME OVER SCREEN
1220 PCLS:LINE(0,176)-(255,176),PSET:LINE(0,15)-(255,15),PSET
1230 HP=91:VP=40:O$="GAME@OVER": GOSUB1410
1240 HP=87:VP=80:O$="YOU@SCORED": GOSUB1410
1250 HP=INT((255-LEN(STR$(S)))/2):VP=100:N$=MID$(STR$(S),2):GOSUB1440
1260 X=6:Y=6:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),C,PSET
1270 A$=INKEY$:FORD=1TO4000:NEXT:GOSUB1300
1280 RETURN
1290 REM AWAIT INPUT
1300 A$=INKEY$:IFA$=""THEN1300
1310 RETURN
1320 REM STICK MAN
1330 DATA24,60,24,126,24,24,36,66
1340 REM LAVA
1350 DATA97,95,204,32,244,31,120,30
1360 REM CASH
1370 DATA0,0,60,24,60,126,126,60
1380 REM PORTAL
1390 DATA126,129,189,165,165,189,129,126
1400 REM DISPLAY TEXT
1410 FORQ=1TOLEN(O$):DRAW"BM"+STR$(HP)+","+STR$(VP)+";"+C$(ASC(MID$(O$,Q,1))-64):HP=HP+8:NEXT
1420 RETURN
1430 REM DISPLAY NUMBERS
1440 FORQ=1TOLEN(N$):DRAW"BM"+STR$(HP)+","+STR$(VP)+";"+N$(ASC(MID$(N$,Q,1))-48):HP=HP+8:NEXT
1450 RETURN
1460 REM LETTER DATA
1470 C$(1)="D8U8R4D4L4R4D4"
1480 C$(2)="R4D8L4U8D4R4"
1490 C$(3)="R4L4D8R4"
1500 C$(4)="D8R3E1U6M-4,-1"
1510 C$(5)="R4L4D4R4L4D4R4"
1520 C$(6)="R4L4D4R4L4D4"
1530 C$(7)="R4L4D8R4U4"
1540 C$(8)="D8U4R4U4D8"
1550 C$(9)="R4L2D8R2L4"
1560 C$(10)="R4L2D8L2"
1570 C$(11)="D8U4M+4,-4M-4,+4M+4,+4"
1580 C$(12)="D8R4"
1590 C$(13)="D8U8M+2,+2M+2,-2D8"
1600 C$(14)="D8U8M+4,+8U8"
1610 C$(15)="D8R4U8L4"
1620 C$(16)="D8U8R4D4L4"
1630 C$(17)="D6R4D2U8L4"
1640 C$(18)="D8U8R4D4L4F4"
1650 C$(19)="R4L4D4R4D4L4"
1660 C$(20)="R4L2D8"
1670 C$(21)="D8R4U8"
1680 C$(22)="D6F2E2U6"
1690 C$(23)="D6F2E2U6D6F2E2U6"
1700 C$(24)="M+4,+8BM+0,-8M-4,+8"
1710 C$(25)="D4R4U4D4L2D4"
1720 C$(26)="R4D2G2G2D2R4"
1730 REM NUMBERS
1740 N$(0)="D8R4U8L4"
1750 N$(1)="BM+2,+0D8"
1760 N$(2)="D2U2R4D4G4R4"
1770 N$(3)="R4D4L4R4D4L4"
1780 N$(4)="D4R4U4D8"
1790 N$(5)="R4L4D4R4D4L4"
1800 N$(6)="R4L4D4R4D4L4U4"
1810 N$(7)="R4D8"
1820 N$(8)="R4D8L4U8D4R4"
1830 N$(9)="R4D8U4L4U4"
1840 REM OUCH
1850 OU$(1)="THAT@STUFF@IS@HOT"
1860 OU$(2)="OUCH@OUCH@"
1870 OU$(3)="FEEL@THE@BURN"
1880 OU$(4)="BAD@CARPET"
1890 OU$(5)="SCORCHIO"
1900 OU$(6)="SMOKEY@SHOES"
1910 OU$(7)="@HOT@HOT@HOT"
1920 OU$(8)="BURN@BABY@BURN@"
1930 RETURN
1940 REM