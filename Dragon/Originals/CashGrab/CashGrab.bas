10 DIMH(16),A(1),B(1),C(1),P(1),R(1),M(16,11),N(11),X,Y,V,W,D,J,O,R,L,S,M,Q,HP,VP,CL,A$,T,PR,OU$(8),HS
20 DIMC$(27),N$(10):GOSUB1740:REM SET UP TEXT
30 FORI=0TO15:H(I)=I*16:NEXT
40 GOSUB860:REM SET UP GRAPHICS
50 GOSUB980:REM TITLE SCREEN
60 GOSUB1320:REM LEGEND SCREEN
70 GOSUB720:REM INIT GAME
80 GOSUB750:REM INIT LEVEL
90 PCLS:GOSUB450
100 REM GAME LOOP
110 A$=INKEY$:IFA$="P"THENX=X+1ELSEIFA$="O"THENX=X-1
120 IFA$="Q"THENY=Y-1ELSEIFA$="A"THENY=Y+1
130 J=JOYSTK(0):IFJ=0THENX=X-1ELSEIFJ=63THENX=X+1
140 J=JOYSTK(1):IFJ=0THENY=Y-1ELSEIFJ=63THENY=Y+1
150 IFX<0THENX=0ELSEIFX>15THENX=15
160 IFY<1THENY=1ELSEIFY>11THENY=11
170 GOSUB220
180 IFX=V THEN IF Y=W THEN200
190 PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET:PUT(H(V),H(W))-(H(V)+15,H(W)+15),B,PSET:V=X:W=Y
200 ON M(X,Y) GOSUB300,360,390:IF M THEN110
210 GOSUB1440:GOTO50
220 REM DRAW SCREEN UPDATES
230 R=RND(11):IF N(R) THENN(R)=N(R)-1:O=N(R):M(O,R)=1:PUT(H(O),H(R))-(H(O)+15,H(R)+15),R,PSET
240 IF PR THEN PUT(0,H(PR))-(15,H(PR)+15),P,NOT
250 IFRND(10)>1THENRETURN
260 O=2+RND(14):R=1+RND(9):IFT<3THEN O=2+RND(7)
270 IF H(O) AND M(O,R)=0 THEN M(O,R)=2:PUT(H(O),H(R))-(H(O)+15,H(R)+15),C,PSET
280 RETURN
290 REM BURN
300 SCREEN1,0:PLAY"T255AEAEAAAAEEA":SCREEN1,1
310 O$=OU$(RND(8)):HP=INT((255-(LEN(O$)*8))/2):VP=100:
320 LINE(0,VP-2)-(255,VP+10),PRESET,BF:GOSUB1680:PLAY"T15AEAEAAAAEEA"
330 M=M-1:IF M THENGOSUB600:GOSUB450:GOSUB760ELSEGOTO340
340 RETURN
350 REM SCORE
360 M(X,Y)=0:S=S+10+X:GOSUB550:PLAY"O3;T255BCDGG;O2":GOSUB260:T=T+1:IFT=3THENGOSUB680
370 RETURN
380 REM PORTAL AND NEXT LEVEL
390 L=L+1:T=0:PLAY"O3;V15;T194;GFEDCBAAAAAAAO2AAAAV9AAGFEDCBAAAAAAAO2AAAAV8AAGFEDCBAAAAAAAO2AAAAV7AA;V15"
400 O$="NEXT@LEVEL":HP=INT((255-(LEN(O$)*8))/2):VP=100:
410 LINE(0,VP-2)-(255,VP+10),PRESET,BF:GOSUB1680
420 GOSUB570:GOSUB640:GOSUB760
430 RETURN
440 REM DRAW INITIAL SCREEN
450 LINE(0,15)-(255,15),PSET
460 HP=6:VP=2:O$="LIVES": GOSUB1680
470 HP=84:VP=2:O$="SCORE": GOSUB1680
480 HP=180:VP=2:O$="LEVEL": GOSUB1680
490 GOSUB520:GOSUB550:GOSUB570
500 RETURN
510 REM UPDATE LIVES DISPLAY
520 LINE(55,2)-(64,10),PRESET,BF:HP=55:VP=2:N$=MID$(STR$(M),2):GOSUB1710
530 RETURN
540 REM UPDATE SCORE DISPLAY
550 LINE(135,2)-(160,10),PRESET,BF:HP=135:VP=2:N$=MID$(STR$(S),2):FORQ=1TOLEN(N$):DRAW"BM"+STR$(HP)+",2"+N$(ASC(MID$(N$,Q,1))-48):HP=HP+8:NEXT:RETURN
560 REM UPDATE LEVEL DISPLAY
570 LINE(231,2)-(240,10),PRESET,BF:HP=231:VP=2:N$=MID$(STR$(L),2):GOSUB1710
580 RETURN
590 REM CLEAR PLAYFIELD HORIZONTAL
600 FORCL=16TO192STEP32:LINE(0,CL)-(255,CL+16),PRESET,BF:PLAY"T255BAA":NEXT
610 FORCL=32TO192STEP32:LINE(0,CL)-(255,CL+16),PRESET,BF:PLAY"T255ABB":NEXT
620 RETURN
630 REM CLEAR PLAYFIELD VERTICAL
640 FORCL=0TO255STEP32:LINE(CL,16)-(CL+16,192),PRESET,BF:PLAY"T255CDD":NEXT
650 FORCL=16TO255STEP32:LINE(CL,16)-(CL+16,192),PRESET,BF:PLAY"T255GFF":NEXT
660 RETURN
670 REM POSITION AND DISPLAY PORTAL
680 PR=RND(11):M(0,PR)=3
690 PUT(0,H(PR))-(15,H(PR)+15),P,PSET
700 PLAY"O4;V15;T255;AEEEEV10EEEV13DCGG;O2;V15"
710 RETURN
720 REM INIT GAME
730 L=1:S=0:M=3
740 RETURN
750 REM LEVEL SET UP
760 X=5:Y=2:V=1:W=1:T=0:PR=0
770 GOSUB820
780 IFL>2THENI=L+2:FORJ=1TOI:O=RND(16):R=RND(11):M(O,R)=1:PUT(H(O),H(R))-(H(O)+15,H(R)+15),R,PSET:NEXT
790 M(X,Y)=0: REM ENSURE STARTING POSITION CLEAR
800 RETURN
810 REM RESET LEVEL DATA - SCREEN LAYOUT AND LAVA
820 FORJ=0TO16:FORI=0TO11:M(J,I)=0:NEXT:NEXT
830 FORI=0TO11:N(I)=16:NEXT
840 RETURN
850 REM DRAW GFX
860 PMODE0,1:PCLS:SCREEN1,1
870 GET(0,0)-(15,15),B
880 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
890 GET(0,0)-(15,15),A
900 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
910 GET(0,0)-(15,15),R
920 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
930 GET(0,0)-(15,15),C
940 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
950 GET(0,0)-(15,15),P
960 RETURN
970 REM TITLE SCREEN
980 PCLS:LINE(0,176)-(255,176),PSET:LINE(0,15)-(255,15),PSET
990 HP=87:VP=40:O$="CASH@GRAB":GOSUB1680:PLAY"T255AO3AO2GG"
1000 HP=85:VP=180:O$="VERSION":GOSUB1680
1010 HP=145:VP=180:N$="0":GOSUB1710
1020 HP=157:VP=180:N$="9":GOSUB1710:PSET(153,188,1):PLAY"T255AO3AO2GG"
1030 HP=110:VP=60:O$="BY":GOSUB1680
1040 HP=70:VP=80:O$="DAVY@MITCHELL":GOSUB1680:PLAY"T255AO3AO2GG"
1050 Y=7:DE=125
1060 X=7:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET
1070 X=5:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),C,PSET
1080 X=9:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),P,PSET
1090 GOSUB1570
1100 FORD=1TODE:NEXT
1110 X=7:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),B,PSET
1120 X=6:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET
1130 FORD=1TODE:NEXT
1140 X=6:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),B,PSET
1150 X=5:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET
1160 PLAY"O4;V15;T255;AEEEEEEEEEDCGGG"
1170 X=9:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),P,NOT
1180 FORD=1TODE:NEXT
1190 X=5:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),B,PSET
1200 X=6:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET
1210 FORD=1TODE:NEXT
1220 X=6:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),B,PSET
1230 X=7:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET
1240 FORD=1TODE:NEXT
1250 X=7:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),B,PSET
1260 X=8:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET
1270 FORD=1TODE:NEXT
1280 X=8:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),B,PSET
1290 X=9:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET
1300 PLAY"O3;V15;T255;GFEDCBAO2AV9AGFEDCBAO2AV8AAGFEDCBAO2AV7A;O2"
1310 RETURN
1320 REM LEGEND SCREEN
1330 LINE(14,30)-(200,170),PRESET,BF
1340 X=5:Y=2:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET
1350 X=5:Y=4:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),C,PSET
1360 X=5:Y=6:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),P,PSET
1370 X=5:Y=8:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),R,PSET
1380 HP=16*8:VP=8*4.25:O$="THE@HERO":GOSUB1680
1390 HP=16*8:VP=8*8.25:O$="CASH":GOSUB1680
1400 HP=16*8:VP=8*12.25:O$="PORTAL":GOSUB1680
1410 HP=16*8:VP=8*16.25:O$="LAVA":GOSUB1680
1420 GOSUB1570
1430 RETURN
1440 REM GAME OVER SCREEN
1450 PCLS:LINE(0,176)-(255,176),PSET:LINE(0,15)-(255,15),PSET
1460 HP=91:VP=40:O$="GAME@OVER": GOSUB1680
1470 HP=87:VP=80:O$="YOU@SCORED": GOSUB1680
1480 HP=INT((255-LEN(STR$(S)))/2):VP=100:N$=MID$(STR$(S),2):GOSUB1710
1490 X=6:Y=6:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),C,PSET
1500 IF HS<S THEN HS=S:REM SFX HERE
1510 HP=87:VP=140:O$="HIGH@SCORE": GOSUB1680
1520 HP=INT((255-LEN(STR$(HS)))/2):VP=160:N$=MID$(STR$(HS),2):GOSUB1710
1530 X=6:Y=10:PUT(H(X),H(Y)-4)-(H(X)+15,H(Y)+11),A,PSET
1540 A$=INKEY$:FORD=1TO4000:NEXT:GOSUB1570
1550 RETURN
1560 REM AWAIT INPUT
1570 A$=INKEY$:IFA$=""THEN1570
1580 RETURN
1590 REM STICK MAN
1600 DATA24,60,24,126,24,24,36,66
1610 REM LAVA
1620 DATA97,95,204,32,244,31,120,30
1630 REM CASH
1640 DATA0,0,60,24,60,126,126,60
1650 REM PORTAL
1660 DATA126,129,189,165,165,189,129,126
1670 REM DISPLAY TEXT
1680 FORQ=1TOLEN(O$):DRAW"BM"+STR$(HP)+","+STR$(VP)+";"+C$(ASC(MID$(O$,Q,1))-64):HP=HP+8:NEXT
1690 RETURN
1700 REM DISPLAY NUMBERS
1710 FORQ=1TOLEN(N$):DRAW"BM"+STR$(HP)+","+STR$(VP)+";"+N$(ASC(MID$(N$,Q,1))-48):HP=HP+8:NEXT
1720 RETURN
1730 REM LETTER DATA
1740 C$(1)="D8U8R4D4L4R4D4"
1750 C$(2)="R4D8L4U8D4R4"
1760 C$(3)="R4L4D8R4"
1770 C$(4)="D8R3E1U6M-4,-1"
1780 C$(5)="R4L4D4R4L4D4R4"
1790 C$(6)="R4L4D4R4L4D4"
1800 C$(7)="R4L4D8R4U4"
1810 C$(8)="D8U4R4U4D8"
1820 C$(9)="R4L2D8R2L4"
1830 C$(10)="R4L2D8L2"
1840 C$(11)="D8U4M+4,-4M-4,+4M+4,+4"
1850 C$(12)="D8R4"
1860 C$(13)="D8U8M+2,+2M+2,-2D8"
1870 C$(14)="D8U8M+4,+8U8"
1880 C$(15)="D8R4U8L4"
1890 C$(16)="D8U8R4D4L4"
1900 C$(17)="D6R4D2U8L4"
1910 C$(18)="D8U8R4D4L4F4"
1920 C$(19)="R4L4D4R4D4L4"
1930 C$(20)="R4L2D8"
1940 C$(21)="D8R4U8"
1950 C$(22)="D6F2E2U6"
1960 C$(23)="D6F2E2U6D6F2E2U6"
1970 C$(24)="M+4,+8BM+0,-8M-4,+8"
1980 C$(25)="D4R4U4D4L2D4"
1990 C$(26)="R4D2G2G2D2R4"
2000 REM NUMBERS
2010 N$(0)="D8R4U8L4"
2020 N$(1)="BM+2,+0D8"
2030 N$(2)="D2U2R4D4G4R4"
2040 N$(3)="R4D4L4R4D4L4"
2050 N$(4)="D4R4U4D8"
2060 N$(5)="R4L4D4R4D4L4"
2070 N$(6)="R4L4D4R4D4L4U4"
2080 N$(7)="R4D8"
2090 N$(8)="R4D8L4U8D4R4"
2100 N$(9)="R4D8U4L4U4"
2110 REM OUCH
2120 OU$(1)="THAT@STUFF@IS@HOT"
2130 OU$(2)="OUCH@OUCH@"
2140 OU$(3)="FEEL@THE@BURN"
2150 OU$(4)="BAD@CARPET"
2160 OU$(5)="SCORCHIO"
2170 OU$(6)="SMOKEY@SHOES"
2180 OU$(7)="@HOT@HOT@HOT"
2190 OU$(8)="BURN@BABY@BURN@"
2200 RETURN
2210 REM