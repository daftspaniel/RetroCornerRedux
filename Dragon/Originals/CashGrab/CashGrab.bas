10 DIMH(16),A(1),B(1),C(1),P(1),R(1),M(16,11),N(11),X,Y,V,W,D,J,O,R,L,S,M,Q,HP,VP,CL,A$,T,PR,OU$(8)
20 DIMC$(27),N$(10):GOSUB1260:REM SET UP TEXT
30 FORI=0TO15:H(I)=I*16:NEXT
40 GOSUB780:REM SET UP GRAPHICS
50 GOSUB900:REM TITLE SCREEN
60 GOSUB640:REM INIT GAME
70 GOSUB670:REM INIT LEVEL
80 PCLS:GOSUB420
90 REM GAME LOOP
100 A$=INKEY$:IFA$="P"THENX=X+1ELSEIFA$="O"THENX=X-1
110 IFA$="Q"THENY=Y-1ELSEIFA$="A"THENY=Y+1
120 J=JOYSTK(0):IFJ=0THENX=X-1ELSEIFJ=63THENX=X+1
130 J=JOYSTK(1):IFJ=0THENY=Y-1ELSEIFJ=63THENY=Y+1
140 IFX<0THENX=0ELSEIFX>15THENX=15
150 IFY<1THENY=1ELSEIFY>11THENY=11
160 GOSUB210
170 IFX=V THEN IF Y=W THEN190
180 PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET:PUT(H(V),H(W))-(H(V)+15,H(W)+15),B,PSET:V=X:W=Y
190 ON M(X,Y) GOSUB290,350,380:IF M THEN100
200 GOSUB1000:GOTO50
210 REM DRAW SCREEN UPDATES
220 R=RND(11):IF N(R) THENN(R)=N(R)-1:O=N(R):M(O,R)=1:PUT(H(O),H(R))-(H(O)+15,H(R)+15),R,PSET
230 IF PR THEN PUT(0,H(PR))-(15,H(PR)+15),P,NOT
240 IFRND(10)>1THENRETURN
250 O=2+RND(14):R=1+RND(9):IFT<3THEN O=2+RND(7)
260 IF H(O) AND M(O,R)=0 THEN M(O,R)=2:PUT(H(O),H(R))-(H(O)+15,H(R)+15),C,PSET
270 RETURN
280 REM BURN
290 SCREEN1,0:PLAY"T255AEAEAAAAEEA":SCREEN1,1
300 O$=OU$(RND(8)):HP=INT((255-(LEN(O$)*8))/2):VP=100:
310 LINE(0,VP-2)-(255,VP+10),PRESET,BF:GOSUB1200:PLAY"T15AEAEAAAAEEA"
320 M=M-1:IF M THENGOSUB570:GOSUB420:GOSUB680ELSEGOTO330
330 RETURN
340 REM SCORE
350 M(X,Y)=0:S=S+10+X:GOSUB520:PLAY"T255BCDGG":GOSUB250:T=T+1:IFT=3THENGOSUB610
360 RETURN
370 REM PORTAL AND NEXT LEVEL
380 L=L+1:T=0:PLAY"O3;V15;T194;GFEDCBAAAAAAAO2AAAAV9AAGFEDCBAAAAAAAO2AAAAV8AAGFEDCBAAAAAAAO2AAAAV7AA;V15":GOSUB570
390 GOSUB540:GOSUB680
400 RETURN
410 REM DRAW INITIAL SCREEN
420 LINE(0,15)-(255,15),PSET
430 HP=6:VP=2:O$="LIVES": GOSUB1200
440 HP=84:VP=2:O$="SCORE": GOSUB1200
450 HP=180:VP=2:O$="LEVEL": GOSUB1200
460 GOSUB490:GOSUB520:GOSUB540
470 RETURN
480 REM UPDATE LIVES DISPLAY
490 LINE(55,2)-(64,10),PRESET,BF:HP=55:VP=2:N$=MID$(STR$(M),2):GOSUB1230
500 RETURN
510 REM UPDATE SCORE DISPLAY
520 LINE(135,2)-(160,10),PRESET,BF:HP=135:VP=2:N$=MID$(STR$(S),2):FORQ=1TOLEN(N$):DRAW"BM"+STR$(HP)+",2"+N$(ASC(MID$(N$,Q,1))-48):HP=HP+8:NEXT:RETURN
530 REM UPDATE LEVEL DISPLAY
540 LINE(231,2)-(240,10),PRESET,BF:HP=231:VP=2:N$=MID$(STR$(L),2):GOSUB1230
550 RETURN
560 REM CLEAR PLAYFIELD HORIZONTAL
570 FORCL=16TO192STEP32:LINE(0,CL)-(255,CL+16),PRESET,BF:PLAY"T255BAAA":NEXT
580 FORCL=32TO192STEP32:LINE(0,CL)-(255,CL+16),PRESET,BF:PLAY"T255ABBB":NEXT
590 RETURN
600 REM POSITION AND DISPLAY PORTAL
610 PR=RND(11):M(0,PR)=3
620 PUT(0,H(PR))-(15,H(PR)+15),P,PSET
630 RETURN
640 REM INIT GAME
650 L=1:S=0:M=3
660 RETURN
670 REM LEVEL SET UP
680 X=5:Y=2:V=1:W=1:T=0:PR=0
690 GOSUB740
700 IFL>2THENI=L+2:FORJ=1TOI:O=RND(16):R=RND(11):M(O,R)=1:PUT(H(O),H(R))-(H(O)+15,H(R)+15),R,PSET:SOUND255,1:NEXT
710 M(X,Y)=0: REM ENSURE STARTING POSITION CLEAR
720 RETURN
730 REM RESET LEVEL DATA - SCREEN LAYOUT AND LAVA
740 FORJ=0TO16:FORI=0TO11:M(J,I)=0:NEXT:NEXT
750 FORI=0TO11:N(I)=16:NEXT
760 RETURN
770 REM DRAW GFX
780 PMODE0,1:PCLS:SCREEN1,1
790 GET(0,0)-(15,15),B
800 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
810 GET(0,0)-(15,15),A
820 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
830 GET(0,0)-(15,15),R
840 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
850 GET(0,0)-(15,15),C
860 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
870 GET(0,0)-(15,15),P
880 RETURN
890 REM TITLE SCREEN
900 PCLS:LINE(0,176)-(255,176),PSET:LINE(0,15)-(255,15),PSET
910 HP=87:VP=40:O$="CASH@GRAB":GOSUB1200:PLAY"T255AO3AO2GG"
920 HP=85:VP=180:O$="VERSION":GOSUB1200
930 HP=145:VP=180:N$="0":GOSUB1230
940 HP=157:VP=180:N$="2":GOSUB1230:PSET(153,188,1):PLAY"T255AO3AO2GG"
950 HP=110:VP=60:O$="BY":GOSUB1200
960 HP=70:VP=80:O$="DAVY@MITCHELL":GOSUB1200:PLAY"T255AO3AO2GG"
970 X=7:Y=7:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET
980 GOSUB1090
990 RETURN
1000 REM GAME OVER SCREEN
1010 PCLS:LINE(0,176)-(255,176),PSET:LINE(0,15)-(255,15),PSET
1020 HP=91:VP=40:O$="GAME@OVER": GOSUB1200
1030 HP=87:VP=80:O$="YOU@SCORED": GOSUB1200
1040 HP=INT((255-LEN(STR$(S)))/2):VP=100:O$=MID$(STR$(S),2):GOSUB1230
1050 X=6:Y=6:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),C,PSET
1060 A$=INKEY$:FORD=1TO4000:NEXT:GOSUB1090
1070 RETURN
1080 REM AWAIT INPUT
1090 A$=INKEY$:IFA$=""THEN1090
1100 RETURN
1110 REM STICK MAN
1120 DATA24,60,24,126,24,24,36,66
1130 REM LAVA
1140 DATA97,95,204,32,244,31,120,30
1150 REM CASH
1160 DATA0,0,60,24,60,126,126,60
1170 REM PORTAL
1180 DATA126,129,189,165,165,189,129,126
1190 REM DISPLAY TEXT
1200 FORQ=1TOLEN(O$):DRAW"BM"+STR$(HP)+","+STR$(VP)+";"+C$(ASC(MID$(O$,Q,1))-64):HP=HP+8:NEXT
1210 RETURN
1220 REM DISPLAY NUMBERS
1230 FORQ=1TOLEN(N$):DRAW"BM"+STR$(HP)+","+STR$(VP)+";"+N$(ASC(MID$(N$,Q,1))-48):HP=HP+8:NEXT
1240 RETURN
1250 REM LETTER DATA
1260 C$(1)="D8U8R4D4L4R4D4"
1270 C$(2)="R4D8L4U8D4R4"
1280 C$(3)="R4L4D8R4"
1290 C$(4)="D8R3E1U6M-4,-1"
1300 C$(5)="R4L4D4R4L4D4R4"
1310 C$(6)="R4L4D4R4L4D4"
1320 C$(7)="R4L4D8R4U4"
1330 C$(8)="D8U4R4U4D8"
1340 C$(9)="R4L2D8R2L4"
1350 C$(10)="R4L2D8L2"
1360 C$(11)="D8U4M+4,-4M-4,+4M+4,+4"
1370 C$(12)="D8R4"
1380 C$(13)="D8U8M+2,+2M+2,-2D8"
1390 C$(14)="D8U8M+4,+8U8"
1400 C$(15)="D8R4U8L4"
1410 C$(16)="D8U8R4D4L4"
1420 C$(17)="D6R4D2U8L4"
1430 C$(18)="D8U8R4D4L4F4"
1440 C$(19)="R4L4D4R4D4L4"
1450 C$(20)="R4L2D8"
1460 C$(21)="D8R4U8"
1470 C$(22)="D6F2E2U6"
1480 C$(23)="D6F2E2U6D6F2E2U6"
1490 C$(24)="M+4,+8BM+0,-8M-4,+8"
1500 C$(25)="D4R4U4D4L2D4"
1510 C$(26)="R4D2G2G2D2R4"
1520 REM NUMBERS
1530 N$(0)="D8R4U8L4"
1540 N$(1)="BM+2,+0D8"
1550 N$(2)="D2U2R4D4G4R4"
1560 N$(3)="R4D4L4R4D4L4"
1570 N$(4)="D4R4U4D8"
1580 N$(5)="R4L4D4R4D4L4"
1590 N$(6)="R4L4D4R4D4L4U4"
1600 N$(7)="R4D8"
1610 N$(8)="R4D8L4U8D4R4"
1620 N$(9)="R4D8U4L4U4"
1630 REM OUCH
1640 OU$(1)="HEY@THAT@STUFF@IS@HOT"
1650 OU$(2)="OUCH@OUCH@OUCH@OUCH"
1660 OU$(3)="FEELING@THE@BURN"
1670 OU$(4)="LAVA@IS@BAD@CARPET"
1680 OU$(5)="DO@NOT@TREAD@ON@LAVA"
1690 OU$(6)="I@NEED@NEW@SHOES"
1700 OU$(7)="FEELING@HOT@HOT@HOT"
1710 OU$(8)="BURN@BABY@BURN@"
1720 RETURN
1730 REM