10 DIMH(16),A(1),B(1),C(1),P(1),R(1),M(16,11),N(11),X,Y,V,W,D,J,O,R,L,S,M,Q,HP,VP,CL,A$,T,PR,OU$(8),HS
20 DIMC$(27),N$(10):GOSUB1720:REM SET UP TEXT
30 FORI=0TO15:H(I)=I*16:NEXT
40 GOSUB840:REM SET UP GRAPHICS
50 GOSUB960:REM TITLE SCREEN
60 GOSUB1300:REM LEGEND SCREEN
70 GOSUB700:REM INIT GAME
80 GOSUB730:REM INIT LEVEL
90 PCLS:GOSUB430
100 REM GAME LOOP
110 J=JOYSTK(0):IFJ=0THENX=X-1ELSEIFJ=63THENX=X+1
120 J=JOYSTK(1):IFJ=0THENY=Y-1ELSEIFJ=63THENY=Y+1
130 IFX<0THENX=0ELSEIFX>15THENX=15
140 IFY<1THENY=1ELSEIFY>11THENY=11
150 GOSUB210
160 IFX=V THEN IF Y=W THEN180
170 PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET:PUT(H(V),H(W))-(H(V)+15,H(W)+15),B,PSET:V=X:W=Y
180 ON M(X,Y) GOSUB280,340,370:IF M THEN110
190 GOSUB1420:GOTO50
200 REM DRAW SCREEN UPDATES
210 R=RND(11):IF N(R) THENN(R)=N(R)-1:O=N(R):M(O,R)=1:PUT(H(O),H(R))-(H(O)+15,H(R)+15),R,PSET
220 IF PR THEN PUT(0,H(PR))-(15,H(PR)+15),P,NOT
230 IFRND(10)>1THENRETURN
240 O=2+RND(14):R=1+RND(9):IFT<3THEN O=2+RND(7)
250 IF H(O) AND M(O,R)=0 THEN M(O,R)=2:PUT(H(O),H(R))-(H(O)+15,H(R)+15),C,PSET
260 RETURN
270 REM BURN
280 SCREEN1,0:PLAY"T255AEAEAAAAEEA":SCREEN1,1
290 O$=OU$(RND(8)):HP=INT((255-(LEN(O$)*8))/2):VP=100:
300 LINE(0,VP-2)-(255,VP+10),PRESET,BF:GOSUB1660:PLAY"T15AEAEAAAAEEA"
310 M=M-1:IF M THENGOSUB580:GOSUB430:GOSUB740ELSEGOTO320
320 RETURN
330 REM SCORE
340 M(X,Y)=0:S=S+10+X:GOSUB530:PLAY"O3;T255BCDGG;O2":GOSUB240:T=T+1:IFT=3THENGOSUB660
350 RETURN
360 REM PORTAL AND NEXT LEVEL
370 L=L+1:T=0:PLAY"O3;V15;T194;GFEDCBAAAAAAAO2AAAAV9AAGFEDCBAAAAAAAO2AAAAV8AAGFEDCBAAAAAAAO2AAAAV7AA;V15"
380 O$="NEXT@LEVEL":HP=INT((255-(LEN(O$)*8))/2):VP=100:
390 LINE(0,VP-2)-(255,VP+10),PRESET,BF:GOSUB1660
400 GOSUB550:GOSUB620:GOSUB740
410 RETURN
420 REM DRAW INITIAL SCREEN
430 LINE(0,15)-(255,15),PSET
440 HP=6:VP=2:O$="LIVES": GOSUB1660
450 HP=84:VP=2:O$="SCORE": GOSUB1660
460 HP=180:VP=2:O$="LEVEL": GOSUB1660
470 GOSUB500:GOSUB530:GOSUB550
480 RETURN
490 REM UPDATE LIVES DISPLAY
500 LINE(55,2)-(64,10),PRESET,BF:HP=55:VP=2:N$=MID$(STR$(M),2):GOSUB1690
510 RETURN
520 REM UPDATE SCORE DISPLAY
530 LINE(135,2)-(160,10),PRESET,BF:HP=135:VP=2:N$=MID$(STR$(S),2):FORQ=1TOLEN(N$):DRAW"BM"+STR$(HP)+",2"+N$(ASC(MID$(N$,Q,1))-48):HP=HP+8:NEXT:RETURN
540 REM UPDATE LEVEL DISPLAY
550 LINE(231,2)-(240,10),PRESET,BF:HP=231:VP=2:N$=MID$(STR$(L),2):GOSUB1690
560 RETURN
570 REM CLEAR PLAYFIELD HORIZONTAL
580 FORCL=16TO192STEP32:LINE(0,CL)-(255,CL+16),PRESET,BF:PLAY"T255BAA":NEXT
590 FORCL=32TO192STEP32:LINE(0,CL)-(255,CL+16),PRESET,BF:PLAY"T255ABB":NEXT
600 RETURN
610 REM CLEAR PLAYFIELD VERTICAL
620 FORCL=0TO255STEP32:LINE(CL,16)-(CL+16,192),PRESET,BF:PLAY"T255CDD":NEXT
630 FORCL=16TO255STEP32:LINE(CL,16)-(CL+16,192),PRESET,BF:PLAY"T255GFF":NEXT
640 RETURN
650 REM POSITION AND DISPLAY PORTAL
660 PR=RND(11):M(0,PR)=3
670 PUT(0,H(PR))-(15,H(PR)+15),P,PSET
680 PLAY"O4;V15;T255;AEEEEV10EEEV13DCGG;O2;V15"
690 RETURN
700 REM INIT GAME
710 L=1:S=0:M=3
720 RETURN
730 REM LEVEL SET UP
740 X=5:Y=2:V=1:W=1:T=0:PR=0
750 GOSUB800
760 IFL>2THENI=L+2:FORJ=1TOI:O=RND(16):R=RND(11):M(O,R)=1:PUT(H(O),H(R))-(H(O)+15,H(R)+15),R,PSET:NEXT
770 M(X,Y)=0: REM ENSURE STARTING POSITION CLEAR
780 RETURN
790 REM RESET LEVEL DATA - SCREEN LAYOUT AND LAVA
800 FORJ=0TO16:FORI=0TO11:M(J,I)=0:NEXT:NEXT
810 FORI=0TO11:N(I)=16:NEXT
820 RETURN
830 REM DRAW GFX
840 PMODE0,1:PCLS:SCREEN1,1
845 AD=PEEK(186)*256+PEEK(187):REM FIND GFX SCREEN ADDRESS
850 GET(0,0)-(15,15),B
860 FORL=0TO7:READD:POKEAD+(L*16),D:NEXT
870 GET(0,0)-(15,15),A
880 FORL=0TO7:READD:POKEAD+(L*16),D:NEXT
890 GET(0,0)-(15,15),R
900 FORL=0TO7:READD:POKEAD+(L*16),D:NEXT
910 GET(0,0)-(15,15),C
920 FORL=0TO7:READD:POKEAD+(L*16),D:NEXT
930 GET(0,0)-(15,15),P
940 RETURN
950 REM TITLE SCREEN
960 PCLS:LINE(0,176)-(255,176),PSET:LINE(0,15)-(255,15),PSET
970 HP=87:VP=40:O$="CASH@GRAB":GOSUB1660:PLAY"T255AO3AO2GG"
980 HP=85:VP=180:O$="VERSION":GOSUB1660
990 HP=145:VP=180:N$="1":GOSUB1690
1000 HP=157:VP=180:N$="0":GOSUB1690:PSET(153,188,1):PLAY"T255AO3AO2GG"
1010 HP=110:VP=60:O$="BY":GOSUB1660
1020 HP=70:VP=80:O$="DAVY@MITCHELL":GOSUB1660:PLAY"T255AO3AO2GG"
1030 Y=7:DE=125
1040 X=7:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET
1050 X=5:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),C,PSET
1060 X=9:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),P,PSET
1070 GOSUB1550
1080 FORD=1TODE:NEXT
1090 X=7:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),B,PSET
1100 X=6:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET
1110 FORD=1TODE:NEXT
1120 X=6:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),B,PSET
1130 X=5:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET
1140 PLAY"O4;V15;T255;AEEEEEEEEEDCGGG"
1150 X=9:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),P,NOT
1160 FORD=1TODE:NEXT
1170 X=5:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),B,PSET
1180 X=6:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET
1190 FORD=1TODE:NEXT
1200 X=6:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),B,PSET
1210 X=7:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET
1220 FORD=1TODE:NEXT
1230 X=7:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),B,PSET
1240 X=8:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET
1250 FORD=1TODE:NEXT
1260 X=8:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),B,PSET
1270 X=9:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET
1280 PLAY"O3;V15;T255;GFEDCBAO2AV9AGFEDCBAO2AV8AAGFEDCBAO2AV7A;O2"
1290 RETURN
1300 REM LEGEND SCREEN
1310 LINE(14,30)-(200,170),PRESET,BF
1320 X=5:Y=2:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET
1330 X=5:Y=4:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),C,PSET
1340 X=5:Y=6:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),P,PSET
1350 X=5:Y=8:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),R,PSET
1360 HP=16*8:VP=8*4.25:O$="THE@HERO":GOSUB1660
1370 HP=16*8:VP=8*8.25:O$="CASH":GOSUB1660
1380 HP=16*8:VP=8*12.25:O$="PORTAL":GOSUB1660
1390 HP=16*8:VP=8*16.25:O$="LAVA":GOSUB1660
1400 GOSUB1550
1410 RETURN
1420 REM GAME OVER SCREEN
1430 PCLS:LINE(0,176)-(255,176),PSET:LINE(0,15)-(255,15),PSET
1440 HP=91:VP=40:O$="GAME@OVER": GOSUB1660
1450 HP=87:VP=80:O$="YOU@SCORED": GOSUB1660
1460 HP=INT((255-LEN(STR$(S)))/2):VP=100:N$=MID$(STR$(S),2):GOSUB1690
1470 X=6:Y=6:PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),C,PSET
1480 IF HS<S THEN HS=S:PLAY"O4;T20;CAEDCDCA;T10;O2"
1490 HP=87:VP=140:O$="HIGH@SCORE": GOSUB1660
1500 HP=INT((255-LEN(STR$(HS)))/2):VP=160:N$=MID$(STR$(HS),2):GOSUB1690
1510 X=6:Y=10:PUT(H(X),H(Y)-4)-(H(X)+15,H(Y)+11),A,PSET
1520 A$=INKEY$:FORD=1TO4000:NEXT:GOSUB1550
1530 RETURN
1540 REM AWAIT INPUT
1550 A$=INKEY$:IFA$=""THEN1550
1560 RETURN
1570 REM STICK MAN
1580 DATA24,60,24,126,24,24,36,66
1590 REM LAVA
1600 DATA97,95,204,32,244,31,120,30
1610 REM CASH
1620 DATA0,0,60,24,60,126,126,60
1630 REM PORTAL
1640 DATA126,129,189,165,165,189,129,126
1650 REM DISPLAY TEXT
1660 FORQ=1TOLEN(O$):DRAW"BM"+STR$(HP)+","+STR$(VP)+";"+C$(ASC(MID$(O$,Q,1))-64):HP=HP+8:NEXT
1670 RETURN
1680 REM DISPLAY NUMBERS
1690 FORQ=1TOLEN(N$):DRAW"BM"+STR$(HP)+","+STR$(VP)+";"+N$(ASC(MID$(N$,Q,1))-48):HP=HP+8:NEXT
1700 RETURN
1710 REM LETTER DATA
1720 C$(1)="D8U8R4D4L4R4D4"
1730 C$(2)="R4D8L4U8D4R4"
1740 C$(3)="R4L4D8R4"
1750 C$(4)="D8R3E1U6M-4,-1"
1760 C$(5)="R4L4D4R4L4D4R4"
1770 C$(6)="R4L4D4R4L4D4"
1780 C$(7)="R4L4D8R4U4"
1790 C$(8)="D8U4R4U4D8"
1800 C$(9)="R4L2D8R2L4"
1810 C$(10)="R4L2D8L2"
1820 C$(11)="D8U4M+4,-4M-4,+4M+4,+4"
1830 C$(12)="D8R4"
1840 C$(13)="D8U8M+2,+2M+2,-2D8"
1850 C$(14)="D8U8M+4,+8U8"
1860 C$(15)="D8R4U8L4"
1870 C$(16)="D8U8R4D4L4"
1880 C$(17)="D6R4D2U8L4"
1890 C$(18)="D8U8R4D4L4F4"
1900 C$(19)="R4L4D4R4D4L4"
1910 C$(20)="R4L2D8"
1920 C$(21)="D8R4U8"
1930 C$(22)="D6F2E2U6"
1940 C$(23)="D6F2E2U6D6F2E2U6"
1950 C$(24)="M+4,+8BM+0,-8M-4,+8"
1960 C$(25)="D4R4U4D4L2D4"
1970 C$(26)="R4D2G2G2D2R4"
1980 REM NUMBERS
1990 N$(0)="D8R4U8L4"
2000 N$(1)="BM+2,+0D8"
2010 N$(2)="D2U2R4D4G4R4"
2020 N$(3)="R4D4L4R4D4L4"
2030 N$(4)="D4R4U4D8"
2040 N$(5)="R4L4D4R4D4L4"
2050 N$(6)="R4L4D4R4D4L4U4"
2060 N$(7)="R4D8"
2070 N$(8)="R4D8L4U8D4R4"
2080 N$(9)="R4D8U4L4U4"
2090 REM OUCH
2100 OU$(1)="THAT@STUFF@IS@HOT"
2110 OU$(2)="OUCH@OUCH@"
2120 OU$(3)="FEEL@THE@BURN"
2130 OU$(4)="LAVA@GOTCHA"
2140 OU$(5)="SCORCHIO"
2150 OU$(6)="SMOKEY@SHOES"
2160 OU$(7)="@HOT@HOT@HOT"
2170 OU$(8)="BURN@BABY@BURN@"
2180 RETURN
2190 REM