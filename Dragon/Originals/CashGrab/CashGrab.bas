10 DIMH(16),I(16),A(1),B(1),C(1),P(1),R(1),M(16,11),N(11),X,Y,V,W,D,J,R
20 FORI=0TO15:H(I)=I*16:NEXT
30 REM SET UP GRAPHICS
40 PMODE0,1:PCLS:SCREEN1,1
50 GET(0,0)-(15,15),B
60 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
70 GET(0,0)-(15,15),A
80 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
90 GET(0,0)-(15,15),R
100 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
110 GET(0,0)-(15,15),C
120 FORL=0TO7:READD:POKE1536+(L*16),D:NEXT
130 GET(0,0)-(15,15),P
140 REM INIT LEVEL
150 GOSUB510
160 PCLS:GOSUB470
170 REM GAME LOOP
180 A$=INKEY$:IFA$="P"THENX=X+1ELSEIFA$="O"THENX=X-1
190 IFA$="Q"THENY=Y-1ELSEIFA$="A"THENY=Y+1
200 J=JOYSTK(0):IFJ=0THENX=X-1ELSEIFJ=63THENX=X+1
210 J=JOYSTK(1):IFJ=0THENY=Y-1ELSEIFJ=63THENY=Y+1
220 IFX<0THENX=0
230 IFX>15THENX=15
240 IFY<1THENY=1
250 IFY>11THENY=11
260 GOSUB 370
270 IF X=V THEN IF Y=W THEN 290
280 PUT(H(X),H(Y))-(H(X)+15,H(Y)+15),A,PSET:PUT(H(V),H(W))-(H(V)+15,H(W)+15),B,PSET:V=X:W=Y
290 GOSUB 320
300 GOTO 180 : REM CONTINUE LOOP
310 REM CHECK LOCATION
320 Z=M(X,Y):IFZ=0THENRETURN
330 IF Z=1 THEN GOSUB400
340 IF Z=2 THEN GOSUB430
350 IF Z=3 THEN 150
360 RETURN
370 REM DRAW SCREEN UPDATES
380 R=RND(11):IF N(R)>0 THEN N(R)=N(R)-1:O=N(R):M(O,R)=1:PUT(H(O),H(R))-(H(O)+15,H(R)+15),R,PSET
390 RETURN
400 REM BURN
410 SCREEN1,0
420 RETURN
430 REM SCORE
440 M(X,Y)=0
450 RETURN
460 REM DRAW INTIAL SCREEN
470 LINE(0,15)-(255,15),PSET
480 REM PUT(H(3),H(5))-(H(3)+15,H(5)+15),C,PSET
490 PUT(H(0),H(R))-(H(0)+15,H(R)+15),P,PSET
500 RETURN
510 REM LEVEL SET UP
520 X=0:Y=2:V=1:W=1
530 GOSUB 570
540 M(3,5)=2:REM CASH BAG
550 R=RND(11):M(0,R)=3:REM PORTAL
560 RETURN
570 REM RESET LEVEL DATA
580 FORJ=0TO16:FORI=0TO11:M(J,I)=0:NEXT:NEXT: REM SCREEN LAYOUT
590 FORI=0TO11:N(I)=16:NEXT: REM LAVA POSITIONS
600 RETURN
610 REM STICK MAN
620 DATA24,60,24,126,24,24,36,66
630 REM LAVA
640 DATA97,95,204,32,244,31,120,30
650 REM CASH
660 DATA0,0,60,24,60,126,126,60
670 REM PORTAL
680 DATA126,129,189,165,165,189,129,126
690 REM