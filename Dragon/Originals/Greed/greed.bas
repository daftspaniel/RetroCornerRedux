10 CLEAR 500:DIM G(32,16),X,Y,A$
20 DIM BX,BY,BH,BW,BQ,BC$,T$,TT$,BR,LX,LC$,LZ,LY,LL,TL,LO$
30 CLS 0
40 TT$="GREED":TBY$="DAVY MITCHELL":TD$="IDEA FROM ROSETTACODE.ORG"
50 TV$="VERSION 0.1 AUG 2025":TC$=CHR$(175):GOSUB 750
60 GOSUB 1370
70 CLS:TL=0:T$="welcome to greed":GOSUB1240
80 TL=2:T$="Q / A = UP AND DOWN":GOSUB1240
90 TL=3:T$="O / P = LEFT AND RIGHT":GOSUB1240
100 TL=5:T$="X = FINISH ROUND ":GOSUB1240
110 TL=7:T$="MOVING ONTO A NUMBERED SQUARE":GOSUB1240
120 TL=8:T$="MOVES YOU N STEPS IN THAT":GOSUB1240
130 TL=9:T$="DIRECTION. YOU MUST STAY ON THE":GOSUB1240
140 TL=10:T$="BOARD AND CANNOT STEP ON A":GOSUB1240
150 TL=11:T$="SQUARE TWICE. KEEP PLAYING":GOSUB1240
160 TL=12:T$="UNTIL YOU CANNOT MAKE A MOVE.":GOSUB1240

170 GOSUB 1370

180 REM SET THE INITIAL PLAYING FIELD
190 FORY=1TO16
200 FORX=1TO32
210 G(X,Y)=RND(9)
220 POKE&H400+((Y-1)*&H20)+(X-1),&H30+G(X,Y)
230 NEXT
240 NEXT
250 REM INIT GAME
260 S=0
270 X=15:Y=8:G(X,Y)=0
280 POKE&H400+((Y-1)*&H20)+(X-1),106
290 A$=INKEY$:IFA$=""THEN290
300 IFA$="O" AND X>1 THEN GOSUB370
310 IFA$="P" AND X<32 THEN GOSUB450
320 IFA$="Q" AND Y>1 THEN GOSUB530
330 IFA$="A" AND Y<16 THEN GOSUB610
340 IFA$="X" THEN 270
345 S=S+D
350 GOTO 280
360 GOTO 360
370 REM *** MOVE LEFT ***
380 D=G(X-1,Y)
390 IF D=0 OR D=>X THEN SOUND100,1:RETURN
400 V=0:FOR J=X-1 TO X-D STEP-1:IFG(J,Y)=0THENV=1
410 NEXT:IFV=1THENSOUND100,1:RETURN
420 FOR J=X TO X-D STEP-1:G(J,Y)=0:POKE&H400+((Y-1)*&H20)+(J-1),128:NEXT
430 X=X-D
440 RETURN
450 REM *** MOVE RIGHT ***
460 D=G(X+1,Y)
470 IF D=0 OR D+X>32 THEN SOUND100,1:RETURN
480 V=0:FOR J=X+1 TO X+D:IFG(J,Y)=0THENV=1
490 NEXT:IFV=1THENSOUND100,1:RETURN
500 FOR J=X TO X+D:G(J,Y)=0:POKE&H400+((Y-1)*&H20)+(J-1),128:NEXT
510 X=X+D
520 RETURN
530 REM *** MOVE UP ***
540 D=G(X,Y-1)
550 IF D=0 OR Y-D<1 THEN SOUND100,1:RETURN
560 V=0:FOR J=Y-1 TO Y-D STEP-1:IFG(X,J)=0THENV=1
570 NEXT:IFV=1THENSOUND100,1:RETURN
580 FOR J=Y TO Y-D STEP-1:G(X,J)=0:POKE&H400+((J-1)*&H20)+(X-1),128:NEXT
590 Y=Y-D
600 RETURN
610 REM *** MOVE DOWN ***
620 D=G(X,Y+1)
630 IF D=0 OR Y+D>16 THEN SOUND100,1:RETURN
640 V=0:FOR J=Y+1 TO Y+D:IFG(X,J)=0THENV=1
650 NEXT:IFV=1THENSOUND100,1:RETURN
660 FOR J=Y TO Y+D:G(X,J)=0:POKE&H400+((J-1)*&H20)+(X-1),128:NEXT
670 Y=Y+D
680 RETURN


690 REM ------LIBRARY LIVES HERE------

700 REM TITLE SCREEN
710 REM TT$ = PROGRAM NAME
720 REM TBY$ = AUTHOR
730 REM TD$ = DATE
740 REM TV$ = VERSION
750 TM=LEN(TT$):IF LEN(TBY$)>TM THEN TM=LEN(TBY$)
760 IF LEN(TV$)>TM THEN TM=LEN(TV$)
770 IF LEN(TD$)>TM THEN TM=LEN(TD$)
780 BH=7:BW=TM+2:BY=3:BX=(30-BW)/2+1:BC$=CHR$(143):GOSUB1180
790 BH=9:BW=TM+4:BY=1:BX=(32-BW)/2:BC$=TC$:GOSUB1060
800 ?@(BY+1)*32 + ((32-LEN(TT$))/2),TT$;
810 ?@(BY+3)*32 + ((32-LEN(TBY$))/2),TBY$;
820 ?@(BY+5)*32 + ((32-LEN(TD$))/2),TD$;
830 ?@(BY+7)*32 + ((32-LEN(TV$))/2),TV$;
840 RETURN

850 REM VERTICAL LINE
860 REM LC$ = CHARACTER FOR LINE
870 REM LX,LY = POSITION 
880 REM LL = LENGTH
890 LZ=LY+LL-1
900 FOR BR=LY TO LZ:?@LX+(BR*32),LC$;:NEXT
910 RETURN

920 REM HORIZONTAL LINE
930 REM LC$ = CHARACTER FOR LINE
940 REM LX,LY = POSITION 
950 REM LL = LENGTH
960 LO$=""
970 REM MC10: FOR BR=1TOLL:LO$=LO$+LC$:NEXT
980 REM COCO/DRAGON: LO$=STRING$(LL,LC$)
990 LO$=STRING$(LL,LC$)
1000 ?@LX+(LY*32),LO$;
1010 RETURN

1020 REM BOX OUTLINE
1030 REM BX,BY = BOX POSITION
1040 REM BH,BW = BOX HEIGHT & WIDTH
1050 REM BC$ = CHARACTER FOR BOX EDGE
1060 REM MC10: BR$="":FOR BR=1TOBW:BR$=BR$+BC$:NEXT
1070 REM COCO/DRAGON: BR$=STRING$(BW,BC$)
1080 BR$=STRING$(BW,BC$)
1090 ?@BX+((BY+1)*32),BR$;
1100 ?@BX+((BY+BH)*32),BR$;
1110 BH=BH-1:BY=BY+1:BW=BW-1
1120 FOR BQ=1TOBH:?@BX+((BY+BQ)*32),BC$;:?@BX+BW+((BY+BQ)*32),BC$;:NEXT
1130 RETURN

1140 REM FILLED BOX
1150 REM BX,BY = BOX POSITION
1160 REM BH,BW = BOX HEIGHT & WIDTH
1170 REM BC$ = CHARACTER FOR BOX
1180 REM MC10: BR$="":FOR BR=1TOBW:BR$=BR$+BC$:NEXT
1190 REM COCO/DRAGON: BR$=STRING$(BW,BC$)
1200 BR$=STRING$(BW,BC$)
1210 FOR BQ=1TOBH:?@BX+((BY-1+BQ)*32),BR$;:NEXT
1220 RETURN

1230 REM CENTER TEXT. TL = vertical line number.
1240 PRINT@(32-LEN(T$))/2+32*TL,T$;
1250 RETURN

1260 REM DELAY
1270 FORDZ=1TO1000:NEXT:RETURN

1280 REM WAIT FOR SPACEBAR
1290 BC$=CHR$(128):?@483,"press"+BC$+"space"+BC$+"bar"+BC$+"to"+BC$+"continue";:
1300 WA$=INKEY$:IFWA$<>" "THEN1300ELSE RETURN

1310 REM WAIT FOR ANY KEY
1320 WA$=INKEY$:IF WA$="" THEN 1320
1330 RETURN

1340 REM ANY KEY PROMPT AT BOTTOM OF SCREEN.
1350 ?@483,"PRESS ANY KEY TO CONTINUE";:GOSUB1320:RETURN

1360 REM INVERTED ANY KEY PROMPT AT BOTTOM OF SCREEN.
1370 BC$=CHR$(128):?@483,"press"+BC$+"any"+BC$+"key"+BC$+"to"+BC$+"continue";:GOSUB1320:RETURN

1380 REM LOWER CASE TEXT
1390 TL=LEN(T$):O$=""
1400 FORBR=1TOTL:C$=MID$(T$,BR,1):BH=ASC(C$):IF BH<91 AND BH>64 THEN C$=CHR$(BH+32)
1410 IFBH=32THENC$=CHR$(128)
1420 O$=O$+C$
1430 NEXT
1440 T$=O$
1450 RETURN


