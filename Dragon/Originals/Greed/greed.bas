10 DIM G(32,16),X,Y,A$:TIMER=0
20 REM POKE 65495,0
30 FORY=1TO16
40 FORX=1TO32
50 G(X,Y)=RND(9)
60 POKE&H400+((Y-1)*&H20)+(X-1),&H30+G(X,Y)
70 NEXT
80 NEXT
90 REM ?TIMER/60
100 REM
110 X=15:Y=8:G(X,Y)=0
120 POKE&H400+((Y-1)*&H20)+(X-1),42
130 A$=INKEY$:IFA$=""THEN130
140 IFA$="O" AND X>1 THEN GOSUB210
150 IFA$="P" AND X<32 THEN GOSUB290
160 IFA$="Q" AND Y>1 THEN GOSUB370
170 IFA$="A" AND Y<16 THEN GOSUB450
180 IFA$="R" THEN 110
190 GOTO 120
200 GOTO 200
210 REM *** MOVE LEFT ***
220 D=G(X-1,Y)
230 IF D=0 OR D=>X THEN SOUND100,1:RETURN
240 V=0:FOR J=X-1 TO X-D STEP-1:IFG(J,Y)=0THENV=1
250 NEXT:IFV=1THENSOUND100,1:RETURN
260 FOR J=X TO X-D STEP-1:G(J,Y)=0:POKE&H400+((Y-1)*&H20)+(J-1),128:NEXT
270 X=X-D
280 RETURN
290 REM *** MOVE RIGHT ***
300 D=G(X+1,Y)
310 IF D=0 OR D+X>32 THEN SOUND100,1:RETURN
320 V=0:FOR J=X+1 TO X+D:IFG(J,Y)=0THENV=1
330 NEXT:IFV=1THENSOUND100,1:RETURN
340 FOR J=X TO X+D:G(J,Y)=0:POKE&H400+((Y-1)*&H20)+(J-1),128:NEXT
350 X=X+D
360 RETURN
370 REM *** MOVE UP ***
380 D=G(X,Y-1)
390 IF D=0 OR Y-D<1 THEN SOUND100,1:RETURN
400 V=0:FOR J=Y-1 TO Y-D STEP-1:IFG(X,J)=0THENV=1
410 NEXT:IFV=1THENSOUND100,1:RETURN
420 FOR J=Y TO Y-D STEP-1:G(X,J)=0:POKE&H400+((J-1)*&H20)+(X-1),128:NEXT
430 Y=Y-D
440 RETURN
450 REM *** MOVE DOWN ***
460 D=G(X,Y+1)
470 IF D=0 OR Y+D>16 THEN SOUND100,1:RETURN
480 V=0:FOR J=Y+1 TO Y+D:IFG(X,J)=0THENV=1
490 NEXT:IFV=1THENSOUND100,1:RETURN
500 FOR J=Y TO Y+D:G(X,J)=0:POKE&H400+((J-1)*&H20)+(X-1),128:NEXT
510 Y=Y+D
520 RETURN



