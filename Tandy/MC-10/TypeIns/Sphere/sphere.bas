5 GOSUB 2000
10 X=0:Y=0:K=0:PI=3.1415926:C=RND(8):CX=32:CY=14:CLS 0
20 I=14:J=2
30 FOR K=-PI TO PI STEP 0.07
40 X=CX+I*SIN(K):Y=CY+13*COS(K):SET(X,Y,C)
50 X=CX+20*SIN(K):Y=CY+(I-1)*COS(K):SET(X,Y,C)
60 NEXT
70 I=I-J
80 J=J+1
90 IF I>0 THEN GOTO 30
100 NC=RND(8):IF NC=C THEN 100
110 C=NC:FORD=1TO300:NEXT
120 GOTO 20

2000 CLS0:TT$="DRAW A SPHERE":TBY$="WWW.ROSETTACODE.ORG":TD$="MC-10 PORT FROM ZX81"
2010 TV$="DAVY MITCHELL MAY 2023":TC$=CHR$(175):GOSUB 9010
2020 GOSUB9930:GOTO9810
2500 RETURN

9010 TM=LEN(TT$):IF LEN(TBY$)>TM THEN TM=LEN(TBY$)
9015 IF LEN(TV$)>TM THEN TM=LEN(TV$)
9017 IF LEN(TD$)>TM THEN TM=LEN(TD$)
9020 BH=7:BW=TM+2:BY=3:BX=(30-BW)/2+1:BC$=CHR$(143):GOSUB9510
9030 BH=9:BW=TM+4:BY=1:BX=(32-BW)/2:BC$=TC$:GOSUB9410
9040 ?@(BY+1)*32 + ((32-LEN(TT$))/2),TT$;
9050 ?@(BY+3)*32 + ((32-LEN(TBY$))/2),TBY$;
9060 ?@(BY+5)*32 + ((32-LEN(TD$))/2),TD$;
9070 ?@(BY+7)*32 + ((32-LEN(TV$))/2),TV$;
9100 RETURN

9410 BR$="":FOR BR=1TOBW:BR$=BR$+BC$:NEXT
9430 ?@BX+((BY+1)*32),BR$;
9435 ?@BX+((BY+BH)*32),BR$;
9437 BH=BH-1:BY=BY+1:BW=BW-1
9440 FOR BQ=1TOBH:?@BX+((BY+BQ)*32),BC$;:?@BX+BW+((BY+BQ)*32),BC$;:NEXT
9499 RETURN

9510 BR$=""
9520 FOR BR=1TOBW:BR$=BR$+BC$:NEXT
9530 FOR BQ=1TOBH:?@BX+((BY-1+BQ)*32),BR$;:NEXT
9599 RETURN

9810 WA$=INKEY$:IF WA$="" THEN 9810
9820 RETURN

9930 BC$=CHR$(128):?@483,"press"+BC$+"any"+BC$+"key"+BC$+"to"+BC$+"continue";:GOSUB9810:RETURN
