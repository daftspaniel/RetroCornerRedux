1 CLEAR4000:DIM BX,BY,BH,BW,BQ,BC$,T$,TT$,BR,LX,LC$,LZ,LY,LL,TL,LO$
2 REM DATA STRUCTURE
3 I=1 : REM INDEX
4 DIMS$(10): REM STRINGS
5 DIMT(10): REM TEMPOS
6 DIMR(10): REM REPEATS
9 REM --------------
10 MP=1:OP=0: REM MENU POSITION
20 FORD=1TO10:S$(D)="ABC":T(D)=100:R(D)=1:NEXT
30 CLS0:TT$="ECHO-CO SFX TOOL":TBY$="BY DAVY MITCHELL":TD$="AUGUST 2024"
40 TV$="V0.1 -  A SEPTANDY PROJECT":TC$=CHR$(191):GOSUB 9010
50 GOSUB9750:CLS

100 BX=0:BY=0:BW=32:BH=5:BC$=CHR$(207):GOSUB9410
110 S$(I)="ABC":T=T(I)
111 S$(2)="ABCD"
112 S$(3)="ABCDE"
113 R(3)=3
120 GOSUB1210:GOSUB1000
130 GOSUB1500
190 REM INTERACT
200 A$=INKEY$:IFA$=""THEN200
205 IF A$<>"A" AND A$<>"Q" THEN 228
210 IFA$="Q"THENOP=MP:MP=MP-1:IFMP<1THENMP=1
220 IFA$="A"THENOP=MP:MP=MP+1:IFMP>4THENMP=4
225 GOSUB1510:GOTO200
228 IF MP=1 AND A$="." THEN I=I+1:IFI>10THENI=10
229 IF MP=1 AND A$="," THEN I=I-1:IFI<1THENI=1
230 IF MP=3 AND A$="." THEN T(I)=T(I)+1:IFT(I)>255THENT(I)=255
231 IF MP=3 AND A$="," THEN T(I)=T(I)-1:IFT(I)<1THENT(I)=1
996 IF A$="P" THEN PLAYPS$:GOTO200
997 GOSUB1210:REM BUILD PLAY STRING
998 GOSUB1000:REM UPDATE SCREEN
999 GOTO 200:REM FURTHER INPUT
1000 REM UPDATE SCREEN
1010 ?@97,STRING$(30,CHR$(143));:T$=PS$:TL=3:GOSUB9610
1020 ?@289,"sound    ";:?@295,STR$(I);
1030 ?@321,"string                     ";:?@328,S$(I);
1040 ?@353,"tempo      ";:?@359,STR$(T(I));
1050 ?@385,"repeat   ";:?@391,STR$(R(I));
1100 RETURN
1200 REM BUILD SOUND STRING
1210 PS$="T"+MID$(STR$(T(I)),2)+";"
1220 FORD=1TOR(I):PS$=PS$+S$(I):NEXT
1300 RETURN
1500 REM MENU UPDATE
1510 ?@256+(MP*32),CHR$(42);
1520 IF OP<>MP THEN?@256+(OP*32),CHR$(143);
1600 RETURN
5999 REM ------LIBRARY LIVES HERE------

9000 REM TITLE SCREEN
9001 REM TT$ = PROGRAM NAME
9002 REM TBY$ = AUTHOR
9003 REM TD$ = DATE
9004 REM TV$ = VERSION
9010 TM=LEN(TT$):IF LEN(TBY$)>TM THEN TM=LEN(TBY$)
9015 IF LEN(TV$)>TM THEN TM=LEN(TV$)
9017 IF LEN(TD$)>TM THEN TM=LEN(TD$)
9020 BH=7:BW=TM+2:BY=3:BX=(30-BW)/2+1:BC$=CHR$(143):GOSUB9510
9030 BH=9:BW=TM+4:BY=1:BX=(32-BW)/2:BC$=TC$:GOSUB9410
9040 ?@(BY+1)*32 + ((32-LEN(TT$))/2),TT$;
9050 ?@(BY+3)*32 + ((32-LEN(TBY$))/2),TBY$;
9060 ?@(BY+5)*32 + ((32-LEN(TD$))/2),TD$;
9070 ?@(BY+7)*32 + ((32-LEN(TV$))/2),TV$;
9100 RETURN

9200 REM VERTICAL LINE
9201 REM LC$ = CHARACTER FOR LINE
9202 REM LX,LY = POSITION 
9203 REM LL = LENGTH
9210 LZ=LY+LL-1
9220 FOR BR=LY TO LZ:?@LX+(BR*32),LC$;:NEXT
9240 RETURN

9300 REM HORIZONTAL LINE
9301 REM LC$ = CHARACTER FOR LINE
9302 REM LX,LY = POSITION 
9304 REM LL = LENGTH
9310 LO$=""
9320 REM MC10: FOR BR=1TOLL:LO$=LO$+LC$:NEXT
9320 REM COCO/DRAGON: LO$=STRING$(LL,LC$)
9320 LO$=STRING$(LL,LC$)
9330 ?@LX+(LY*32),LO$;
9340 RETURN

9400 REM BOX OUTLINE
9403 REM BX,BY = BOX POSITION
9404 REM BH,BW = BOX HEIGHT & WIDTH
9405 REM BC$ = CHARACTER FOR BOX EDGE
9410 REM MC10: BR$="":FOR BR=1TOBW:BR$=BR$+BC$:NEXT
9410 REM COCO/DRAGON: BR$=STRING$(BW,BC$)
9410 BR$=STRING$(BW,BC$)
9420 ?@BX+((BY+1)*32),BR$;
9430 ?@BX+((BY+BH)*32),BR$;
9440 BH=BH-1:BY=BY+1:BW=BW-1
9450 FOR BQ=1TOBH:?@BX+((BY+BQ)*32),BC$;:?@BX+BW+((BY+BQ)*32),BC$;:NEXT
9460 RETURN

9500 REM FILLED BOX
9503 REM BX,BY = BOX POSITION
9504 REM BH,BW = BOX HEIGHT & WIDTH
9505 REM BC$ = CHARACTER FOR BOX
9510 REM MC10: BR$="":FOR BR=1TOBW:BR$=BR$+BC$:NEXT
9510 REM COCO/DRAGON: BR$=STRING$(BW,BC$)
9510 BR$=STRING$(BW,BC$)
9530 FOR BQ=1TOBH:?@BX+((BY-1+BQ)*32),BR$;:NEXT
9540 RETURN

9600 REM CENTER TEXT. TL = vertical line number.
9610 PRINT@(32-LEN(T$))/2+32*TL,T$;
9620 RETURN

9700 REM DELAY
9710 FORDZ=1TO1000:NEXT:RETURN

9740 REM WAIT FOR SPACEBAR
9750 BC$=CHR$(128):?@483,"press"+BC$+"space"+BC$+"bar"+BC$+"to"+BC$+"continue";:
9760 WA$=INKEY$:IFWA$<>" "THEN9760 ELSE RETURN

9800 REM WAIT FOR ANY KEY
9810 WA$=INKEY$:IF WA$="" THEN 9810
9820 RETURN

9900 REM ANY KEY PROMPT AT BOTTOM OF SCREEN.
9910 ?@483,"PRESS ANY KEY TO CONTINUE";:GOSUB9810:RETURN

9920 REM INVERTED ANY KEY PROMPT AT BOTTOM OF SCREEN.
9930 BC$=CHR$(128):?@483,"press"+BC$+"any"+BC$+"key"+BC$+"to"+BC$+"continue";:GOSUB9810:RETURN

9950 REM LOWER CASE TEXT
9960 TL=LEN(T$):O$=""
9970 FORBR=1TOTL:C$=MID$(T$,BR,1):BH=ASC(C$):IF BH<91 AND BH>64 THEN C$=CHR$(BH+32)
9971 IFBH=32THENC$=CHR$(128)
9975 O$=O$+C$
9979 NEXT
9980 T$=O$
9999 RETURN
