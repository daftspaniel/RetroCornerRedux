10 CLEAR4000:DIM BX,BY,BH,BW,BQ,BC$,T$,TT$,BR,LX,LC$,LZ,LY,LL,TL,LO$
20 REM DATA STRUCTURE
30 I=1 : REM INDEX
40 DIMS$(10): REM STRINGS
50 DIMT(10): REM TEMPOS
60 DIMR(10): REM REPEATS
70 DIMO(10): REM OCTAVES
80 REM --------------
90 MP=1:OP=0: REM MENU POSITION
100 FORD=1TO10:S$(D)="ABC":T(D)=100:R(D)=1:O(D)=2:NEXT
110 CLS0:TT$="ECHO-CO SFX TOOL":TBY$="BY DAVY MITCHELL":TD$="AUGUST 2024"
120 TV$="V0.1 -  A SEPTANDY PROJECT":TC$=CHR$(191):GOSUB 750
130 GOSUB1290:CLS

140 BX=0:BY=0:BW=32:BH=5:BC$=CHR$(207):GOSUB1060
150 S$(I)="ABC":T=T(I)
160 S$(2)="ABCD"
170 S$(3)="ABCDE"
180 R(3)=3
190 GOSUB620:GOSUB320
200 GOSUB650
210 REM INTERACT
220 A$=INKEY$:IFA$=""THEN220
230 IF A$<>"A" AND A$<>"Q" THEN 270
240 IFA$="Q"THENOP=MP:MP=MP-1:IFMP<1THENMP=1
250 IFA$="A"THENOP=MP:MP=MP+1:IFMP>5THENMP=5
260 GOSUB660:GOTO220
270 ON MP GOSUB 400,440,470,530,570
280 IF A$="P" THEN PLAYPS$:GOTO220
290 GOSUB620:REM BUILD PLAY STRING
300 GOSUB320:REM UPDATE SCREEN
310 GOTO 220:REM FURTHER INPUT
320 REM UPDATE SCREEN
330 ?@97,STRING$(30,CHR$(143));:T$=PS$:TL=3:GOSUB1240
335 PO=226
340 ?@PO+32,"sound     ";:?@PO+32+9,STR$(I);
350 ?@PO+32*2,"string                     ";:?@PO+32*2+10,S$(I);
360 ?@PO+32*3,"tempo":?@PO+32*3+9,STR$(T(I));
370 ?@PO+32*4,"repeat   ";:?@PO+32*4+9,STR$(R(I));
380 ?@PO+32*5,"octave   ";:?@PO+32*5+9,STR$(O(I));
390 RETURN
400 REM MP=1
410 IF A$="." THEN I=I+1:IFI>10THENI=10
420 IF A$="," THEN I=I-1:IFI<1THENI=1
430 RETURN
440 REM MP=2
450 REM TODO
460 RETURN
470 REM MP=3
480 IF A$="." THEN T(I)=T(I)+1:IFT(I)>255THENT(I)=255
490 IF A$="," THEN T(I)=T(I)-1:IFT(I)<1THENT(I)=1
500 IF A$=">" THEN T(I)=T(I)+10:IFT(I)>255THENT(I)=255
510 IF A$="<" THEN T(I)=T(I)-10:IFT(I)<1THENT(I)=1
520 RETURN
530 REM MP=4
540 IF A$="." THEN R(I)=R(I)+1:IFR(I)>5THENR(I)=5
550 IF A$="," THEN R(I)=R(I)-1:IFR(I)<1THENR(I)=1
560 RETURN
570 REM MP=5
580 IF A$="." THEN O(I)=O(I)+1:IFO(I)>5THENO(I)=5
590 IF A$="," THEN O(I)=O(I)-1:IFO(I)<1THENO(I)=1
600 RETURN
610 REM BUILD SOUND STRING
620 PS$="O"+MID$(STR$(O(I)),2)+"T"+MID$(STR$(T(I)),2)+";"
630 FORD=1TOR(I):PS$=PS$+S$(I):NEXT
640 RETURN
650 REM MENU UPDATE
660 ?@-1+PO+(MP*32),CHR$(42);
670 IF OP<>MP THEN?@-1+PO+(OP*32),CHR$(143);
680 RETURN
690 REM ------LIBRARY LIVES HERE------

700 REM TITLE SCREEN
710 REM TT$ = PROGRAM NAME
720 REM TBY$ = AUTHOR
730 REM TD$ = DATE
740 REM TV$ = VERSION
750 TM=LEN(TT$):IF LEN(TBY$)>TM THEN TM=LEN(TBY$)
760 IF LEN(TV$)>TM THEN TM=LEN(TV$)
770 IF LEN(TD$)>TM THEN TM=LEN(TD$)
780 BH=7:BW=TM+2:BY=3:BX=(30-BW)/2+1:BC$=CHR$(143):GOSUB1180
790 BH=9:BW=TM+4:BY=1:BX=(32-BW)/2:BC$=TC$:GOSUB1060
800 ?@(BY+1)*32 + ((32-LEN(TT$))/2),TT$;
810 ?@(BY+3)*32 + ((32-LEN(TBY$))/2),TBY$;
820 ?@(BY+5)*32 + ((32-LEN(TD$))/2),TD$;
830 ?@(BY+7)*32 + ((32-LEN(TV$))/2),TV$;
840 RETURN

850 REM VERTICAL LINE
860 REM LC$ = CHARACTER FOR LINE
870 REM LX,LY = POSITION 
880 REM LL = LENGTH
890 LZ=LY+LL-1
900 FOR BR=LY TO LZ:?@LX+(BR*32),LC$;:NEXT
910 RETURN

920 REM HORIZONTAL LINE
930 REM LC$ = CHARACTER FOR LINE
940 REM LX,LY = POSITION 
950 REM LL = LENGTH
960 LO$=""
970 REM MC10: FOR BR=1TOLL:LO$=LO$+LC$:NEXT
980 REM COCO/DRAGON: LO$=STRING$(LL,LC$)
990 LO$=STRING$(LL,LC$)
1000 ?@LX+(LY*32),LO$;
1010 RETURN

1020 REM BOX OUTLINE
1030 REM BX,BY = BOX POSITION
1040 REM BH,BW = BOX HEIGHT & WIDTH
1050 REM BC$ = CHARACTER FOR BOX EDGE
1060 REM MC10: BR$="":FOR BR=1TOBW:BR$=BR$+BC$:NEXT
1070 REM COCO/DRAGON: BR$=STRING$(BW,BC$)
1080 BR$=STRING$(BW,BC$)
1090 ?@BX+((BY+1)*32),BR$;
1100 ?@BX+((BY+BH)*32),BR$;
1110 BH=BH-1:BY=BY+1:BW=BW-1
1120 FOR BQ=1TOBH:?@BX+((BY+BQ)*32),BC$;:?@BX+BW+((BY+BQ)*32),BC$;:NEXT
1130 RETURN

1140 REM FILLED BOX
1150 REM BX,BY = BOX POSITION
1160 REM BH,BW = BOX HEIGHT & WIDTH
1170 REM BC$ = CHARACTER FOR BOX
1180 REM MC10: BR$="":FOR BR=1TOBW:BR$=BR$+BC$:NEXT
1190 REM COCO/DRAGON: BR$=STRING$(BW,BC$)
1200 BR$=STRING$(BW,BC$)
1210 FOR BQ=1TOBH:?@BX+((BY-1+BQ)*32),BR$;:NEXT
1220 RETURN

1230 REM CENTER TEXT. TL = vertical line number.
1240 PRINT@(32-LEN(T$))/2+32*TL,T$;
1250 RETURN

1260 REM DELAY
1270 FORDZ=1TO1000:NEXT:RETURN

1280 REM WAIT FOR SPACEBAR
1290 BC$=CHR$(128):?@483,"press"+BC$+"space"+BC$+"bar"+BC$+"to"+BC$+"continue";:
1300 WA$=INKEY$:IFWA$<>" "THEN1300ELSE RETURN

1310 REM WAIT FOR ANY KEY
1320 WA$=INKEY$:IF WA$="" THEN 1320
1330 RETURN

1340 REM ANY KEY PROMPT AT BOTTOM OF SCREEN.
1350 ?@483,"PRESS ANY KEY TO CONTINUE";:GOSUB1320:RETURN

1360 REM INVERTED ANY KEY PROMPT AT BOTTOM OF SCREEN.
1370 BC$=CHR$(128):?@483,"press"+BC$+"any"+BC$+"key"+BC$+"to"+BC$+"continue";:GOSUB1320:RETURN

1380 REM LOWER CASE TEXT
1390 TL=LEN(T$):O$=""
1400 FORBR=1TOTL:C$=MID$(T$,BR,1):BH=ASC(C$):IF BH<91 AND BH>64 THEN C$=CHR$(BH+32)
1410 IFBH=32THENC$=CHR$(128)
1420 O$=O$+C$
1430 NEXT
1440 T$=O$
1450 RETURN
