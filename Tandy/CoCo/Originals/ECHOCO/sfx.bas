10 CLEAR4000:DIM BX,BY,BH,BW,BQ,BC$,T$,TT$,BR,LX,LC$,LZ,LY,LL,TL,LO$
20 REM DATA STRUCTURE
30 I=1 : REM INDEX
40 DIMS$(10): REM STRINGS
50 DIMT(10): REM TEMPOS
60 DIMR(10): REM REPEATS
70 DIMO(10): REM OCTAVES
80 DIMV(10): REM VOLUMES
90 REM --------------
100 MP=1:OP=0: REM MENU POSITION
110 FORD=1TO10:S$(D)="ABC":T(D)=10:R(D)=1:O(D)=2:V(D)=15:NEXT
120 CLS0:TT$="ECHO-CO SFX TOOL":TBY$="BY DAVY MITCHELL":TD$="SEPTEMBER 2024"
130 TV$="V0.2 -  A SEPTANDY PROJECT":TC$=CHR$(191):GOSUB 1250
140 GOSUB1790:CLS
150 PRINT"echo-co sfx tool"
160 PRINT"1. SOUND EDITOR"
170 PRINT"2. SOUND BOARD"
180 PRINT"3. SEND SOUNDS TO PRINTER"
190 INPUT A
200 IF A<1 OR A<3 THEN 150
210 ON A GOSUB 230,930,1100
220 CLS:GOTO160
230 CLS:GOSUB850:GOSUB370
240 GOSUB880
250 REM INTERACT
260 A$=INKEY$:IFA$=""THEN260
270 IFA$<>"A" AND A$<>"Q" THEN 310
280 IFA$="Q"THENOP=MP:MP=MP-1:IFMP<1THENMP=1
290 IFA$="A"THENOP=MP:MP=MP+1:IFMP>6THENMP=6
300 GOSUB890:GOTO260
310 ON MP GOSUB 480,520,560,620,660,700
320 IFA$="P" THEN PLAYPS$:GOTO260
330 IFA$="M" THEN RETURN
340 GOSUB850:REM BUILD PLAY STRING
350 GOSUB370:REM UPDATE SCREEN
360 GOTO 260:REM FURTHER INPUT
370 REM UPDATE SCREEN
380 ?@0,STRING$(255," ");:T$=PS$:TL=3:GOSUB1740
390 PO=226
400 ?@PO+32,"sound     ";:?@PO+32+9,STR$(I)+" ";
410 ?@PO+32*2,"string                     ";
420 ?@PO+32*2+10,S$(I)+" ";
430 ?@PO+32*3,"tempo":?@PO+32*3+9,STR$(T(I))+" ";
440 ?@PO+32*4,"repeat   ";:?@PO+32*4+9,STR$(R(I))+" ";
450 ?@PO+32*5,"octave   ";:?@PO+32*5+9,STR$(O(I))+" ";
460 ?@PO+32*6,"volume   ";:?@PO+32*6+9,STR$(V(I))+" ";
470 RETURN
480 REM MP=1
490 IF A$="." THEN I=I+1:IFI>10THENI=10
500 IF A$="," THEN I=I-1:IFI<1THENI=1
510 RETURN
520 REM MP=2
530 IF A$="R" THEN GOSUB 760
540 IF A$="E" THEN GOSUB 810
550 RETURN
560 REM MP=3
570 IF A$="." THEN T(I)=T(I)+1:IFT(I)>255THENT(I)=255
580 IF A$="," THEN T(I)=T(I)-1:IFT(I)<1THENT(I)=1
590 IF A$=">" THEN T(I)=T(I)+10:IFT(I)>255THENT(I)=255
600 IF A$="<" THEN T(I)=T(I)-10:IFT(I)<1THENT(I)=1
610 RETURN
620 REM MP=4
630 IF A$="." THEN R(I)=R(I)+1:IFR(I)>5THENR(I)=5
640 IF A$="," THEN R(I)=R(I)-1:IFR(I)<1THENR(I)=1
650 RETURN
660 REM MP=5
670 IF A$="." THEN O(I)=O(I)+1:IFO(I)>5THENO(I)=5
680 IF A$="," THEN O(I)=O(I)-1:IFO(I)<1THENO(I)=1
690 RETURN
700 REM MP=6
710 IF A$="." THEN V(I)=V(I)+1:IFV(I)>31THENV(I)=31
720 IF A$="," THEN V(I)=V(I)-1:IFV(I)<1THENV(I)=1
730 IF A$=">" THEN V(I)=V(I)+10:IFV(I)>31THENV(I)=31
740 IF A$="<" THEN V(I)=V(I)-10:IFV(I)<1THENV(I)=1
750 RETURN
760 REM RANDOM NOTES
770 LE=RND(16)
780 S$(I)=""
790 FORRL=1TOLE:S$(I)=S$(I)+CHR$(64+RND(7)):NEXT
800 RETURN
810 REM ENTER MANUALLY
820 ?@0,"";:INPUTUS$:S$(I)=US$
830 RETURN

840 REM BUILD SOUND STRING
850 PS$="O"+MID$(STR$(O(I)),2)+";V"+MID$(STR$(V(I)),2)+";T"+MID$(STR$(T(I)),2)+";"
860 FORD=1TOR(I):PS$=PS$+S$(I):NEXT
870 RETURN
880 REM MENU UPDATE
890 ?@-1+PO+(MP*32),CHR$(42);
900 IF OP<>MP THEN?@-1+PO+(OP*32),CHR$(143);
910 RETURN

920 REM SOUND BOARD
930 CLS
940 PRINT"SOUND BOARD":PRINT
950 PRINT"PRESS 1 TO 9 TO PLAY SOUNDS"
960 PRINT"PRESS 0 FOR SOUND 10"
970 PRINT"PRESS Q TO RETURN TO MAIN MENU"
980 A$=INKEY$:IFA$=""THEN980
990 IF A$="Q" THEN 1080
1000 I=ASC(A$)-48:IFI=0THENI=10
1010 IFI>10THEN980
1020 GOSUB850
1030 ?@194,"sound"I
1040 ?@256,STRING$(255," ");
1050 ?@256,PS$
1060 PLAYPS$
1070 GOTO980
1080 RETURN

1090 REM PRINT
1100 CLS
1110 PRINT"PRINTING"
1120 FORI=1TO10
1130 GOSUB850
1140 ?#-2,"SOUND"+STR$(I)
1150 ?#-2,PS$
1160 ?#-2,""
1170 NEXT
1180 RETURN

1190 REM ------LIBRARY LIVES HERE------

1200 REM TITLE SCREEN
1210 REM TT$ = PROGRAM NAME
1220 REM TBY$ = AUTHOR
1230 REM TD$ = DATE
1240 REM TV$ = VERSION
1250 TM=LEN(TT$):IF LEN(TBY$)>TM THEN TM=LEN(TBY$)
1260 IF LEN(TV$)>TM THEN TM=LEN(TV$)
1270 IF LEN(TD$)>TM THEN TM=LEN(TD$)
1280 BH=7:BW=TM+2:BY=3:BX=(30-BW)/2+1:BC$=CHR$(143):GOSUB1680
1290 BH=9:BW=TM+4:BY=1:BX=(32-BW)/2:BC$=TC$:GOSUB1560
1300 ?@(BY+1)*32 + ((32-LEN(TT$))/2),TT$;
1310 ?@(BY+3)*32 + ((32-LEN(TBY$))/2),TBY$;
1320 ?@(BY+5)*32 + ((32-LEN(TD$))/2),TD$;
1330 ?@(BY+7)*32 + ((32-LEN(TV$))/2),TV$;
1340 RETURN

1350 REM VERTICAL LINE
1360 REM LC$ = CHARACTER FOR LINE
1370 REM LX,LY = POSITION 
1380 REM LL = LENGTH
1390 LZ=LY+LL-1
1400 FOR BR=LY TO LZ:?@LX+(BR*32),LC$;:NEXT
1410 RETURN

1420 REM HORIZONTAL LINE
1430 REM LC$ = CHARACTER FOR LINE
1440 REM LX,LY = POSITION 
1450 REM LL = LENGTH
1460 LO$=""
1470 REM MC10: FOR BR=1TOLL:LO$=LO$+LC$:NEXT
1480 REM COCO/DRAGON: LO$=STRING$(LL,LC$)
1490 LO$=STRING$(LL,LC$)
1500 ?@LX+(LY*32),LO$;
1510 RETURN

1520 REM BOX OUTLINE
1530 REM BX,BY = BOX POSITION
1540 REM BH,BW = BOX HEIGHT & WIDTH
1550 REM BC$ = CHARACTER FOR BOX EDGE
1560 REM MC10: BR$="":FOR BR=1TOBW:BR$=BR$+BC$:NEXT
1570 REM COCO/DRAGON: BR$=STRING$(BW,BC$)
1580 BR$=STRING$(BW,BC$)
1590 ?@BX+((BY+1)*32),BR$;
1600 ?@BX+((BY+BH)*32),BR$;
1610 BH=BH-1:BY=BY+1:BW=BW-1
1620 FOR BQ=1TOBH:?@BX+((BY+BQ)*32),BC$;:?@BX+BW+((BY+BQ)*32),BC$;:NEXT
1630 RETURN

1640 REM FILLED BOX
1650 REM BX,BY = BOX POSITION
1660 REM BH,BW = BOX HEIGHT & WIDTH
1670 REM BC$ = CHARACTER FOR BOX
1680 REM MC10: BR$="":FOR BR=1TOBW:BR$=BR$+BC$:NEXT
1690 REM COCO/DRAGON: BR$=STRING$(BW,BC$)
1700 BR$=STRING$(BW,BC$)
1710 FOR BQ=1TOBH:?@BX+((BY-1+BQ)*32),BR$;:NEXT
1720 RETURN

1730 REM CENTER TEXT. TL = vertical line number.
1740 PRINT@(32-LEN(T$))/2+32*TL,T$;
1750 RETURN

1760 REM DELAY
1770 FORDZ=1TO1000:NEXT:RETURN

1780 REM WAIT FOR SPACEBAR
1790 BC$=CHR$(128):?@483,"press"+BC$+"space"+BC$+"bar"+BC$+"to"+BC$+"continue";:
1800 WA$=INKEY$:IFWA$<>" "THEN1800ELSE RETURN

1810 REM WAIT FOR ANY KEY
1820 WA$=INKEY$:IF WA$="" THEN 1820
1830 RETURN

1840 REM ANY KEY PROMPT AT BOTTOM OF SCREEN.
1850 ?@483,"PRESS ANY KEY TO CONTINUE";:GOSUB1820:RETURN

1860 REM INVERTED ANY KEY PROMPT AT BOTTOM OF SCREEN.
1870 BC$=CHR$(128):?@483,"press"+BC$+"any"+BC$+"key"+BC$+"to"+BC$+"continue";:GOSUB1820:RETURN

1880 REM LOWER CASE TEXT
1890 TL=LEN(T$):O$=""
1900 FORBR=1TOTL:C$=MID$(T$,BR,1):BH=ASC(C$):IF BH<91 AND BH>64 THEN C$=CHR$(BH+32)
1910 IFBH=32THENC$=CHR$(128)
1920 O$=O$+C$
1930 NEXT
1940 T$=O$
1950 RETURN
