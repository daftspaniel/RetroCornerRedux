10 CLEAR4000:DIM BX,BY,BH,BW,BQ,BC$,T$,TT$,BR,LX,LC$,LZ,LY,LL,TL,LO$
20 REM DATA STRUCTURE
30 I=1 : REM INDEX
40 DIMS$(10): REM STRINGS
50 DIMT(10): REM TEMPOS
60 DIMR(10): REM REPEATS
70 DIMO(10): REM OCTAVES
80 DIMV(10): REM VOLUMES
90 REM --------------
100 MP=1:OP=0: REM MENU POSITION
110 FORD=1TO10:S$(D)="ABC":T(D)=100:R(D)=1:O(D)=2:V(D)=15:NEXT
120 CLS0:TT$="ECHO-CO SFX TOOL":TBY$="BY DAVY MITCHELL":TD$="AUGUST 2024"
130 TV$="V0.1 -  A SEPTANDY PROJECT":TC$=CHR$(191):GOSUB 890
140 GOSUB1430:CLS
150 REM BX=0:BY=0:BW=32:BH=5:BC$=CHR$(207):GOSUB1200
160 S$(I)="ABC":T=T(I)
170 S$(2)="ABCD"
180 S$(3)="ABCDE"
190 R(3)=3
200 GOSUB760:GOSUB330
210 GOSUB790
220 REM INTERACT
230 A$=INKEY$:IFA$=""THEN230
240 IF A$<>"A" AND A$<>"Q" THEN 280
250 IFA$="Q"THENOP=MP:MP=MP-1:IFMP<1THENMP=1
260 IFA$="A"THENOP=MP:MP=MP+1:IFMP>5THENMP=6
270 GOSUB800:GOTO230
280 ON MP GOSUB 430,470,500,560,600,640
290 IF A$="P" THEN PLAYPS$:GOTO230
300 GOSUB760:REM BUILD PLAY STRING
310 GOSUB330:REM UPDATE SCREEN
320 GOTO 230:REM FURTHER INPUT
330 REM UPDATE SCREEN
340 ?@97,STRING$(30,CHR$(143));:T$=PS$:TL=3:GOSUB1380
350 PO=226
360 ?@PO+32,"sound     ";:?@PO+32+9,STR$(I)+" ";
370 ?@PO+32*2,"string                     ";:?@PO+32*2+10,S$(I)+" ";
380 ?@PO+32*3,"tempo":?@PO+32*3+9,STR$(T(I))+" ";
390 ?@PO+32*4,"repeat   ";:?@PO+32*4+9,STR$(R(I))+" ";
400 ?@PO+32*5,"octave   ";:?@PO+32*5+9,STR$(O(I))+" ";
410 ?@PO+32*6,"volume   ";:?@PO+32*6+9,STR$(V(I))+" ";
420 RETURN
430 REM MP=1
440 IF A$="." THEN I=I+1:IFI>10THENI=10
450 IF A$="," THEN I=I-1:IFI<1THENI=1
460 RETURN
470 REM MP=2
480 IF A$="R" THEN GOSUB 700
490 RETURN
500 REM MP=3
510 IF A$="." THEN T(I)=T(I)+1:IFT(I)>255THENT(I)=255
520 IF A$="," THEN T(I)=T(I)-1:IFT(I)<1THENT(I)=1
530 IF A$=">" THEN T(I)=T(I)+10:IFT(I)>255THENT(I)=255
540 IF A$="<" THEN T(I)=T(I)-10:IFT(I)<1THENT(I)=1
550 RETURN
560 REM MP=4
570 IF A$="." THEN R(I)=R(I)+1:IFR(I)>5THENR(I)=5
580 IF A$="," THEN R(I)=R(I)-1:IFR(I)<1THENR(I)=1
590 RETURN
600 REM MP=5
610 IF A$="." THEN O(I)=O(I)+1:IFO(I)>5THENO(I)=5
620 IF A$="," THEN O(I)=O(I)-1:IFO(I)<1THENO(I)=1
630 RETURN
640 REM MP=6
650 IF A$="." THEN V(I)=V(I)+1:IFV(I)>31THENV(I)=31
660 IF A$="," THEN V(I)=V(I)-1:IFV(I)<1THENV(I)=1
670 IF A$=">" THEN V(I)=V(I)+10:IFV(I)>31THENV(I)=31
680 IF A$="<" THEN V(I)=V(I)-10:IFV(I)<1THENV(I)=1
690 RETURN
700 REM RANDOM NOTES
710 LE=RND(16)
720 S$(I)=""
730 FORRL=1TOLE:S$(I)=S$(I)+CHR$(64+RND(7)):NEXT
740 RETURN
750 REM BUILD SOUND STRING
760 PS$="O"+MID$(STR$(O(I)),2)+";V"+MID$(STR$(V(I)),2)+";T"+MID$(STR$(T(I)),2)+";"
770 FORD=1TOR(I):PS$=PS$+S$(I):NEXT
780 RETURN
790 REM MENU UPDATE
800 ?@-1+PO+(MP*32),CHR$(42);
810 IF OP<>MP THEN?@-1+PO+(OP*32),CHR$(143);
820 RETURN
830 REM ------LIBRARY LIVES HERE------

840 REM TITLE SCREEN
850 REM TT$ = PROGRAM NAME
860 REM TBY$ = AUTHOR
870 REM TD$ = DATE
880 REM TV$ = VERSION
890 TM=LEN(TT$):IF LEN(TBY$)>TM THEN TM=LEN(TBY$)
900 IF LEN(TV$)>TM THEN TM=LEN(TV$)
910 IF LEN(TD$)>TM THEN TM=LEN(TD$)
920 BH=7:BW=TM+2:BY=3:BX=(30-BW)/2+1:BC$=CHR$(143):GOSUB1320
930 BH=9:BW=TM+4:BY=1:BX=(32-BW)/2:BC$=TC$:GOSUB1200
940 ?@(BY+1)*32 + ((32-LEN(TT$))/2),TT$;
950 ?@(BY+3)*32 + ((32-LEN(TBY$))/2),TBY$;
960 ?@(BY+5)*32 + ((32-LEN(TD$))/2),TD$;
970 ?@(BY+7)*32 + ((32-LEN(TV$))/2),TV$;
980 RETURN

990 REM VERTICAL LINE
1000 REM LC$ = CHARACTER FOR LINE
1010 REM LX,LY = POSITION 
1020 REM LL = LENGTH
1030 LZ=LY+LL-1
1040 FOR BR=LY TO LZ:?@LX+(BR*32),LC$;:NEXT
1050 RETURN

1060 REM HORIZONTAL LINE
1070 REM LC$ = CHARACTER FOR LINE
1080 REM LX,LY = POSITION 
1090 REM LL = LENGTH
1100 LO$=""
1110 REM MC10: FOR BR=1TOLL:LO$=LO$+LC$:NEXT
1120 REM COCO/DRAGON: LO$=STRING$(LL,LC$)
1130 LO$=STRING$(LL,LC$)
1140 ?@LX+(LY*32),LO$;
1150 RETURN

1160 REM BOX OUTLINE
1170 REM BX,BY = BOX POSITION
1180 REM BH,BW = BOX HEIGHT & WIDTH
1190 REM BC$ = CHARACTER FOR BOX EDGE
1200 REM MC10: BR$="":FOR BR=1TOBW:BR$=BR$+BC$:NEXT
1210 REM COCO/DRAGON: BR$=STRING$(BW,BC$)
1220 BR$=STRING$(BW,BC$)
1230 ?@BX+((BY+1)*32),BR$;
1240 ?@BX+((BY+BH)*32),BR$;
1250 BH=BH-1:BY=BY+1:BW=BW-1
1260 FOR BQ=1TOBH:?@BX+((BY+BQ)*32),BC$;:?@BX+BW+((BY+BQ)*32),BC$;:NEXT
1270 RETURN

1280 REM FILLED BOX
1290 REM BX,BY = BOX POSITION
1300 REM BH,BW = BOX HEIGHT & WIDTH
1310 REM BC$ = CHARACTER FOR BOX
1320 REM MC10: BR$="":FOR BR=1TOBW:BR$=BR$+BC$:NEXT
1330 REM COCO/DRAGON: BR$=STRING$(BW,BC$)
1340 BR$=STRING$(BW,BC$)
1350 FOR BQ=1TOBH:?@BX+((BY-1+BQ)*32),BR$;:NEXT
1360 RETURN

1370 REM CENTER TEXT. TL = vertical line number.
1380 PRINT@(32-LEN(T$))/2+32*TL,T$;
1390 RETURN

1400 REM DELAY
1410 FORDZ=1TO1000:NEXT:RETURN

1420 REM WAIT FOR SPACEBAR
1430 BC$=CHR$(128):?@483,"press"+BC$+"space"+BC$+"bar"+BC$+"to"+BC$+"continue";:
1440 WA$=INKEY$:IFWA$<>" "THEN1440ELSE RETURN

1450 REM WAIT FOR ANY KEY
1460 WA$=INKEY$:IF WA$="" THEN 1460
1470 RETURN

1480 REM ANY KEY PROMPT AT BOTTOM OF SCREEN.
1490 ?@483,"PRESS ANY KEY TO CONTINUE";:GOSUB1460:RETURN

1500 REM INVERTED ANY KEY PROMPT AT BOTTOM OF SCREEN.
1510 BC$=CHR$(128):?@483,"press"+BC$+"any"+BC$+"key"+BC$+"to"+BC$+"continue";:GOSUB1460:RETURN

1520 REM LOWER CASE TEXT
1530 TL=LEN(T$):O$=""
1540 FORBR=1TOTL:C$=MID$(T$,BR,1):BH=ASC(C$):IF BH<91 AND BH>64 THEN C$=CHR$(BH+32)
1550 IFBH=32THENC$=CHR$(128)
1560 O$=O$+C$
1570 NEXT
1580 T$=O$
1590 RETURN
