10 CLEAR4000:DIM BX,BY,BH,BW,BQ,BC$,T$,TT$,BR,LX,LC$,LZ,LY,LL,TL,LO$
20 REM DATA STRUCTURE
30 I=1 : REM INDEX
40 DIMS$(10): REM STRINGS
50 DIMT(10): REM TEMPOS
60 DIMR(10): REM REPEATS
70 DIMO(10): REM OCTAVES
80 DIMV(10): REM VOLUMES
90 REM --------------
100 MP=1:OP=0: REM MENU POSITION
110 FORD=1TO10:S$(D)="ABC":T(D)=100:R(D)=1:O(D)=2:V(D)=15:NEXT
120 CLS0:TT$="ECHO-CO SFX TOOL":TBY$="BY DAVY MITCHELL":TD$="AUGUST 2024"
130 TV$="V0.1 -  A SEPTANDY PROJECT":TC$=CHR$(191):GOSUB 930
140 GOSUB1470:CLS
150 REM BX=0:BY=0:BW=32:BH=5:BC$=CHR$(207):GOSUB1240
160 S$(I)="ABC":T=T(I)
170 S$(2)="ABCD"
180 S$(3)="ABCDE"
190 R(3)=3
200 GOSUB800:GOSUB330
210 GOSUB830
220 REM INTERACT
230 A$=INKEY$:IFA$=""THEN230
240 IF A$<>"A" AND A$<>"Q" THEN 280
250 IFA$="Q"THENOP=MP:MP=MP-1:IFMP<1THENMP=1
260 IFA$="A"THENOP=MP:MP=MP+1:IFMP>5THENMP=6
270 GOSUB840:GOTO230
280 ON MP GOSUB 430,470,510,570,610,650
290 IF A$="P" THEN PLAYPS$:GOTO230
300 GOSUB800:REM BUILD PLAY STRING
310 GOSUB330:REM UPDATE SCREEN
320 GOTO 230:REM FURTHER INPUT
330 REM UPDATE SCREEN
340 ?@0,STRING$(255," ");:T$=PS$:TL=3:GOSUB1420
350 PO=226
360 ?@PO+32,"sound     ";:?@PO+32+9,STR$(I)+" ";
370 ?@PO+32*2,"string                     ";:?@PO+32*2+10,S$(I)+" ";
380 ?@PO+32*3,"tempo":?@PO+32*3+9,STR$(T(I))+" ";
390 ?@PO+32*4,"repeat   ";:?@PO+32*4+9,STR$(R(I))+" ";
400 ?@PO+32*5,"octave   ";:?@PO+32*5+9,STR$(O(I))+" ";
410 ?@PO+32*6,"volume   ";:?@PO+32*6+9,STR$(V(I))+" ";
420 RETURN
430 REM MP=1
440 IF A$="." THEN I=I+1:IFI>10THENI=10
450 IF A$="," THEN I=I-1:IFI<1THENI=1
460 RETURN
470 REM MP=2
480 IF A$="R" THEN GOSUB 710
490 IF A$="E" THEN GOSUB 760
500 RETURN
510 REM MP=3
520 IF A$="." THEN T(I)=T(I)+1:IFT(I)>255THENT(I)=255
530 IF A$="," THEN T(I)=T(I)-1:IFT(I)<1THENT(I)=1
540 IF A$=">" THEN T(I)=T(I)+10:IFT(I)>255THENT(I)=255
550 IF A$="<" THEN T(I)=T(I)-10:IFT(I)<1THENT(I)=1
560 RETURN
570 REM MP=4
580 IF A$="." THEN R(I)=R(I)+1:IFR(I)>5THENR(I)=5
590 IF A$="," THEN R(I)=R(I)-1:IFR(I)<1THENR(I)=1
600 RETURN
610 REM MP=5
620 IF A$="." THEN O(I)=O(I)+1:IFO(I)>5THENO(I)=5
630 IF A$="," THEN O(I)=O(I)-1:IFO(I)<1THENO(I)=1
640 RETURN
650 REM MP=6
660 IF A$="." THEN V(I)=V(I)+1:IFV(I)>31THENV(I)=31
670 IF A$="," THEN V(I)=V(I)-1:IFV(I)<1THENV(I)=1
680 IF A$=">" THEN V(I)=V(I)+10:IFV(I)>31THENV(I)=31
690 IF A$="<" THEN V(I)=V(I)-10:IFV(I)<1THENV(I)=1
700 RETURN
710 REM RANDOM NOTES
720 LE=RND(16)
730 S$(I)=""
740 FORRL=1TOLE:S$(I)=S$(I)+CHR$(64+RND(7)):NEXT
750 RETURN
760 REM ENTER
770 ?@0,"";:INPUTUS$:S$(I)=US$
780 RETURN
790 REM BUILD SOUND STRING
800 PS$="O"+MID$(STR$(O(I)),2)+";V"+MID$(STR$(V(I)),2)+";T"+MID$(STR$(T(I)),2)+";"
810 FORD=1TOR(I):PS$=PS$+S$(I):NEXT
820 RETURN
830 REM MENU UPDATE
840 ?@-1+PO+(MP*32),CHR$(42);
850 IF OP<>MP THEN?@-1+PO+(OP*32),CHR$(143);
860 RETURN
870 REM ------LIBRARY LIVES HERE------

880 REM TITLE SCREEN
890 REM TT$ = PROGRAM NAME
900 REM TBY$ = AUTHOR
910 REM TD$ = DATE
920 REM TV$ = VERSION
930 TM=LEN(TT$):IF LEN(TBY$)>TM THEN TM=LEN(TBY$)
940 IF LEN(TV$)>TM THEN TM=LEN(TV$)
950 IF LEN(TD$)>TM THEN TM=LEN(TD$)
960 BH=7:BW=TM+2:BY=3:BX=(30-BW)/2+1:BC$=CHR$(143):GOSUB1360
970 BH=9:BW=TM+4:BY=1:BX=(32-BW)/2:BC$=TC$:GOSUB1240
980 ?@(BY+1)*32 + ((32-LEN(TT$))/2),TT$;
990 ?@(BY+3)*32 + ((32-LEN(TBY$))/2),TBY$;
1000 ?@(BY+5)*32 + ((32-LEN(TD$))/2),TD$;
1010 ?@(BY+7)*32 + ((32-LEN(TV$))/2),TV$;
1020 RETURN

1030 REM VERTICAL LINE
1040 REM LC$ = CHARACTER FOR LINE
1050 REM LX,LY = POSITION 
1060 REM LL = LENGTH
1070 LZ=LY+LL-1
1080 FOR BR=LY TO LZ:?@LX+(BR*32),LC$;:NEXT
1090 RETURN

1100 REM HORIZONTAL LINE
1110 REM LC$ = CHARACTER FOR LINE
1120 REM LX,LY = POSITION 
1130 REM LL = LENGTH
1140 LO$=""
1150 REM MC10: FOR BR=1TOLL:LO$=LO$+LC$:NEXT
1160 REM COCO/DRAGON: LO$=STRING$(LL,LC$)
1170 LO$=STRING$(LL,LC$)
1180 ?@LX+(LY*32),LO$;
1190 RETURN

1200 REM BOX OUTLINE
1210 REM BX,BY = BOX POSITION
1220 REM BH,BW = BOX HEIGHT & WIDTH
1230 REM BC$ = CHARACTER FOR BOX EDGE
1240 REM MC10: BR$="":FOR BR=1TOBW:BR$=BR$+BC$:NEXT
1250 REM COCO/DRAGON: BR$=STRING$(BW,BC$)
1260 BR$=STRING$(BW,BC$)
1270 ?@BX+((BY+1)*32),BR$;
1280 ?@BX+((BY+BH)*32),BR$;
1290 BH=BH-1:BY=BY+1:BW=BW-1
1300 FOR BQ=1TOBH:?@BX+((BY+BQ)*32),BC$;:?@BX+BW+((BY+BQ)*32),BC$;:NEXT
1310 RETURN

1320 REM FILLED BOX
1330 REM BX,BY = BOX POSITION
1340 REM BH,BW = BOX HEIGHT & WIDTH
1350 REM BC$ = CHARACTER FOR BOX
1360 REM MC10: BR$="":FOR BR=1TOBW:BR$=BR$+BC$:NEXT
1370 REM COCO/DRAGON: BR$=STRING$(BW,BC$)
1380 BR$=STRING$(BW,BC$)
1390 FOR BQ=1TOBH:?@BX+((BY-1+BQ)*32),BR$;:NEXT
1400 RETURN

1410 REM CENTER TEXT. TL = vertical line number.
1420 PRINT@(32-LEN(T$))/2+32*TL,T$;
1430 RETURN

1440 REM DELAY
1450 FORDZ=1TO1000:NEXT:RETURN

1460 REM WAIT FOR SPACEBAR
1470 BC$=CHR$(128):?@483,"press"+BC$+"space"+BC$+"bar"+BC$+"to"+BC$+"continue";:
1480 WA$=INKEY$:IFWA$<>" "THEN1480ELSE RETURN

1490 REM WAIT FOR ANY KEY
1500 WA$=INKEY$:IF WA$="" THEN 1500
1510 RETURN

1520 REM ANY KEY PROMPT AT BOTTOM OF SCREEN.
1530 ?@483,"PRESS ANY KEY TO CONTINUE";:GOSUB1500:RETURN

1540 REM INVERTED ANY KEY PROMPT AT BOTTOM OF SCREEN.
1550 BC$=CHR$(128):?@483,"press"+BC$+"any"+BC$+"key"+BC$+"to"+BC$+"continue";:GOSUB1500:RETURN

1560 REM LOWER CASE TEXT
1570 TL=LEN(T$):O$=""
1580 FORBR=1TOTL:C$=MID$(T$,BR,1):BH=ASC(C$):IF BH<91 AND BH>64 THEN C$=CHR$(BH+32)
1590 IFBH=32THENC$=CHR$(128)
1600 O$=O$+C$
1610 NEXT
1620 T$=O$
1630 RETURN
