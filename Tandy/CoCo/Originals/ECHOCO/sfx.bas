10 CLEAR4000:DIM BX,BY,BH,BW,BQ,BC$,T$,TT$,BR,LX,LC$,LZ,LY,LL,TL,LO$
20 REM DATA STRUCTURE
30 I=1 : REM INDEX
40 DIMS$(10): REM STRINGS
50 DIMT(10): REM TEMPOS
60 DIMR(10): REM REPEATS
70 DIMO(10): REM OCTAVES
80 DIMV(10): REM VOLUMES
90 REM --------------
100 MP=1:OP=0: REM MENU POSITION
110 FORD=1TO10:S$(D)="ABC":T(D)=10:R(D)=1:O(D)=2:V(D)=15:NEXT
120 CLS0:TT$="ECHO-CO SFX TOOL":TBY$="BY DAVY MITCHELL":TD$="SEPTEMBER 2024"
130 TV$="V0.2 -  A SEPTANDY PROJECT":TC$=CHR$(191):GOSUB 1220
140 GOSUB1760:CLS
150 PRINT"1. SOUND EDITOR"
160 PRINT"2. SOUND BOARD"
170 PRINT"3. SEND SOUNDS TO PRINTER"
180 INPUT A
190 ON A GOSUB 210,900,1070
200 CLS:GOTO150
210 CLS:GOSUB820:GOSUB350
220 GOSUB850
230 REM INTERACT
240 A$=INKEY$:IFA$=""THEN240
250 IFA$<>"A" AND A$<>"Q" THEN 290
260 IFA$="Q"THENOP=MP:MP=MP-1:IFMP<1THENMP=1
270 IFA$="A"THENOP=MP:MP=MP+1:IFMP>6THENMP=6
280 GOSUB860:GOTO240
290 ON MP GOSUB 450,490,530,590,630,670
300 IFA$="P" THEN PLAYPS$:GOTO240
310 IFA$="M" THEN RETURN
320 GOSUB820:REM BUILD PLAY STRING
330 GOSUB350:REM UPDATE SCREEN
340 GOTO 240:REM FURTHER INPUT
350 REM UPDATE SCREEN
360 ?@0,STRING$(255," ");:T$=PS$:TL=3:GOSUB1710
370 PO=226
380 ?@PO+32,"sound     ";:?@PO+32+9,STR$(I)+" ";
390 ?@PO+32*2,"string                     ";
305 ?@PO+32*2+10,S$(I)+" ";
400 ?@PO+32*3,"tempo":?@PO+32*3+9,STR$(T(I))+" ";
410 ?@PO+32*4,"repeat   ";:?@PO+32*4+9,STR$(R(I))+" ";
420 ?@PO+32*5,"octave   ";:?@PO+32*5+9,STR$(O(I))+" ";
430 ?@PO+32*6,"volume   ";:?@PO+32*6+9,STR$(V(I))+" ";
440 RETURN
450 REM MP=1
460 IF A$="." THEN I=I+1:IFI>10THENI=10
470 IF A$="," THEN I=I-1:IFI<1THENI=1
480 RETURN
490 REM MP=2
500 IF A$="R" THEN GOSUB 730
510 IF A$="E" THEN GOSUB 780
520 RETURN
530 REM MP=3
540 IF A$="." THEN T(I)=T(I)+1:IFT(I)>255THENT(I)=255
550 IF A$="," THEN T(I)=T(I)-1:IFT(I)<1THENT(I)=1
560 IF A$=">" THEN T(I)=T(I)+10:IFT(I)>255THENT(I)=255
570 IF A$="<" THEN T(I)=T(I)-10:IFT(I)<1THENT(I)=1
580 RETURN
590 REM MP=4
600 IF A$="." THEN R(I)=R(I)+1:IFR(I)>5THENR(I)=5
610 IF A$="," THEN R(I)=R(I)-1:IFR(I)<1THENR(I)=1
620 RETURN
630 REM MP=5
640 IF A$="." THEN O(I)=O(I)+1:IFO(I)>5THENO(I)=5
650 IF A$="," THEN O(I)=O(I)-1:IFO(I)<1THENO(I)=1
660 RETURN
670 REM MP=6
680 IF A$="." THEN V(I)=V(I)+1:IFV(I)>31THENV(I)=31
690 IF A$="," THEN V(I)=V(I)-1:IFV(I)<1THENV(I)=1
700 IF A$=">" THEN V(I)=V(I)+10:IFV(I)>31THENV(I)=31
710 IF A$="<" THEN V(I)=V(I)-10:IFV(I)<1THENV(I)=1
720 RETURN
730 REM RANDOM NOTES
740 LE=RND(16)
750 S$(I)=""
760 FORRL=1TOLE:S$(I)=S$(I)+CHR$(64+RND(7)):NEXT
770 RETURN
780 REM ENTER MANUALLY
790 ?@0,"";:INPUTUS$:S$(I)=US$
800 RETURN

810 REM BUILD SOUND STRING
820 PS$="O"+MID$(STR$(O(I)),2)+";V"+MID$(STR$(V(I)),2)+";T"+MID$(STR$(T(I)),2)+";"
830 FORD=1TOR(I):PS$=PS$+S$(I):NEXT
840 RETURN
850 REM MENU UPDATE
860 ?@-1+PO+(MP*32),CHR$(42);
870 IF OP<>MP THEN?@-1+PO+(OP*32),CHR$(143);
880 RETURN

890 REM SOUND BOARD
900 CLS
910 PRINT"SOUND BOARD":PRINT
920 PRINT"PRESS 1 TO 9 TO PLAY SOUNDS"
930 PRINT"PRESS 0 FOR SOUND 10"
940 PRINT"PRESS Q TO RETURN TO MAIN MENU"
950 A$=INKEY$:IFA$=""THEN950
960 IF A$="Q" THEN 1050
970 I=ASC(A$)-48:IFI=0THENI=10
980 IFI>10THEN950
990 GOSUB820
1000 ?@194,"sound"I
1010 ?@256,STRING$(255," ");
1020 ?@256,PS$
1030 PLAYPS$
1040 GOTO950
1050 RETURN

1060 REM PRINT
1070 CLS
1080 PRINT"PRINTING"
1090 FORI=1TO10
1100 GOSUB820
1110 ?#-2,"SOUND"+STR$(I)
1120 ?#-2,PS$
1130 ?#-2,""
1140 NEXT
1150 RETURN

1160 REM ------LIBRARY LIVES HERE------

1170 REM TITLE SCREEN
1180 REM TT$ = PROGRAM NAME
1190 REM TBY$ = AUTHOR
1200 REM TD$ = DATE
1210 REM TV$ = VERSION
1220 TM=LEN(TT$):IF LEN(TBY$)>TM THEN TM=LEN(TBY$)
1230 IF LEN(TV$)>TM THEN TM=LEN(TV$)
1240 IF LEN(TD$)>TM THEN TM=LEN(TD$)
1250 BH=7:BW=TM+2:BY=3:BX=(30-BW)/2+1:BC$=CHR$(143):GOSUB1650
1260 BH=9:BW=TM+4:BY=1:BX=(32-BW)/2:BC$=TC$:GOSUB1530
1270 ?@(BY+1)*32 + ((32-LEN(TT$))/2),TT$;
1280 ?@(BY+3)*32 + ((32-LEN(TBY$))/2),TBY$;
1290 ?@(BY+5)*32 + ((32-LEN(TD$))/2),TD$;
1300 ?@(BY+7)*32 + ((32-LEN(TV$))/2),TV$;
1310 RETURN

1320 REM VERTICAL LINE
1330 REM LC$ = CHARACTER FOR LINE
1340 REM LX,LY = POSITION 
1350 REM LL = LENGTH
1360 LZ=LY+LL-1
1370 FOR BR=LY TO LZ:?@LX+(BR*32),LC$;:NEXT
1380 RETURN

1390 REM HORIZONTAL LINE
1400 REM LC$ = CHARACTER FOR LINE
1410 REM LX,LY = POSITION 
1420 REM LL = LENGTH
1430 LO$=""
1440 REM MC10: FOR BR=1TOLL:LO$=LO$+LC$:NEXT
1450 REM COCO/DRAGON: LO$=STRING$(LL,LC$)
1460 LO$=STRING$(LL,LC$)
1470 ?@LX+(LY*32),LO$;
1480 RETURN

1490 REM BOX OUTLINE
1500 REM BX,BY = BOX POSITION
1510 REM BH,BW = BOX HEIGHT & WIDTH
1520 REM BC$ = CHARACTER FOR BOX EDGE
1530 REM MC10: BR$="":FOR BR=1TOBW:BR$=BR$+BC$:NEXT
1540 REM COCO/DRAGON: BR$=STRING$(BW,BC$)
1550 BR$=STRING$(BW,BC$)
1560 ?@BX+((BY+1)*32),BR$;
1570 ?@BX+((BY+BH)*32),BR$;
1580 BH=BH-1:BY=BY+1:BW=BW-1
1590 FOR BQ=1TOBH:?@BX+((BY+BQ)*32),BC$;:?@BX+BW+((BY+BQ)*32),BC$;:NEXT
1600 RETURN

1610 REM FILLED BOX
1620 REM BX,BY = BOX POSITION
1630 REM BH,BW = BOX HEIGHT & WIDTH
1640 REM BC$ = CHARACTER FOR BOX
1650 REM MC10: BR$="":FOR BR=1TOBW:BR$=BR$+BC$:NEXT
1660 REM COCO/DRAGON: BR$=STRING$(BW,BC$)
1670 BR$=STRING$(BW,BC$)
1680 FOR BQ=1TOBH:?@BX+((BY-1+BQ)*32),BR$;:NEXT
1690 RETURN

1700 REM CENTER TEXT. TL = vertical line number.
1710 PRINT@(32-LEN(T$))/2+32*TL,T$;
1720 RETURN

1730 REM DELAY
1740 FORDZ=1TO1000:NEXT:RETURN

1750 REM WAIT FOR SPACEBAR
1760 BC$=CHR$(128):?@483,"press"+BC$+"space"+BC$+"bar"+BC$+"to"+BC$+"continue";:
1770 WA$=INKEY$:IFWA$<>" "THEN1770ELSE RETURN

1780 REM WAIT FOR ANY KEY
1790 WA$=INKEY$:IF WA$="" THEN 1790
1800 RETURN

1810 REM ANY KEY PROMPT AT BOTTOM OF SCREEN.
1820 ?@483,"PRESS ANY KEY TO CONTINUE";:GOSUB1790:RETURN

1830 REM INVERTED ANY KEY PROMPT AT BOTTOM OF SCREEN.
1840 BC$=CHR$(128):?@483,"press"+BC$+"any"+BC$+"key"+BC$+"to"+BC$+"continue";:GOSUB1790:RETURN

1850 REM LOWER CASE TEXT
1860 TL=LEN(T$):O$=""
1870 FORBR=1TOTL:C$=MID$(T$,BR,1):BH=ASC(C$):IF BH<91 AND BH>64 THEN C$=CHR$(BH+32)
1880 IFBH=32THENC$=CHR$(128)
1890 O$=O$+C$
1900 NEXT
1910 T$=O$
1920 RETURN
