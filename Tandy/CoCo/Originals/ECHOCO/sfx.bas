10 CLEAR4000:DIM BX,BY,BH,BW,BQ,BC$,T$,TT$,BR,LX,LC$,LZ,LY,LL,TL,LO$
20 REM DATA STRUCTURE
30 I=1 : REM INDEX
40 DIMS$(10): REM STRINGS
50 DIMT(10): REM TEMPOS
60 DIMR(10): REM REPEATS
70 DIMO(10): REM OCTAVES
80 REM --------------
90 MP=1:OP=0: REM MENU POSITION
100 FORD=1TO10:S$(D)="ABC":T(D)=100:R(D)=1:O(D)=2:NEXT
110 CLS0:TT$="ECHO-CO SFX TOOL":TBY$="BY DAVY MITCHELL":TD$="AUGUST 2024"
120 TV$="V0.1 -  A SEPTANDY PROJECT":TC$=CHR$(191):GOSUB 810
130 GOSUB1350:CLS

140 REM BX=0:BY=0:BW=32:BH=5:BC$=CHR$(207):GOSUB1120
150 S$(I)="ABC":T=T(I)
160 S$(2)="ABCD"
170 S$(3)="ABCDE"
180 R(3)=3
190 GOSUB680:GOSUB320
200 GOSUB710
210 REM INTERACT
220 A$=INKEY$:IFA$=""THEN220
230 IF A$<>"A" AND A$<>"Q" THEN 270
240 IFA$="Q"THENOP=MP:MP=MP-1:IFMP<1THENMP=1
250 IFA$="A"THENOP=MP:MP=MP+1:IFMP>5THENMP=5
260 GOSUB720:GOTO220
270 ON MP GOSUB 410,450,480,540,580
280 IF A$="P" THEN PLAYPS$:GOTO220
290 GOSUB680:REM BUILD PLAY STRING
300 GOSUB320:REM UPDATE SCREEN
310 GOTO 220:REM FURTHER INPUT
320 REM UPDATE SCREEN
330 ?@97,STRING$(30,CHR$(143));:T$=PS$:TL=3:GOSUB1300
340 PO=226
350 ?@PO+32,"sound     ";:?@PO+32+9,STR$(I)+" ";
360 ?@PO+32*2,"string                     ";:?@PO+32*2+10,S$(I);
370 ?@PO+32*3,"tempo":?@PO+32*3+9,STR$(T(I));
380 ?@PO+32*4,"repeat   ";:?@PO+32*4+9,STR$(R(I));
390 ?@PO+32*5,"octave   ";:?@PO+32*5+9,STR$(O(I));
400 RETURN
410 REM MP=1
420 IF A$="." THEN I=I+1:IFI>10THENI=10
430 IF A$="," THEN I=I-1:IFI<1THENI=1
440 RETURN
450 REM MP=2
460 IF A$="R" THEN GOSUB 620
470 RETURN
480 REM MP=3
490 IF A$="." THEN T(I)=T(I)+1:IFT(I)>255THENT(I)=255
500 IF A$="," THEN T(I)=T(I)-1:IFT(I)<1THENT(I)=1
510 IF A$=">" THEN T(I)=T(I)+10:IFT(I)>255THENT(I)=255
520 IF A$="<" THEN T(I)=T(I)-10:IFT(I)<1THENT(I)=1
530 RETURN
540 REM MP=4
550 IF A$="." THEN R(I)=R(I)+1:IFR(I)>5THENR(I)=5
560 IF A$="," THEN R(I)=R(I)-1:IFR(I)<1THENR(I)=1
570 RETURN
580 REM MP=5
590 IF A$="." THEN O(I)=O(I)+1:IFO(I)>5THENO(I)=5
600 IF A$="," THEN O(I)=O(I)-1:IFO(I)<1THENO(I)=1
610 RETURN
620 REM RANDOM NOTES
630 LE=RND(16)
640 S$(I)=""
650 FORRL=1TOLE:S$(I)=S$(I)+CHR$(64+RND(7)):NEXT
660 RETURN
670 REM BUILD SOUND STRING
680 PS$="O"+MID$(STR$(O(I)),2)+"T"+MID$(STR$(T(I)),2)+";"
690 FORD=1TOR(I):PS$=PS$+S$(I):NEXT
700 RETURN
710 REM MENU UPDATE
720 ?@-1+PO+(MP*32),CHR$(42);
730 IF OP<>MP THEN?@-1+PO+(OP*32),CHR$(143);
740 RETURN
750 REM ------LIBRARY LIVES HERE------

760 REM TITLE SCREEN
770 REM TT$ = PROGRAM NAME
780 REM TBY$ = AUTHOR
790 REM TD$ = DATE
800 REM TV$ = VERSION
810 TM=LEN(TT$):IF LEN(TBY$)>TM THEN TM=LEN(TBY$)
820 IF LEN(TV$)>TM THEN TM=LEN(TV$)
830 IF LEN(TD$)>TM THEN TM=LEN(TD$)
840 BH=7:BW=TM+2:BY=3:BX=(30-BW)/2+1:BC$=CHR$(143):GOSUB1240
850 BH=9:BW=TM+4:BY=1:BX=(32-BW)/2:BC$=TC$:GOSUB1120
860 ?@(BY+1)*32 + ((32-LEN(TT$))/2),TT$;
870 ?@(BY+3)*32 + ((32-LEN(TBY$))/2),TBY$;
880 ?@(BY+5)*32 + ((32-LEN(TD$))/2),TD$;
890 ?@(BY+7)*32 + ((32-LEN(TV$))/2),TV$;
900 RETURN

910 REM VERTICAL LINE
920 REM LC$ = CHARACTER FOR LINE
930 REM LX,LY = POSITION 
940 REM LL = LENGTH
950 LZ=LY+LL-1
960 FOR BR=LY TO LZ:?@LX+(BR*32),LC$;:NEXT
970 RETURN

980 REM HORIZONTAL LINE
990 REM LC$ = CHARACTER FOR LINE
1000 REM LX,LY = POSITION 
1010 REM LL = LENGTH
1020 LO$=""
1030 REM MC10: FOR BR=1TOLL:LO$=LO$+LC$:NEXT
1040 REM COCO/DRAGON: LO$=STRING$(LL,LC$)
1050 LO$=STRING$(LL,LC$)
1060 ?@LX+(LY*32),LO$;
1070 RETURN

1080 REM BOX OUTLINE
1090 REM BX,BY = BOX POSITION
1100 REM BH,BW = BOX HEIGHT & WIDTH
1110 REM BC$ = CHARACTER FOR BOX EDGE
1120 REM MC10: BR$="":FOR BR=1TOBW:BR$=BR$+BC$:NEXT
1130 REM COCO/DRAGON: BR$=STRING$(BW,BC$)
1140 BR$=STRING$(BW,BC$)
1150 ?@BX+((BY+1)*32),BR$;
1160 ?@BX+((BY+BH)*32),BR$;
1170 BH=BH-1:BY=BY+1:BW=BW-1
1180 FOR BQ=1TOBH:?@BX+((BY+BQ)*32),BC$;:?@BX+BW+((BY+BQ)*32),BC$;:NEXT
1190 RETURN

1200 REM FILLED BOX
1210 REM BX,BY = BOX POSITION
1220 REM BH,BW = BOX HEIGHT & WIDTH
1230 REM BC$ = CHARACTER FOR BOX
1240 REM MC10: BR$="":FOR BR=1TOBW:BR$=BR$+BC$:NEXT
1250 REM COCO/DRAGON: BR$=STRING$(BW,BC$)
1260 BR$=STRING$(BW,BC$)
1270 FOR BQ=1TOBH:?@BX+((BY-1+BQ)*32),BR$;:NEXT
1280 RETURN

1290 REM CENTER TEXT. TL = vertical line number.
1300 PRINT@(32-LEN(T$))/2+32*TL,T$;
1310 RETURN

1320 REM DELAY
1330 FORDZ=1TO1000:NEXT:RETURN

1340 REM WAIT FOR SPACEBAR
1350 BC$=CHR$(128):?@483,"press"+BC$+"space"+BC$+"bar"+BC$+"to"+BC$+"continue";:
1360 WA$=INKEY$:IFWA$<>" "THEN1360ELSE RETURN

1370 REM WAIT FOR ANY KEY
1380 WA$=INKEY$:IF WA$="" THEN 1380
1390 RETURN

1400 REM ANY KEY PROMPT AT BOTTOM OF SCREEN.
1410 ?@483,"PRESS ANY KEY TO CONTINUE";:GOSUB1380:RETURN

1420 REM INVERTED ANY KEY PROMPT AT BOTTOM OF SCREEN.
1430 BC$=CHR$(128):?@483,"press"+BC$+"any"+BC$+"key"+BC$+"to"+BC$+"continue";:GOSUB1380:RETURN

1440 REM LOWER CASE TEXT
1450 TL=LEN(T$):O$=""
1460 FORBR=1TOTL:C$=MID$(T$,BR,1):BH=ASC(C$):IF BH<91 AND BH>64 THEN C$=CHR$(BH+32)
1470 IFBH=32THENC$=CHR$(128)
1480 O$=O$+C$
1490 NEXT
1500 T$=O$
1510 RETURN
